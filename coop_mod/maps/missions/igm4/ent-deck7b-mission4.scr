//-----------------------------------------------------------------
//
//  EF2 Level Script File
//
//  Level:	      ent-deck7b - MISSION 4
//  Script By:    K.Thompson
//  Created on:   08/12/2002
//	Last Edited:  08/12/2002 - kt
//-----------------------------------------------------------------

void setupMission4Deck7b();

void cinematicDrullShuttleLeavesIGM4();
void cinematicDrullShuttleLeavesIGM4Skipthread();
void cinematicDrullShuttleLeavesIGM4Scene2();
void cinematicDrullShuttleLeavesIGM4End();

//-----------------------------
//--- variables

entity cinematicArm_IGM4_Shuttlebay;


//===================================================================================================================
// Setup
//===================================================================================================================

//---------------------
// setupMission4Deck7b
// does all the setup stuff for the mission
//---------------------
void setupMission4Deck7b()
{
	//--- Fade Out
	globalCoop_main_camFadeOut(.01);
	spawn( "Camera", "targetname", "cam1" );

	cam.load( "cam_igm4_deck7b_1" );

	$tempshuttle.remove();
	//$shuttleExterior.anim( "closed_new" );
	//globalShuttle_Setup( "sydney", "chang", "chell", "jurot" );
	$shuttleExterior.hide();


	// setup DrullShuttle
	initDrullShuttle( "shuttle" );

	$drullShuttleCenterOrigin.origin( $shuttleStartOrigin0.getorigin() );

	$drullShuttleCenterOrigin.time( .05 );
	$drullShuttleCenterOrigin.moveUp( 200 );
	waitfor( $drullShuttleCenterOrigin );

	$drullShuttleCenterOrigin.bind( $shuttleStartOrigin2 );

	// Spawn Actors
	globalCommon_SpawnActor( "char/starfleet_tuvok.tik", "tuvok", '-8644 -5068 -216', 45 );
	globalCommon_SpawnActor( "char/inigor-noweapon.tik", "inigor", '-8556 -4972 -216', 225 );

	globalCommon_SpawnActor( "char/drull_son-male.tik", "krindo", '-8524 -5052 -216', 185 );
	globalCommon_SpawnActor( "char/kleeya-noweapon.tik", "kleeya", '-8692 -5044 -216', 25 );

	globalCommon_SpawnActor( "char/hazardteam_sf-munro.tik", "scriptmunro", '-8612 -5100 -216', 45 );

	//Actors For Dialog
	if(!cvar_bool_multiplayer){//Singleplayer
		globalCommon_SpawnActor( "char/starfleet_picard.tik", "picard", '-8644 -5068 -216', 45 );
		$picard.hide();
		$picard.notsolid();
	}
	else{
		spawn("script_model","model","fx/fx-dummy.tik","targetname","picard");
	}


	//--- armature setup
	cinematicArm_IGM4_Shuttlebay = createCinematic( "igm4_leave" );
	cinematicArm_IGM4_Shuttlebay.setendthread( "cinematicDrullShuttleLeavesIGM4Scene2" );

	developerMode = 0;

	if( developerMode == 1 )
	{
		cinematicDrullShuttleLeavesIGM4Scene2();
	}

	thread cinematicDrullShuttleLeavesIGM4();
}

//---------------------
// cinematicDrullShuttleLeavesIGM4
// setup and begin cinematicDrullShuttleLeavesIGM4
//---------------------
void cinematicDrullShuttleLeavesIGM4()
{
	//--- starting cinematic commands
	globalCoop_cin_start();

	//--- fade out
	globalCoop_main_camFadeOut(.5);
	wait( .6 );

	//--- hide the player and AI
	globalCommon_AiDummyHide( $scriptmunro );

	//allowmusicducking ( 0 );
	music ( "aux2" );
	//--- start the armature
	cinematicArm_IGM4_Shuttlebay.beginCinematic( "igm4_leave" );
	wait( .2 );

	//--- fade in
	globalCoop_main_camFadeIn(1);

	//--- set the skipthread
	globalCoop_cin_skipThread( "cinematicDrullShuttleLeavesIGM4End" );
}

//---------------------
// cinematicDrullShuttleLeavesIGM4Scene2
//------------------------
void cinematicDrullShuttleLeavesIGM4Scene2()
{
	//--- fade out and set the level up
	globalCoop_main_camFadeOut(.5);
	wait( .75 );

	$shuttlePath01b0.speed( 1 );
	$shuttlePath01b1.speed( 1 );
	$shuttlePath01b2.speed( .5 );
	$shuttlePath01b3.speed( .5 );
	$shuttlePath01b4.speed( 1 );
	$shuttlePath01b5.speed( 1 );
	$shuttlePath01b6.speed( 1 );
	$shuttlePath01b7.speed( 1 );
	$shuttlePath01b8.speed( 1 );

	// --- Que Cams
	globalCoop_cin_follow($cam1,$cam_igm4_deck7b_1 );
	globalCoop_cin_fov($cam1, 60 );
	$cam1.cut();

	$sky.rendereffects( "+skyorigin" );
	$enterpriseE1.show();
	$enterpriseE2.show();
	$enterpriseE3.show();

	thread openLeftShuttleBayDoor();

	globalCoop_cin_cuecamera( $cam1 );

	wait( .05 );

	//--- Start & Fade in
	globalCoop_main_camFadeIn(.5);
	wait( 3 );
	
	$shuttleStartOrigin2.playsound( "sound/vehicles/shuttlecraft/shuttle_takeoff.wav", 3, 1, 3000 );
	$shuttleStartOrigin2.followpath( $shuttlePath01b0 );
	wait( 6 );

	//--- fade out and set the level up
	globalCoop_main_camFadeOut(.5);
	wait( 2.05 );

	//--- End Thread
	thread cinematicDrullShuttleLeavesIGM4End();
}

//---------------------
// cinematicDrullShuttleLeavesIGM4Skipthread
// skipthread that calls the skipthread routines
//------------------------
void cinematicDrullShuttleLeavesIGM4Skipthread()
{
	//--- kill the cinematicDrullShuttleLeavesIGM4 thread
	skipthread( "Null" );
	killthread( "cinematicDrullShuttleLeavesIGM4Scene2" );

	thread cinematicDrullShuttleLeavesIGM4End();
}


//---------------------
// cinematicDrullShuttleLeavesIGM4End
// wrapup the cinematicDrullShuttleLeavesIGM4
//------------------------
void cinematicDrullShuttleLeavesIGM4End()
{
	//kill the cinematic
	skipthread( "null" );

	//fade to black
	globalCoop_main_camFadeOut(.5);
	wait( .6 );

	//--- end the armature cinematics, FALSE means don't launch the end thread
	cinematicArm_IGM4_Shuttlebay.endCinematic( FALSE );

	killthread( "cinematicDrullShuttleLeavesIGM4" );
	killthread( "cinematicDrullShuttleLeavesIGM4Scene2" );

	//--- IGM stuff
	setfloatvar ("game.checkEntMission4RoomsVisited", 6 );
	if(cvar_bool_multiplayer){
		stuffCmd("seta coop_igmR 6\n");
	}

	if( developerMode == 1 )
	{
		//--- release the cinematic
		globalCoop_cin_end();

		//--- Start & Fade in
		globalCoop_main_camFadeIn(.5);
		wait( .75 );
		pause();
	}
	sNextLevelToLoadForCoop="ent-deck1_bridge";
	thread coop_endLevel();
}

//===================================================================================================================
// Deck Change Check
//===================================================================================================================
//---------------------
// checkForDeckChangeIGM4
//---------------------
void checkForDeckChangeIGM4()
{
	if(!cvar_bool_multiplayer){//Singleplayer
		if( getfloatvar( "game.checkEntMission4RoomsVisited" ) == 4 )
		{
			setfloatvar(  "level.deckToChangeToIGM", 1 );

			$scriptmunro.playdialog("localization/sound/dialog/ent_m1/munro_d1bridge.mp3", 1, 10000, 0); //Deck 1: Bridge
		}
		if( getfloatvar( "game.checkEntMission4RoomsVisited" ) == 12 )
		{
			setfloatvar(  "level.deckToChangeToIGM", 11 );

			$scriptmunro.playdialog("localization/sound/dialog/ent_m1/munro_d11astro.mp3", 1, 10000, 0); //Deck 11: Astrometrics
		}
	}
}























