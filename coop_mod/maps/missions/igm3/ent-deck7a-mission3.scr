//-----------------------------------------------------------------
//
//  EF2 Level Script File
//
//  Level:	      ent-deck7a - MISSION 3
//  Script By:    K.Thompson
//  Created on:   08/12/2002
//	Last Edited:  08/12/2002 - kt
//-----------------------------------------------------------------

void setupMission3Deck7a();
void justLoadedDeckThreadIGM3();

void picardBriefingCinematicIGM3();

void cinematicPicardBriefing();
void cinematicPicardBriefingSkipthread();
void cinematicPicardBriefingEnd();

void ambientActorsIGM3();

//-----------------------------
//--- variables

entity cinematicArm_IGM3_Briefing;


//===================================================================================================================
// Setup
//===================================================================================================================

//---------------------
// does all the setup stuff for the mission
//---------------------
void setupMission3Deck7a()
{
	$triggerHazardOps1.thread( "picardBriefingCinematicIGM3" );
	$triggerHazardOps2.thread( "picardBriefingCinematicIGM3" );

	spawn( "Camera", "targetname", "cam1" );
	spawn( "Camera", "targetname", "cam2" );

	cam.load( "cam1_ent_deck7a_IGM3" );
	cam.load( "cam2_ent_deck7a_IGM3" );

	globalCommon_SpawnActor( "char/starfleet_picard.tik", "picard", '-11328 -1136 -68', 0 );
	globalCommon_SpawnActor( "char/starfleet_tuvok.tik", "tuvok", '-11200 -1388 -68', 45 );
	globalCommon_SpawnActor( "char/hazardteam_sf-jurot.tik", "jurot", '-11051.30 -951.30 -68.00', 195 );
	globalCommon_SpawnActor( "char/hazardteam_sf-chell.tik", "chell", '-11039.00 -1041.00 -68.00', 185 );
	globalCoop_main_waitAFrame();
	globalCommon_SpawnActor( "char/hazardteam_sf-munro.tik", "scriptmunro", '-11038.00 -1136.00 -72.00', 180 );
	globalCommon_SpawnActor( "char/hazardteam_sf-telsia.tik", "telsia", '-11033.00 -1223.00 -72.00', 175 );
	globalCommon_SpawnActor( "char/hazardteam_sf-chang.tik", "chang", '-11055.30 -1313.30 -72.00', 165 );
	//globalCommon_SpawnActor( "char/hazardteam_sf-franklin.tik", "franklin", '-10863.30 -953.30 -48.00', 195 );
	globalCoop_main_waitAFrame();
	globalCommon_SpawnActor( "char/hazardteam_sf-sydney.tik", "sydney", '-10843.00 -1041.00 -48.00', 185 );
	globalCommon_SpawnActor( "char/hazardteam_sf-kourban.tik", "kourban", '-10846.00 -1136.00 -48.00', 180 );
	globalCommon_SpawnActor( "char/hazardteam_sf-gonzales.tik", "gonzales", '-10843.00 -1223.00 -48.00', 175 );
	globalCoop_main_waitAFrame();
	globalCommon_SpawnActor( "char/hazardteam_sf-redshirt3.tik", "stockman", '-10863.30 -1313.30 -48.00', 165 );
	globalCommon_SpawnActor( "char/hazardteam_sf-redshirt6.tik", "devaldenebro", '-10667.30 -949.30 -24.00', 195 );
	globalCommon_SpawnActor( "char/hazardteam_sf-redshirt5.tik", "katcherside", '-10655.00 -1041.00 -24.00', 185 );
	globalCommon_SpawnActor( "char/hazardteam_sf-redshirt4.tik", "waters", '-10652.00 -1136.00 -24.00', 180 );
	globalCoop_main_waitAFrame();
	globalCommon_SpawnActor( "char/hazardteam_sf-redshirt2.tik", "thompson", '-10651.00 -1221.00 -24.00', 175 );
	globalCommon_SpawnActor( "char/hazardteam_sf-redshirt.tik", "law", '-10667.30 -1317.30 -24.00', 165 );
	ambientActorsIGM3();
	globalCoop_main_waitAFrame();

	//--- armature setup
	cinematicArm_IGM3_Briefing = createCinematic( "igm3_brief" );
	cinematicArm_IGM3_Briefing.setendthread( "cinematicPicardBriefingEnd" );

	thread randomlyAnimateActor( $chang, "ent-sit-room" );
	thread randomlyAnimateActor( $jurot, "ent-sit-room" );
	wait( .2 );
	thread randomlyAnimateActor( $chell, "ent-sit-room" );
	thread randomlyAnimateActor( $scriptmunro, "ent-sit-room" );
	wait( .2 );
	thread randomlyAnimateActor( $telsia, "ent-sit-room" );
	thread randomlyAnimateActor( $sydney, "ent-sit-room" );
	wait( .2 );
	thread randomlyAnimateActor( $kourban, "ent-sit-room" );
	thread randomlyAnimateActor( $gonzales, "ent-sit-room" );
	wait( .2 );
	thread randomlyAnimateActor( $stockman, "ent-sit-room" );
	thread randomlyAnimateActor( $devaldenebro, "ent-sit-room" );
	wait( .2 );
	thread randomlyAnimateActor( $katcherside, "ent-sit-room" );
	thread randomlyAnimateActor( $waters, "ent-sit-room" );
	wait( .2 );
	thread randomlyAnimateActor( $thompson, "ent-sit-room" );
	thread randomlyAnimateActor( $law, "ent-sit-room" );
}

//---------------------
// justLoadedDeckThreadIGM1
// Triggered and check to see if A cinematic should happen.
//------------------------
void justLoadedDeckThreadIGM3()
{
	coop_string_objectiveItem1			= "IGM3TalkWithPicard";
	coop_string_objectiveItem2			= "IGM3GoToLibaray";
	coop_string_objectiveItem3			= "IGM3MeetInBriefingRoom";
	coop_string_objectiveItem4			= "IGM3GoToTransporterRoom";
	globalCoop_objectives_update("complete",1,1);//state,slot,show
	globalCoop_objectives_update("complete",2,1);//state,slot,show
	thread globalCoop_objectives_update("inComplete",3,1);//state,slot,show
}

//----------------------
// picardBriefingCinematicIGM3
// Check to see if it is time to play the last cinematc
//----------------------
void picardBriefingCinematicIGM3()
{
	$triggerHazardOps1.thread( "" );
	$triggerHazardOps2.thread( "" );
	thread cinematicPicardBriefing();
}

//---------------------
// cinematic
// setup and begin cinematic
//---------------------
void cinematicPicardBriefing()
{
	justLoadedDeckThreadIGM3();

	wait( .5 );

	//--- fade out
	cam_fadeout( .5, 0, 0, 0, 1, 0 );
	wait( .6 );
	globalCoop_cin_start();
	//--- hide the player and AI
	globalCommon_AiDummyHide( $picard );
	globalCommon_AiDummyHide( $tuvok );
	globalCommon_AiDummyHide( $telsia );
	globalCommon_AiDummyHide( $chell );
	globalCommon_AiDummyHide( $scriptmunro );
	globalCommon_AiDummyHide( $kourban );
	globalCommon_AiDummyHide( $chang );


	//--- start the armature
	cinematicArm_IGM3_Briefing.beginCinematic( "igm3_brief" );
	wait( .2 );

	//allowMusicDucking ( 0 );
	music ( "aux2" );

	//--- fade in
	//cam_fadein( .5, 0, 0, 0, 1, 0 );

	//--- set the skipthread
	globalCoop_cin_skipThread( "cinematicPicardBriefingEnd" );
}

//---------------------
// cinematicSkipthread
// skipthread that calls the skipthread routines
//------------------------
void cinematicPicardBriefingSkipthread()
{
	//--- kill the cinematic thread
	skipthread( "Null" );
	killthread( "cinematicPicardBriefing" );
	thread cinematicPicardBriefingEnd();
}


//---------------------
// cinematicEnd
// wrapup the cinematic
//------------------------
void cinematicPicardBriefingEnd()
{
	//kill the cinematic
	skipthread( "null" );

	//fade to black
	cam_fadeout( .5, 0, 0, 0, 1, 0 );
	wait( .6 );
	music ("normal");

	//--- end the armature cinematics, FALSE means don't launch the end thread
	cinematicArm_IGM3_Briefing.endCinematic( FALSE );

	killthread( "cinematicPicardBriefing" );

	if(!cvar_bool_multiplayer){
		//--- change levels
		spawn ( "trigger_changelevel", "targetname", "trigger_endlevel", "map", "m5l1a-drull_ruins1" );
		setfloatvar( "game.globalMissionEnterprise", 4 );
		noIntermission();
		wait ( .1 );
		trigger ( "$trigger_endlevel" );
	}else{
	//do not set next igm in mp, this will be done on the last mission 5 map
		thread globalCoop_mission_completed("m5l1a-drull_ruins1");
	}
}

//===================================================================================================================
// Ambient Actors
//===================================================================================================================
//---------------------
// ambientActorsIGM3
// Set up ambient Actors
//---------------------
void ambientActorsIGM3()
{
	globalCommon_SpawnActor( "char/starfleet_crew-male.tik", "jacobs", '-10000 1036 16', 90 );

	$jacobs.setnodeid( 50 );
	$jacobs.alias( "jacobsIGMA1", "localization/sound/dialog/ent_m3/crewm_aliens.mp3" ); //Do you think those aliens will attack the Enterprise?
	$jacobs.alias( "jacobsIGMA2", "localization/sound/dialog/ent_m3/crewf_miracle.mp3" ); //It's a miracle anyone on the Dallas survived.
	$jacobs.alias( "jacobsIGMA3", "localization/sound/dialog/ent_m3/jacobs_imagine.mp3" ); //Their struggle to stay alive must have been formidable.
	$jacobs.dialog( "jacobsIGMA" );
	$jacobs.ai_on();

	globalCommon_SpawnActor( "char/starfleet_crew-female6.tik", "jameson", '-10440 552 0', 0 );

	$jameson.setnodeid( 51 );
	$jameson.alias( "jamesonIGMA1", "localization/sound/dialog/ent_m2/myamoto_res.mp3" );
	$jameson.alias( "jamesonIGMA2", "localization/sound/dialog/ent_m3/myamoto_helpyou.mp3" );
	$jameson.alias( "jamesonIGMA3", "localization/sound/dialog/ent_m3/myamoto_whatit.mp3" );
	$jameson.dialog( "jamesonIGMA" );
	$jameson.ai_on();
}

//===================================================================================================================
// Deck Change Check
//===================================================================================================================
//---------------------
// checkForDeckChangeIGM3
//---------------------
void checkForDeckChangeIGM3()
{
	float fRooms;
	fRooms=getFloatVar("game.checkEntMission3RoomsVisited");
	
	setFloatVar("level.deckToChangeToIGM",8);
	
	if(cvar_bool_multiplayer)
	{
		fRooms=getCvarInt("coop_igmR");
	}

	if( fRooms == 3 )
	{
		if(cvar_bool_multiplayer)
		{
			stuffcmd("seta coop_igmT 8\n");
		}
		thread globalCoop_dialog_play($scriptmunro,"ent_m1/munro_d8transp.mp3", 1, 10000, 0); //Deck 8: Transporter Room
	}
}









