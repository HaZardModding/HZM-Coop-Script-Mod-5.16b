//-----------------------------------------------------------------
//
//  EF2 Level Script File
//
//  Level:	      ent-deck7a - MISSION 87
//  Script By:    K.Thompson
//  Created on:   08/12/2002
//	Last Edited:  08/12/2002 - kt
//-----------------------------------------------------------------

void setupMission7Deck7a();
void justLoadedDeckThreadIGM7();
void playerReachedSecretIGM7();

void cinematicPicardBriefingIGM7();
void cinematicPicardBriefingIGM7Skipthread();
void cinematicPicardBriefingIGM7End();

void ambientActorsIGM7();

void alisonGrayTalkIGM7();

void stockmanWatersTalkIGM7();

void stockman_workIGM7();
void stockmanDialog7_1();
void stockmanDialog7_2();
void stockmanDialog7_3();

void waters_workIGM7();
void watersDialog7_1();

void chang_workIGM7();
void changDialog7_1();

void thompson_workIGM7();
void thompsonDialog7_1();

void miyamoto_workIGM7();
void miyamotoDialog7_1();

void stratton_workIGM7();
void strattonDialog7_1();

//-----------------------------
//--- variables

entity cinematicArm_IGM7_Briefing;
entity ePlayerCinematicStarter;


void coopLoadCamInMPOnly(string sCamPath)
{
	if(sCamPath != "" && cvar_bool_multiplayer == 1)
	{
		cam.load(sCamPath);
	}
}
void coop_headWatch(entity eActor)
{
	if(doesEntityExist(eActor))
	{
		eActor.headwatch(globalCoop_return_playerClosestPreferActive(eActor),10);
	}
}
void coop_headWatchTurnTo(entity eActor)
{
	if(doesEntityExist(eActor))
	{
		entity ePlayer;
		ePlayer = globalCoop_return_playerClosestPreferActive(eActor);
		eActor.headwatch(ePlayer,10);
		eActor.turntowardsentity(ePlayer);
	}
}


//===================================================================================================================
// Setup 
//===================================================================================================================

//---------------------
// setupMission7Deck7a	
// does all the setup stuff for the mission
//---------------------
void setupMission7Deck7a()
{
	if(!cvar_bool_multiplayer)
	{
		globalCommon_SpawnActor( "char/hazardteam_sf-munro.tik", "scriptmunro", '-11038.00 -1136.00 -72.00', 180 );
		globalCommon_SpawnActor( "char/starfleet_crew-male6.tik", "russel", '-10440 552 0', 0 );
		$russel.setnodeid( 51 );
		$russel.alias( "russelIGMA1", "localization/sound/dialog/ent_m7/jacobs_chat4.mp3" );
		$russel.dialog( "russelIGMA" );
		$russel.ai_on();
		globalCoop_level_remove( $scriptmunro );
	}
	else
	{
		stuffcmd("seta coop_igm 7\n");
		stuffcmd("seta coop_igmT 71\n");
		stuffcmd("seta coop_igmR 4\n");
		globalCoop_main_waitAFrame();
		
	//set for internal script reference
		setFloatVar( "game.globalMissionEnterprise", 7 );
	//Definie Objective
		coop_string_objectiveItem1			= "IGM7GoToBrig";
		coop_string_objectiveItem2			= "IGM7GoToArmory";
		coop_string_objectiveItem3			= "IGM7HoloDeck";
		coop_string_objectiveItem4			= "IGM7GoToBreifingRoom";
		coop_string_objectiveItem5			= "IGM7GoToSickBay";
		
		thread globalCoop_objectives_update("complete",1,0);
		thread globalCoop_objectives_update("complete",2,0);
		thread globalCoop_objectives_update("complete",3,0);
		thread globalCoop_objectives_update("incomplete",4,1);
		
		spawn("script_model","model","fx/fx-dummy.tik","targetname","scriptmunro","origin","-11038.00 -1136.00 -72.00");
	}
	
	$triggerHazardOps1.thread( "cinematicPicardBriefingIGM7" );
	$triggerHazardOps2.thread( "cinematicPicardBriefingIGM7" );
	
//do not spawn these cuz they are not really visible and we need less models	
	//globalCommon_SpawnActor( "char/starfleet_tuvok.tik", "tuvok", '-11200 -1388 -68', 45 );
	globalCommon_SpawnActor( "char/starfleet_picard.tik", "picard", '-11328 -1136 -68', 0 );
	globalCommon_SpawnActor( "char/hazardteam_sf-jurot.tik", "jurot", '-11051.30 -951.30 -68.00', 195 );
	globalCommon_SpawnActor( "char/hazardteam_sf-chell.tik", "chell", '-11039.00 -1041.00 -68.00', 185 );
	globalCommon_SpawnActor( "char/hazardteam_sf-telsia.tik", "telsia", '-11033.00 -1223.00 -72.00', 175 );
	globalCommon_SpawnActor( "char/hazardteam_sf-chang.tik", "chang", '-11055.30 -1313.30 -72.00', 165 );
	globalCommon_SpawnActor( "char/hazardteam_sf-sydney.tik", "sydney", '-10843.00 -1041.00 -48.00', 185 );
	globalCommon_SpawnActor( "char/hazardteam_sf-kourban.tik", "kourban", '-10846.00 -1136.00 -48.00', 180 );
	globalCommon_SpawnActor( "char/hazardteam_sf-gonzales.tik", "gonzales", '-10843.00 -1223.00 -48.00', 175 );	
	globalCommon_SpawnActor( "char/hazardteam_sf-redshirt3.tik", "stockman", '-10863.30 -1313.30 -48.00', 165 );	
	globalCommon_SpawnActor( "char/hazardteam_sf-redshirt6.tik", "devaldenebro", '-10667.30 -949.30 -24.00', 195 );	
	globalCommon_SpawnActor( "char/hazardteam_sf-redshirt5.tik", "katcherside", '-10655.00 -1041.00 -24.00', 185 );	
	globalCommon_SpawnActor( "char/hazardteam_sf-redshirt4.tik", "waters", '-10652.00 -1136.00 -24.00', 180 );	
	globalCommon_SpawnActor( "char/hazardteam_sf-redshirt2.tik", "thompson", '-10651.00 -1221.00 -24.00', 175 );
	globalCommon_SpawnActor( "char/hazardteam_sf-redshirt.tik", "law", '-10667.30 -1317.30 -24.00', 165 );	
	
	// Spawn These ambient actors.
	globalCommon_SpawnActor( "char/starfleet_crew-female6.tik", "jameson", '-10000 1036 16', 90 );
	$jameson.setnodeid( 50 );
	$jameson.alias( "jamesonIGMA1", "localization/sound/dialog/ent_m7/kretar_help2.mp3" ); 
	$jameson.dialog( "jamesonIGMA" );
	$jameson.ai_on();
	
	globalCommon_SpawnActor( "char/starfleet_crew-male3.tik", "stratton", '-10322 -296 0', 0 );
	$stratton.pushable( 0 );
	thread randomlyAnimateActor( $stratton, "ent-stand" );
	thread stratton_workIGM7();
		
	//--- armature setup
	cinematicArm_IGM7_Briefing = createCinematic( "igm7_brief" );
	cinematicArm_IGM7_Briefing.setendthread( "cinematicPicardBriefingIGM7End" );
	
	//Setup Secret
	$secretTrigger.thread( "playerReachedSecretIGM7" );
	spawn( "trigger_secret", "targetname", "secretSpawner_trigger" );
	trigger( "$secretSpawner" );
	
	//Unlock Secret Door. Unlocked In IGM 8
	$secretDoor.unlock();
	
	$chang.anim( "ent-sit-room-idle" );
	thread randomlyAnimateActor( $jurot, "ent-sit-room" );
	wait( .2 );
	thread randomlyAnimateActor( $chell, "ent-sit-room" );	
	wait( .2 );
	thread randomlyAnimateActor( $telsia, "ent-sit-room" );
	thread randomlyAnimateActor( $sydney, "ent-sit-room" );
	wait( .2 );
	thread randomlyAnimateActor( $kourban, "ent-sit-room" );
	thread randomlyAnimateActor( $gonzales, "ent-sit-room" );
	wait( .2 );
	$stockman.anim( "ent-sit-room-idle-look" );
	thread randomlyAnimateActor( $devaldenebro, "ent-sit-room" );
	wait( .2 );
	thread randomlyAnimateActor( $katcherside, "ent-sit-room" );
	
	$waters.anim( "ent-sit-room-idle" );
	wait( .2 );
	$thompson.anim( "ent-sit-room-idle-look" );
	thread randomlyAnimateActor( $law, "ent-sit-room" );
}

//---------------------
// munroHereToSeePicardIGM7
// Triggered and check to see if A cinematic should happen.
//------------------------
void justLoadedDeckThreadIGM7()
{
	if(!cvar_bool_multiplayer)
	{
		if( getFloatVar( "game.IGM7GoToBrig" ) >= 1 ){
			$player.setobjectiveshow( "IGM7GoToBrig", 1 );		
		}
		if(getFloatVar( "game.IGM7GoToBrig" ) == 2 ){
			$player.setobjectivecomplete( "IGM7GoToBrig", 1 );			
		}
		if(getFloatVar( "game.IGM7GoToArmory" ) >= 1 ){
			$player.setobjectiveshow( "IGM7GoToArmory", 1 );		
		}
		if(getFloatVar( "game.IGM7GoToArmory" ) == 2 ){
			$player.setobjectivecomplete( "IGM7GoToArmory", 1 );			
		}
		if(getFloatVar( "game.IGM7HoloDeck" ) >= 1 ){
			$player.setobjectiveshow( "IGM7HoloDeck", 1 );	
		}
		if(getFloatVar( "game.IGM7HoloDeck" ) == 2 ){
			$player.setobjectivecomplete( "IGM7HoloDeck", 1 );			
		}
		if(getFloatVar( "game.IGM7GoToBreifingRoom" ) >= 1 ){
			$player.setobjectiveshow( "IGM7GoToBreifingRoom", 1 );		
		}
		if(getFloatVar( "game.IGM7GoToBreifingRoom" ) == 2 ){
			$player.setobjectivecomplete( "IGM7GoToBreifingRoom", 1 );			
		}
		
		if(getFloatVar( "game.IGM7GoToSickBay" ) >= 1 ){
			$player.setobjectiveshow( "IGM7GoToSickBay", 1 );		
		}
		if(getFloatVar( "game.IGM7GoToSickBay" ) == 2 ){
			$player.setobjectivecomplete( "IGM7GoToSickBay", 1 );			
		}
	}
}

//---------------------
// used to place secret pieces in level 
//------------------------
void playerReachedSecretIGM7()
{
	$secretTrigger.thread( "" );
	trigger( "$secretSpawner_trigger" );
}

//---------------------
// cinematic
// setup and begin cinematic
//---------------------
void cinematicPicardBriefingIGM7()
{
	entity eTrigger;
	eTrigger=getCurrentEntity();
	if(doesEntityExist(eTrigger))
	{
		ePlayerCinematicStarter=eTrigger.getLastActivatingEntity();	
	}
	
	$triggerHazardOps1.thread( "" );
	$triggerHazardOps2.thread( "" );
	
	spawn( "Camera", "targetname", "cam1" );
	cam.load( "cam1_ent_deck7a_IGM3" );
	
	//--- fade out
	cam_fadeout( .5, 0, 0, 0, 1, 0 );
	wait(0.5);
	if(cvar_bool_multiplayer)
	{
		if(doesEntityExist(ePlayerCinematicStarter))
		{
			ePlayerCinematicStarter.warp('-10952 -1136 -68');
			ePlayerCinematicStarter.playerviewangles('0 188 0');
		}
	}
	globalCoop_level_remove( $picard );
	
	globalCoop_cin_start();
	
	//--- start the armature
	cinematicArm_IGM7_Briefing.beginCinematic( "igm7_brief" );
	wait( 2 );
	
	//--- fade in
	cam_fadein( 1, 0, 0, 0, 1, 0 );
	
	//--- set the skipthread
	globalCoop_cin_skipThread( "cinematicPicardBriefingIGM7End" );
}

//---------------------
// cinematicSkipthread
// skipthread that calls the skipthread routines
//------------------------
void cinematicPicardBriefingIGM7Skipthread()
{
	//--- kill the cinematic thread
	skipthread( "Null" );
	killthread( "cinematicPicardBriefingIGM7" );
	
	thread cinematicPicardBriefingIGM7End();
}


//---------------------
// cinematicEnd
// wrapup the cinematic
//------------------------
void cinematicPicardBriefingIGM7End()
{
	//kill the cinematic
	skipthread( "null" );

	//fade to black
	cam_fadeout( .5, 0, 0, 0, 1, 0 );
	wait( .6 );

	//--- end the armature cinematics, FALSE means don't launch the end thread
	cinematicArm_IGM7_Briefing.endCinematic( FALSE );

	killthread( "cinematicPicardBriefingIGM7" );

	//--- position and setup ambient actors
	globalCoop_level_remove( $tuvok );
	globalCoop_level_remove( $jurot );
	globalCoop_level_remove( $chell );
	globalCoop_level_remove( $telsia );
	globalCoop_level_remove( $sydney );
	globalCoop_level_remove( $kourban );
	globalCoop_level_remove( $gonzales );
	globalCoop_level_remove( $devaldenebro );
	globalCoop_level_remove( $katcherside );
	globalCoop_level_remove( $law );
	globalCoop_level_removePath($cam1_ent_deck7a_IGM3);
	
	ambientActorsIGM7();

	//--- set the player
	if(!cvar_bool_multiplayer)
	{
		$player.warp( '-10952 -1136 -68' );
	}

	//--- release the cinematic
	globalCoop_cin_end();
	wait( .15 );

	cam_fadein( .5, 0, 0, 0, 1, 0 );

	//--- IGM stuff
	setFloatVar( "game.IGM7GoToBreifingRoom", 2 );
	setFloatVar( "game.IGM7GoToSickBay", 1 );
	justLoadedDeckThreadIGM7();
	
	setFloatVar( "game.checkEntMission7RoomsVisited", 5 );
	stuffcmd("seta coop_igm 7\n");
	stuffcmd("seta coop_igmR 5\n");

	//Turn Exit Triggers On
	$triggerExit.triggerable();
	
	thread globalCoop_objectives_update("complete",4,0);
	thread globalCoop_objectives_update("incomplete",5,1);

	//wait( .6 );
	
	//globalCoop_dialog_play($tuvok,"ent_m7/tuvok_surgical.mp3", 1, 10000, 1 ); //Please Report to SickBay for Sirgical alteration.
}

//===================================================================================================================
// Ambient Actors
//===================================================================================================================
//---------------------
// ambientActorsIGM7	
// Set up ambient Actors
//---------------------
void ambientActorsIGM7()
{
	//globalCommon_SpawnActor( "char/hazardteam_sf-redshirt3.tik", "stockman", '-10327.03 -1383.03  0.00', 225 );	
	$stockman.pushable( 0 );
	$stockman.anim( "idle" );
	$stockman.origin( '-10327.03 -1383.03  0.00' );
	$stockman.angle( 225 );
	//globalCommon_SpawnActor( "char/hazardteam_sf-redshirt4.tik", "waters", '-10361.03 -1417.03  0.00', 45 );
	$waters.pushable( 0 );
	$waters.anim( "idle" );
	$waters.origin( '-10361.03 -1417.03  0.00' );
	$waters.angle( 45 );
	//globalCommon_SpawnActor( "char/hazardteam_sf-chang.tik", "chang", '-10426.10 -90.10  0.00', 350 );
	$chang.pushable( 0 );
	$chang.anim( "idle" );
	$chang.origin( '-10426.10 -90.10  0.00' );
	$chang.angle( 350 );
	//globalCommon_SpawnActor( "char/hazardteam_sf-redshirt2.tik", "thompson", '-10378.10 -90.10  0.00', 190 );
	$thompson.pushable( 0 );
	$thompson.anim( "idle" );
	$thompson.origin( '-10378.10 -90.10  0.00' );
	$thompson.angle( 190 );
	
	globalCommon_SpawnActor( "char/starfleet_crew-female5.tik", "alison", '-10080 -184 0', 270 );
	$alison.pushable( 0 );
	globalCommon_SpawnActor( "char/starfleet_crew-male5.tik", "gray", '-10080 -264 0', 90 );
	$gray.pushable( 0 );
	
	$triggerHallConversation1.thread( "alisonGrayTalkIGM7" );

	$alison.headwatch( $gray, 5 );
	$gray.headwatch( $alison, 5 );

	thread randomlyAnimateActor( $alison, "conv" );
	thread randomlyAnimateActor( $gray, "conv" );
	
	globalCommon_SpawnActor( "char/starfleet_crew-female2.tik", "miyamoto", '-10326 -984  0', 0 );
	$miyamoto.pushable( 0 );
	thread randomlyAnimateActor( $miyamoto, "ent-stand" );
	thread miyamoto_workIGM7();
	
	//Remove these becaus they are replacments.
	globalCoop_level_remove($jameson);
	globalCoop_level_remove($russel);
	
	$stockman.headwatch( $waters, 5 );
	$waters.headwatch( $stockman, 5 );
	
	thread randomlyAnimateActor( $stockman, "conv" );
	thread randomlyAnimateActor( $waters, "conv" );

	$triggerHazardOpsConversation1.thread( "stockmanWatersTalkIGM7" );
	
	$chang.headwatch( $thompson, 5 );
	$thompson.headwatch( $chang, 5 );
	
	thread randomlyAnimateActor( $chang, "conv" );
	thread randomlyAnimateActor( $thompson, "conv" );
	
	thread chang_workIGM7();
	thread thompson_workIGM7();
}

////////////////////////////
// alisonGrayTalkIGM7
/////////////////////////////
void alisonGrayTalkIGM7()
{
	$triggerHallConversation1.thread( "" );

	$alison.setFloatVar( "pauseRandomAnimate", 1 );
	$gray.setFloatVar( "pauseRandomAnimate", 1 );
	wait( .25 );
	$alison.anim( "idle" );
	$gray.anim( "idle" );

	//Oh really? So you're the new recreations officer?
	globalCoop_dialog_play($gray,"ent_m7/crewm_6hour.mp3", 1, 64, 0);
	//That may be my assignment, but I'm not happy about it.
	globalCoop_dialog_play($alison,"ent_m7/alison_notmove.mp3", 1, 64, 0);
	//That sounds like a great job?  Why aren't  you happy about it?
	globalCoop_dialog_play($gray,"ent_m7/crewm_harass.mp3", 1, 64, 0);
	//It takes me away from the Hydroponics bay and my research.
	globalCoop_dialog_play($alison,"ent_m7/alison_datpq.mp3", 1, 64, 0);
	//You'd rather be playing with your plants than having some fun?
	globalCoop_dialog_play($gray,"ent_m7/crewm_assure.mp3", 1, 64, 0);
	//Plant's provide me with all the fun I need.
	globalCoop_dialog_play($alison,"ent_m7/alison_sigh.mp3", 1, 64, 0);
	//Yeah…I'm sure they're a riot.
	globalCoop_dialog_play($gray,"ent_m7/crewm_anohour.mp3", 1, 64, 0);
	//Your kind just doesn't understand.
	globalCoop_dialog_play($alison,"ent_m7/alison_butwhatif.mp3", 1, 64, 0);
	//Oh…You mean the human kind?
	globalCoop_dialog_play($gray,"ent_m7/crewm_halfw.mp3", 1, 64, 0);
	//Whatever…
	globalCoop_dialog_play($alison,"ent_m7/alison_but.mp3", 1, 64, 0);
	//See you later Dr. Greenthumb.
	globalCoop_dialog_play($gray,"ent_m7/crewm_half.mp3", 1, 64, 0);

	$alison.resethead();
	$gray.resethead();
	
	$alison.setnodeid( 50 );
	$gray.setnodeid( 51 );
	$alison.ai_on();
	$gray.ai_on();
	
	
	if(!cvar_bool_multiplayer)
	{
		//Please don't disturb me. 
		$alison.alias( "alisonIGMA1", "localization/sound/dialog/ent_m1/alison_kindaBusy.mp3" );
		$alison.dialog( "alisonIGMA" );
			
		//Sorry.  No time for chit-chat.
		$gray.alias( "grayIGMA1", "localization/sound/dialog/ent_m2/stratton_chat1.mp3" );
		$gray.dialog( "grayIGMA" );
	}
	
}


////////////////////////////
// stockmanWatersTalkIGM7
/////////////////////////////
void stockmanWatersTalkIGM7()
{
	$triggerHazardOpsConversation1.thread( "" );

	$stockman.setFloatVar( "pauseRandomAnimate", 1 );
	$waters.setFloatVar( "pauseRandomAnimate", 1 );
	wait( .25 );
	$stockman.anim( "idle" );
	$waters.anim( "idle" );

	//You ever notice all our food tastes like replicated chicken?
	globalCoop_dialog_play($stockman,"ent_m6/crewm1_notice.mp3", 1, 64, 0);
	//No.
	globalCoop_dialog_play($waters,"ent_m6/crewm2_no.mp3", 1, 64, 0);
	//It does… I order chicken teriaki - tastes like replicated chicken, I order chicken pot-pie - tastes like replicated chicken… the chicken cordon-bleu, the chicken tetrizini, the chicken soup, even the Fried Chicken all tastes like replicated chicken…
	globalCoop_dialog_play($stockman,"ent_m6/crewm1_order.mp3", 1, 64, 0);
	//You are the dumbest person alive.
	globalCoop_dialog_play($waters,"ent_m6/crewm2_dumbest.mp3", 1, 64, 0);

	$stockman.setFloatVar( "pauseRandomAnimate", 0 );
	$waters.setFloatVar( "pauseRandomAnimate", 0 );
	
	thread stockman_workIGM7();
	thread waters_workIGM7();
}

///////////////////////////
// stockmanOnUseWaitIGM7
// Wait for player to talk to actor
///////////////////////////
void stockman_workIGM7()
{
	setNextDialog( $stockman, 1 );
	setDialogFunction( $stockman, "stockmanDialog" );
	while( 1 )
	{
		workUntilUsed( $stockman, "", "", 0 );
		talkUntilDone( $stockman );
	}
}

///////////////////////////
// stockmanDialog7_1
// Player and actor talk
///////////////////////////
void stockmanDialog7_1()
{
	$stockman.setFloatVar( "pauseRandomAnimate", 1 );
	wait ( .25 );
	
	$stockman.anim( "idle" );
	coop_headWatchTurnTo($stockman);

	//I hear you're gonna infiltrate a Romulan base?
	globalCoop_dialog_play($stockman,"ent_m7/dev_infiltrate.mp3", 1, 128, 0);
	
	setNextDialog( $stockman, 2 );
	$stockman.resethead();
	$stockman.turnTowardsEntity( $waters );
	wait( .5 );
	$stockman.setFloatVar( "pauseRandomAnimate", 0 );
	setDialogFinished( $stockman );
}

///////////////////////////
// stockmanDialog7_2
// Player and actor talk
///////////////////////////
void stockmanDialog7_2()
{
	$stockman.setFloatVar( "pauseRandomAnimate", 1 );
	wait ( .25 );
	
	$stockman.anim( "idle" );
	
	coop_headWatchTurnTo($stockman);
	
	//That takes courage.  Just come back in one piece.
	globalCoop_dialog_play($stockman,"ent_m7/dev_takes.mp3", 1, 128, 0);
	
	setNextDialog( $stockman, 3 );
	$stockman.resethead();
	$stockman.turnTowardsEntity( $waters );
	wait( .5 );
	$stockman.setFloatVar( "pauseRandomAnimate", 0 );
	setDialogFinished( $stockman );
}

///////////////////////////
// stockmanDialog7_3
// Player and actor talk
///////////////////////////
void stockmanDialog7_3()
{
	$stockman.setFloatVar( "pauseRandomAnimate", 1 );
	wait ( .25 );
	
	$stockman.anim( "idle" );
	
	coop_headWatchTurnTo($stockman);
	
	//Remember the Romulans are pretty clever, don't underestimate them.
	globalCoop_dialog_play($stockman,"ent_m7/dev_clever.mp3", 1, 128, 0);

	setNextDialog( $stockman, 1 );
	$stockman.resethead();
	$stockman.turnTowardsEntity( $waters );
	wait( .5 );
	$stockman.setFloatVar( "pauseRandomAnimate", 0 );
	setDialogFinished( $stockman );
}

///////////////////////////
// watersOnUseWaitIGM7
// Wait for player to talk to actor
///////////////////////////
void waters_workIGM7()
{
	setNextDialog( $waters, 1 );
	setDialogFunction( $waters, "watersDialog" );
	while( 1 )
	{
		workUntilUsed( $waters, "", "", 0 );
		talkUntilDone( $waters );
	}
}

///////////////////////////
// watersDialog7_1
// Player and actor talk
///////////////////////////
void watersDialog7_1()
{
	$waters.setFloatVar( "pauseRandomAnimate", 1 );
	wait ( .25 );
	
	$waters.anim( "idle" );
	coop_headWatchTurnTo($waters);
	
	//I'm busy.
	globalCoop_dialog_play($waters,"ent_m2/stratton_busy1.mp3", 1, 128, 0);
	
	setNextDialog( $waters, 1 );
	$waters.resethead();
	$waters.turnTowardsEntity( $stockman );
	wait( .5 );
	$waters.setFloatVar( "pauseRandomAnimate", 0 );
	setDialogFinished( $waters );
}

///////////////////////////
// changOnUseWaitIGM7
// Wait for player to talk to actor
///////////////////////////
void chang_workIGM7()
{
	setNextDialog( $chang, 1 );
	setDialogFunction( $chang, "changDialog" );
	while( 1 )
	{
		workUntilUsed( $chang, "", "", 0 );
		talkUntilDone( $chang );
	}
}

///////////////////////////
// changDialog7_1
// Player and actor talk
///////////////////////////
void changDialog7_1()
{
	$chang.setFloatVar( "pauseRandomAnimate", 1 );
	wait ( .25 );
	
	$chang.anim( "idle" );
	coop_headWatchTurnTo($chang);
	
	//I'm kinda busy now, can we chat later?
	globalCoop_dialog_play($chang,"ent_m7/chang_busy1.mp3", 1, 128, 0);

	setNextDialog( $chang, 1 );
	$chang.resethead();
	$chang.turnTowardsEntity( $thompson );
	wait( .5 );
	$chang.setFloatVar( "pauseRandomAnimate", 0 );
	setDialogFinished( $chang );
}

///////////////////////////
// thompsonOnUseWaitIGM7
// Wait for player to talk to actor
///////////////////////////
void thompson_workIGM7()
{
	setNextDialog( $thompson, 1 );
	setDialogFunction( $thompson, "thompsonDialog" );
	while( 1 )
	{
		workUntilUsed( $thompson, "", "", 0 );
		talkUntilDone( $thompson );
	}
}

///////////////////////////
// thompsonDialog7_1
// Player and actor talk
///////////////////////////
void thompsonDialog7_1()
{
	$thompson.setFloatVar( "pauseRandomAnimate", 1 );
	wait ( .25 );
	
	$thompson.anim( "idle" );
	coop_headWatchTurnTo($thompson);
	
	//Can I help you?
	globalCoop_dialog_play($thompson,"ent_m3/devald_canihelp.mp3", 1, 128, 0);

	setNextDialog( $thompson, 1 );
	$thompson.resethead();
	$thompson.turnTowardsEntity( $chang );
	wait( .5 );
	$thompson.setFloatVar( "pauseRandomAnimate", 0 );
	setDialogFinished( $thompson );
}

///////////////////////////
// Wait for player to talk to actor
///////////////////////////
void miyamoto_workIGM7()
{
	setNextDialog( $miyamoto, 1 );
	setDialogFunction( $miyamoto, "miyamotoDialog" );
	while( 1 )
	{
		workUntilUsed( $miyamoto, "", "", 0 );
		talkUntilDone( $miyamoto );
	}
}

///////////////////////////
// Player and actor talk
///////////////////////////
void miyamotoDialog7_1()
{
	$miyamoto.setFloatVar( "pauseRandomAnimate", 1 );
	wait( .25 );
	$miyamoto.anim( "ent-stand-scan" );
	
	coop_headWatch($miyamoto);
	
	//Can I help you?
	globalCoop_dialog_play($miyamoto,"ent_m3/myamoto_sorry.mp3", 1, 128, 0);

	setNextDialog( $miyamoto, 1 );
	$miyamoto.resethead();
	setDialogFinished( $miyamoto );
	$miyamoto.setFloatVar( "pauseRandomAnimate", 0 );
}

///////////////////////////
// Wait for player to talk to actor
///////////////////////////
void stratton_workIGM7()
{
	setNextDialog( $stratton, 1 );
	setDialogFunction( $stratton, "strattonDialog" );
	while( 1 )
	{
		workUntilUsed( $stratton, "", "", 0 );
		talkUntilDone( $stratton );
	}
}

///////////////////////////
// Player and actor talk
///////////////////////////
void strattonDialog7_1()
{
	$stratton.setFloatVar( "pauseRandomAnimate", 1 );
	wait( .25 );
	$stratton.anim( "ent-stand-scan" );
	
	coop_headWatch($stratton);
	
	//Can I help you?
	globalCoop_dialog_play($stratton,"ent_m7/stratton_busy4.mp3", 1, 128, 0);

	setNextDialog( $stratton, 1 );
	$stratton.resethead();
	setDialogFinished( $stratton );
	$stratton.setFloatVar( "pauseRandomAnimate", 0 );
}

//===================================================================================================================
// Deck Change Check
//===================================================================================================================
//---------------------
// checkForDeckChangeIGM7	
//---------------------
void checkForDeckChangeIGM7()
{
	$elevatorButton.nouse();
	float fRooms,fRoomsCheck,fDeckGoto;
	string sDialog;
	
	fRoomsCheck	= 5;
	fDeckGoto	= 8;
	sDialog		= "ent_m1/munro_d8sickb.mp3";
	
	fRooms=getFloatVar("game.checkEntMission3RoomsVisited");
	
	if(cvar_bool_multiplayer)
	{
		fRooms=getCvarInt("coop_igmR");
	}

	if( fRooms == fRoomsCheck )
	{
		setFloatVar("level.deckToChangeToIGM",fDeckGoto);
		if(cvar_bool_multiplayer)
		{
			stuffcmd("seta coop_igmT "+fDeckGoto+"\n");
			stuffcmd("seta coop_igmR 5\n");
			stuffcmd("seta coop_igmH 0\n");
		}
		globalCoop_dialog_play($scriptmunro,sDialog, 1, 10000, 0);
	}
}






