//-----------------------------------------------------------------
//  EF2 Level Script File
//
//  Level:	      ent-training3
//  Script By:    J. Keehan
//  Geometry By:  R. Gray, J. Keehan
//  Created on:   11/08/2002
//
//	Last Edited:  Charon
//-----------------------------------------------------------------

void main();
void setupWorld();

void area3Start ();
void area3StartDialog();

void area3bridge_button_OnUse();
void area3BridgeDialog();
void scanCrystalComplete();
void postTricorderScanObjective();
void startTuvokWatch();
void area3nightVisionTraining();
void area3nightVisionTrainingDialog();

void removeLaser();
void destroyPlasmaConduit1();
void destroyPlasmaDialog();

//void setupTricorderFilterMode();
//void removeWoodStructureWait();
//void removeWoodStructure();

void area3modulateTrainingDialog();
void area3shootPlasmaTrainingDialog();
void area3filterModeTrainingDialog();
void coop_endLevel();

float rockDestroyed = 0;


//==========================================================================================
// Includes
//==========================================================================================

//Co-op
/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////
#include "coop_mod/maps/main.scr"
#include "coop_mod/maps/optional_modules/forcefields.scr"
#include "coop_mod/maps/optional_modules/puzzles.scr"
#include "coop_mod/maps/missions/training/tuvok.scr"
/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////
#include "maps/global_scripts/global_common.scr"
#include "maps/global_scripts/global_debugUtils.scr"
#include "maps/global_scripts/global_math.scr"
#include "maps/global_scripts/global_archetype.scr"

//================================================================================
// Main
//================================================================================

//---------------------
// Function: main
// Comments:
// the main thread run at the start of the level
//---------------------
void main()
{
//Co-Op
/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////
	coop_float_spawnAngle0 			= 90;//SpawnOrigin0 Angle
	coop_string_nextMapToCheck		= "ent-training4";//set the map we gona load next while we are in testmode
//Definie Objective
	coop_string_objectiveItem1			= "consoleT3";
	coop_string_objectiveItem2			= "tricorderT3";
	coop_string_objectiveItem3			= "blipT3";
	coop_string_objectiveItem4			= "nightvisionT3";
	coop_string_objectiveItem5			= "consoleModulateT3";
	coop_string_objectiveItem6			= "destroyConduitT3";
	coop_string_objectiveItem7			= "integrityT3";
//spawnorigins, Spawn Players on those locations, at map start
	coop_vector_spawnOrigin1 			= '140 -2228 -94';
	coop_vector_spawnOrigin2 			= '220 -2228 -94';
	coop_vector_spawnOrigin3 			= '300 -2228 -94';
	coop_vector_spawnOrigin4 			= '40 -2400 -94';
	coop_vector_spawnOrigin5 			= '110 -2400 -94';
	coop_vector_spawnOrigin6 			= '190 -2400 -94';
	coop_vector_spawnOrigin7 			= '260 -2400 -94';
	coop_vector_spawnOrigin8 			= '330 -2400 -94';
//Set New Objective (Start Co-Op Mod)
	globalCoop_main();
/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////

	soundtrack( "music/training3.mus" );
	setupWorld();
	initTuvok();
	//Singe Player Only
	//--- fade out
	//if(!cvar_bool_multiplayer){cam_fadeout( .1, 0, 0, 0, 1 );}
	
	waitforplayer();
//Set viewModes, but first wait for the player or it won't work in sp or mp
	$world.clearAvailableViewModes();
	$world.addAvailableViewMode("structuralintegrity");
	
	globalCoop_objectives_update("incomplete",1,1);//State,Item,ShowIt
	
	if(doesEntityExist($tuvokDummy)){
		$tuvokDummy.remove();
	}
	
	if(!cvar_bool_multiplayer){//Co-Op
		$player.model("models/char/munro_voyager.tik");
		$player.warp( '256 -2368 -84' );
		$player.loadobjectives( "ent-training3" );
	}	
	area3Start();
//Set Forcefield
	globalCoop_forcefield_makeInteractive( $area3_clipMovement1 );
	globalCoop_forcefield_makeInteractive( $laserShield1_clip );
	globalCoop_forcefield_makeInteractive( $laserShield2_clip );
}

//---------------------
// Function: setupWorld
// Comments:
// world setup varialbles
//---------------------
void setupWorld()
{
	$tuvokPathClip.remove();
	$clipWall.solid();
	wait(.1);
	thread globalCommon_OnDamage( $plasmaConduit1, "destroyPlasmaConduit1" );

	$plasmaConduit1.contents( "+shootable" );
	$plasmaConduit1_fx.hide();

	$laserShield1.loopsound( "sound/environment/energy/electricfield1.wav", 1 ,200 );
	$laserShield2.loopsound( "sound/ships/borg/borg_forcefield2.wav", .7 ,200 );
	$area3_clipMovement1.loopsound( "sound/ships/enterprise/ent_forcefield.wav", .7 ,150 );
	$scanObject.setsize( '0 0 0', '0 0 0' );
	//setupTricorderFilterMode();
	//thread removeWoodStructureWait();

	//---setup archetypes
	float i;
	float totalNumberOfCrystals;

	totalNumberOfCrystals = 18;
	for ( i = 1 ; i<= totalNumberOfCrystals; i++ )
		{
		string tName;
		tName = "archetype_crystalFormation" + i;
		entity crystalEnt;
		crystalEnt = GetEntity( tName );
		globalArchetype_Setup( crystalEnt , "TrainingCrystalFormation" );
		}

	//globalArchetype_Setup( $archetype_crystalFormation1, "TrainingCrystalFormation" );
	
	globalArchetype_Setup( $archetype_panel1, "TrainingPanel" );
	globalArchetype_Setup( $archetype_panel2, "TrainingPanel2" );
	//--set to not trigger end event, done via destruction of rock formation
	$area3_EndTrigger.nottriggerable();
}

//================================================================================
// Area 3
//================================================================================

//---------------------
// Function: area3Start
// Comments:
// area 3 of training start
//---------------------
void area3Start()
{
	$world.playsound( "sound/environment/computer/holo_change.wav", 3 ,1 ,10000 );

//Immobilize
//Singe Player Only
	if(!cvar_bool_multiplayer){thread globalCoop_player_imobilizeAll( 1 );}
	
//Singe Player Only
	if(!cvar_bool_multiplayer){
		//--fade in
		cam_fadein( 1, 0, 0, 0, 1 );
		wait( 1 );
	}
	
	$tuvok.warpTo( "$tuvokWarpToPos1" );
	
//Wait until the warmup time is gone
	wait( ( getCvarInt("mp_warmUpTime") - getlevelTime()) );
	fadeTuvokIn();
	
	thread area3StartDialog();

//Mobilize
	thread globalCoop_player_imobilizeAll( 0 );
}

//---------------------
// Function: area3StartDialog
// Comments:
//
//---------------------
void area3StartDialog()
{
	thread globalCommon_OnUse( $area1bridge_button, "area3bridge_button_OnUse" );
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_interwrld.mp3",2, 20000,0);//vol,dist,time,head
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_moveterm.mp3",2, 20000,0);//vol,dist,time,head
}

//---------------------
// Function: area3bridge_button_OnUse
// Comments:
// on use thread for the button at the extending bridge
//---------------------

void area3bridge_button_OnUse()
{
	$area1bridge_button.nouse();
	$archetype_panel1.remove();
	$area1bridge_button.playsound ( "sound/ships/klingon/kling_beepyes.wav", 5, 1, 500 );
	$area1bridge_button.playsound ( "sound/environment/machine/bigswitch.wav", 6, 1, 500 );
	wait(2);
	$clipWall.remove();
	killthread( "area3StartDialog" );
	killAllTalking();
	wait( .5 );

	thread area3BridgeDialog();
	
//Set Objective Complete
	thread globalCoop_objectives_update("complete",1,0);//State,Item,ShowIt
	
	$area1bridge.time( 7.0 );
	$area1bridge.playsound ( "sound/ships/attrexian/att-elevat_start.wav", 3, 1, 500 );
	$area1bridge.moveSouth( 352 );
	waitfor( $area1bridge );
	$area1bridge.playsound ( "sound/doors/drull_smldoor_stop_01.wav", 3, 1, 500 );
	

}

//---------------------
// Function: area3bridge_button_OnUse
// Comments:
// on use thread for the button at the extending bridge
//---------------------

void area3BridgeDialog()
{
	killAllTalking();
	killthread( "area3StartDialog" );
	
	$tuvok.walkto( "$tuvokWalkToPos1" );
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_excnotehow.mp3",2, 20000,0);//vol,dist,time,head
	//Excellent.  Note how using the panel activated the bridge.  Interacting with the environment will often
	//allow you continue when there seems to be no way to progress.  Now, Lieutenant, cross the bridge and exit
	//through the door on the other side.  We are ready to begin the next exercise.
}

//---------------------
// Function: area3tricorderScanTraining
// Comments:
// Equip player with Tri-Corder
//---------------------
void area3tricorderScanTraining()
{

//multiplayer
	if(cvar_bool_multiplayer){globalCoop_player_giveAll("models/weapons/worldmodel-Tricorder.tik",.5);}
//Only in Single Player
	else{
	float onGround = 0;	
		while ( onGround != 1 ){
			onGround = $player.isplayeronground();
			wait ( .1 );
		}
		$player.give( "models/weapons/worldmodel-Tricorder.tik" );
	}
	//Immobilize Single Player Only
	if(!cvar_bool_multiplayer)
	{
		thread globalCoop_player_imobilizeAll( 1 );
	}


	killAllTalking();
	killthread( "area3BridgeDialog" );
	wait( 1 );
	$tuvok.turntowardsPlayer();
	wait( 1 );
	$tuvok.anim( "sfa_hands_back" );
	wait( .25 );
	//$player.use( "tricorder" );
	$scanObject.setsize( '-96 -96 -96', '96 96 96' );
	wait( 1 );
//Mobilize Single Player Only
if(!cvar_bool_multiplayer)
{
	thread globalCoop_player_imobilizeAll( 0 );
}
//Set New Objective
	thread globalCoop_objectives_update("incomplete",2,1);//State,Item,ShowIt
	
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_yourz.mp3",2, 20000,0);//vol,dist,time,head
	//Like your hazard suit, your tricorder also has an interface to your Tactical Eye Display.  Approach
	//one of the crystal deposits.  Use your tricorder key to activate your tricorder.  Notice that once 
	//your tricorder is active, an identification overly appears around the deposit in your TED.
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_nowaimy.mp3",2, 20000,0);//vol,dist,time,head
	//Now, aim your tricorder at the crystal deposit and press and hold the fire button.
	//This will activate your tricorder's scanning functionality, and provide you with additional
	//information about the object -- An important feature to remember if you find yourself in a…
	//difficult situation.
	
//Skip this in multiplayer
	if(cvar_bool_multiplayer){
		thread scanCrystalComplete();
	}
}

void scanCrystalComplete()
{
	killAllTalking();
	killthread( "area3tricorderScanTraining" );
	$scanObject.remove();
//Set Objective Complete
	thread globalCoop_objectives_update("complete",2,1);//State,Item,ShowIt
	wait( .25 );
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_exw.mp3",2, 20000,0);//vol,dist,time,head
	//Excellent work, Lieutenant.
	wait( 1 );
	thread postTricorderScanObjective();
}

void postTricorderScanObjective()
{
	//Single Player Only
	if(!cvar_bool_multiplayer)
	{
	//Set New Objective
		thread globalCoop_objectives_update("incomplete",3,1);//State,Item,ShowIt
	//Play Dialog
		globalCoop_dialog_play($tuvok,"train2/tuvok_tricofeed.mp3",2, 20000,0);//vol,dist,time,head
		//When active, your tricorder also feeds tactical data to your TED.  Data from the tricorder's
		//annular proximity discriminator is presented in the upper right of your display.  Now, note the
		//green resonance beacon… That beacon represents my location in your environment
		
		$tuvok.headwatchtarget ( "none" , 10 );
		$tuvok.resethead();
		wait( .25 );
		
		$tuvok.walkTo( "$tuvokWalkToPos3" );
		waitfor( $tuvok );
		
		$tuvok.walkTo( "$tuvokWalkToPos2" );
		
		thread startTuvokWatch();
	//Play Dialog
		globalCoop_dialog_play($tuvok,"train2/tuvok_watchbe.mp3",2, 20000,0);//vol,dist,time,head
		//Watch the beacon as I move through the environment.  Notice how it moves to track my relative location.
	//Play Dialog
		globalCoop_dialog_play($tuvok,"train2/tuvok_yourr.mp3",2, 20000,0);//vol,dist,time,head
		//The annular proximity discriminator provides a unique resonance beacon for most object types.
		//For example, non-hostile life forms are marked with a green beacon, while hostile or aggressive
		//ones are red.  Additionally, the discriminator will denote your mission objectives with a gold
		//circle to signify their importance.
//Set Objective Complete
	thread globalCoop_objectives_update("complete",3,1);//State,Item,ShowIt
	}
	fadeTuvokOut();
	
	$area3_clipMovement1.stoploopsound ();
	$area3_clipMovement1.playsound( "sound/ships/enterprise/ent_forcefield_down.wav", 3 ,.7 ,10000 );

	$area3_clipMovement1.remove();
	thread globalCoop_puzzle_add("coop_modulate_replacement","removeLaser", 15 ,'-2626 -2084 68');
}

void startTuvokWatch()
{
	wait( 7 );
	$tuvok.headwatchtarget( "player" , 10 );
	
}
//---------------------
// Function: area3nightVisionTraining
// Comments:
// Equip player with Night Vision Tri-Corder
//---------------------
void area3nightVisionTraining()
{
//Single Player Only
	if(!cvar_bool_multiplayer){
		float onGround = 0;
		while ( onGround != 1 )
		{
			onGround = $player.isplayeronground();
			wait ( .1 );
		}
	//Immobilize
		thread globalCoop_player_imobilizeAll( 1 );
	}

	$tuvok.warpTo( "$tuvokWarpToPos2" );
	
	fadeTuvokIn();
	
//Set Objective Complete
	thread globalCoop_objectives_update("incomplete",4,1);//State,Item,ShowIt

	thread area3nightVisionTrainingDialog();

//Immobilize
	thread globalCoop_player_imobilizeAll( 0 );
}

//---------------------
// Function: area3nightVisionTrainingDialog
// Comments:
// Equip player with Night Vision Tri-Corder
//---------------------
void area3nightVisionTrainingDialog()
{
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_inaddvis.mp3",2, 20000,0);//vol,dist,time,head
	//Your Tactical Eye Display is capable of artificially boosting the gain levels in all photonic registers.
	//This allows you some degree of night vision
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_forthisexcave.mp3",2, 20000,0);//vol,dist,time,head
	//For this exercise, use your night vision key to activate the night vision mode -- then navigate
	//through this cave to the exit on the other side.
}

//---------------------
// Function: area3modulateTraining
// Comments:
// Equip player with Tri-Corder
//---------------------
void area3modulateTraining()
{
//Single Player Only
	if(!cvar_bool_multiplayer){
		float onGround = 0;
		while ( onGround != 1 )
		{
			onGround = $player.isplayeronground();
			wait ( .1 );
		}
	}
//Set Objective Complete
	thread globalCoop_objectives_update("complete",4,0);//State,Item,ShowIt
//Set New Objectives
	thread globalCoop_objectives_update("incomplete",5,1);//State,Item,ShowIt
	
	fadeTuvokOut();
	
	killthread( "area3nightVisionTrainingDialog" );
	killAllTalking();
	$tuvok.warpTo( "$tuvokWarpToPos3" );
	
	fadeTuvokIn();
	
	thread area3modulateTrainingDialog();
}

//---------------------
// Function: area3modulateTrainingDialog
// Comments:
// Equip player with Tri-Corder
//---------------------
void area3modulateTrainingDialog()
{
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_exw.mp3",2, 20000,0);//vol,dist,time,head
	//Excellent work, Lieutenant.
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_recert.mp3",2, 20000,0);//vol,dist,time,head
	//Now, Lieutenant, we will re-certify you on advanced tricorder mechanics.  Activate your
	//tricorder and move to the nearby console marked with an identification overlay in your TED.
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_tricons.mp3",2, 20000,0);//vol,dist,time,head	
	//Modulate the console by first aiming your tricorder at it, then pressing and holding the fire button.
	//Your tricorder will feed progress data to your TED, providing you with constant feedback on your progress.
	//Continue modulating the console until the process completes.
}

//---------------------
// Function: removeLaser
// Comments:
// remove forcefield after modulation
//---------------------
void removeLaser()
{
	$archetype_panel2.remove();
	$laserShield1.remove();
	$laserShield1_clip.remove();
	$laserShield1.stoploopsound();
		
	killAllTalking();
	killthread( "area3modulateTrainingDialog" );

//Set Objective Complete
	thread globalCoop_objectives_update("complete",5,1);//State,Item,ShowIt

//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_satwrksim.mp3",2, 20000,1);//vol,dist,time,head	
	//Satisfactory work, Lieutenant.  On simple consoles, like this one, your tricorder will be able to modulate
	//itself.  However, more sophisticated systems will require user assistance to complete.  Let’s move on.
}

//---------------------
// Function: area3shootPlasmaTraining
// Comments:
// Equip player with phaser
//---------------------
void area3shootPlasmaTraining()
{
//Single Player Only
	if(!cvar_bool_multiplayer){
		float onGround = 0;
		while ( onGround != 1 )
			{
			onGround = $player.isplayeronground();
			wait ( .1 );
			}
	}
//ImmobilizeSingle Player Only
	if(!cvar_bool_multiplayer)
	{
		thread globalCoop_player_imobilizeAll( 1 );
	}
	killthread( "area3modulateTrainingDialog" );
	killthread( "removeLaser" );
	killAllTalking();
	
	fadeTuvokOut();
	
	$tuvok.warpTo( "$tuvokWarpToPos4" );
	wait( 1 );
	
	fadeTuvokIn();
	
	thread area3shootPlasmaTrainingDialog();

//Set New Objective
	thread globalCoop_objectives_update("incomplete",6,1);//State,Item,ShowIt
	
//Give weapon to players who join later on the sever as well
	coop_string_weapon2 	= "models/weapons/worldmodel-Phaser-stx.tik";
//globalCoop_applayToAll();
//Give Phaser
	globalCoop_player_giveAll("models/weapons/worldmodel-Phaser-stx.tik", -1);
//MobilizeSingle Player Only
	if(!cvar_bool_multiplayer)
	{
		thread globalCoop_player_imobilizeAll( 0 );
	}
}

//---------------------
// Function: area3shootPlasmaTrainingDialog
// Comments:
// Equip player with phaser
//---------------------
void area3shootPlasmaTrainingDialog()
{
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_actiph.mp3",2, 20000,1);//vol,dist,time,head
	//Activate your phaser by pressing the weapon group 1 key.  Your tricorder will lower, your view
	//will return to normal, and your phaser should be readied.
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_redimp.mp3",2, 20000,1);//vol,dist,time,head
	//Notice the red forcefield impeding your progress.  To disable the forcefield, you must shoot the
	//plasma conduit that powers it. When you are ready, press the fire key to destroy the conduit.
	//As you fire, notice how the energy register on your lower right rapidly decreases… This energy
	//bar measures the current energy count for your currently active weapon… In this case, the phaser.

}

//---------------------
// Function: area3filterModeTraining
// Comments:
// Equip player with structural integrity mode
//---------------------
void area3filterModeTraining()
{
	if(!cvar_bool_multiplayer){
		float onGround = 0;
		while ( onGround != 1 )
			{
			onGround = $player.isplayeronground();
			wait ( .1 );
			}
	}
//ImmobilizeSingle Player Only
	if(!cvar_bool_multiplayer)
	{
		thread globalCoop_player_imobilizeAll( 1 );
	}
	
	fadeTuvokOut();

	killthread( "area3shootPlasmaTrainingDialog" );
	killAllTalking();
	$tuvok.warpTo( "$tuvokWarpToPos5" );
	wait( .5 );
	
	fadeTuvokIn();
	
	thread area3filterModeTrainingDialog();

//Set viewModes
	$world.clearAvailableViewModes();
	$world.addAvailableViewMode( "nightvision" );

//Set New Objective
	thread globalCoop_objectives_update("incomplete",7,1);//State,Item,ShowIt

//MobilizeSingle Player Only
	if(!cvar_bool_multiplayer)
	{thread globalCoop_player_imobilizeAll( 0 );}
}

//---------------------
// Function: area3filterModeTrainingDialog
// Comments:
// Equip player with structural integrity mode
//---------------------
void area3filterModeTrainingDialog()
{
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_tedtri.mp3",2, 20000,1);//vol,dist,time,head
	//Through its interface to your TED, your tricorder can provide you with additional viewmodes to
	//analyze your environment.  With your tricorder active, press the alternate fire button.
	//Notice how your view changes.  Your tricorder is feeding information on the structural integrity
	//of nearby objects directly to your TED.  In this viewmode you can see stress fractures and
	//structural weaknesses that would normally go unnoticed.
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_struint.mp3",2, 20000,1);//vol,dist,time,head
	//While you are in the structural integrity view mode, notice how the conduit stands out in your display.
	//That denotes a structural flaw or stress fracture in the object.  Objects with structural flaws are
	//often susceptible to weapons fire.
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_aimyph.mp3",2, 20000,1);//vol,dist,time,head
	//Aim your phaser at the conduit you identified with the structural integrity mode.  When you are
	//ready, press the fire key to destroy the conduit.  As you fire, notice how the energy register
	//on your lower right rapidly decreases… This energy bar measures the current energy count for
	//your currently active weapon… In this case, the phaser.
}

//---------------------
// Function: destroyPlasmaConduit1
// Comments:
// remove forcefield after destroying the plasma conduit
//---------------------
void destroyPlasmaConduit1()
{	
	$plasmaConduit1.nodamage();
	if(doesEntityExist($plasmaConduit1_explode)){trigger( "$plasmaConduit1_explode" );}
	$plasmaConduit1.model( "enviro/borg-sphere_plasmacon_plasmacon_damaged.tik" );
	$plasmaConduit1.notsolid();
	$plasmaConduit1.contents( "-targetable" );
	$plasmaConduit1_fx.show();
	$laserShield2.remove();
	$laserShield2_clip.remove();
	$laserShield2.stoploopsound();
//Set Objective Complete
	thread globalCoop_objectives_update("complete",6,1);//State,Item,ShowIt
	thread destroyPlasmaDialog();
}

void destroyPlasmaDialog()
{
	killthread( "area3shootPlasmaTrainingDialog" );
	killAllTalking();
	wait( 1 );
	
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_exw.mp3",2, 20000,1);//vol,dist,time,head
	//Excellent work, Lieutenant.

	wait( .5 );
	
	fadeTuvokOut();

}
//---------------------
// Function: area3_End
// Comments:
// end level
//---------------------
void area3_End()
{
	
//Immobilize
	thread globalCoop_player_imobilizeAll( 1 );
	
//Set Objective Complete
	thread globalCoop_objectives_update("complete",7,1);//State,Item,ShowIt
	
	//--- setup the holodeck
	$holoDeck.bind( $nodeHoloDeckCenter );
	$holoDeck.rendereffects( "+depthhack" );
	$holoDeck.notSolid();
	$holoDeck.forcealpha( 1 );
	$holoDeck.alpha( 0 );
	$holoDeck.show();
	wait( .5 );
	
	//$player.setobjectivecomplete( "integrityT3", 1 );
	
	killthread( "area3filterModeTrainingDialog" );
	killAllTalking();
	wait( 1 ); 
//Play Dialog
	globalCoop_dialog_play($tuvok,"train2/tuvok_satltmv.mp3",2, 20000,0);//vol,dist,time,head	
	//Satisfactory, Lietuenant.  Let us move on to the weapons examination.

	wait( .2 );
	
	fadeTuvokOut();

	if(cvar_bool_multiplayer){//Multi Player Mode
		wait(2);
		coop_endLevel();
	//Exit
		return;
	}
	else{
		$nodeHoloDeckCenter.origin( $player.getOrigin() );
		$nodeHoloDeckCenter.angles( $player.getAngles() );
		wait( .1 );	
		$holoDeck.fadein( 1, 1 );
		$world.playsound( "sound/environment/computer/holo_change.wav", 3 ,1 ,10000 );
		wait( 1 );
		
		//--tell the game to not translate the player's orientation
		dontSaveOrientation();
		
		spawn ( "trigger_changelevel", "targetname", "trigger_endlevel", "map", "ent-training4" );
		wait ( .5 );
		if(doesEntityExist($trigger_endlevel)){trigger( "$trigger_endlevel" );}
	}
}

void incrementRockCounter()
{
	if (rockDestroyed >= 4)
	{
		thread area3_End();
	}
	else
	{
		rockDestroyed++;
	}
}

/*
//------------------------
// Function: setupTricorderFilterMode
// Comments:
// setup viewmode for struc integ filter for wooden blockade
//------------------------
void setupTricorderFilterMode()
{
	$woodStructure1_crack.notsolid();
	$woodStructure1_crack.viewmode( "structuralintegrity" );
}

//---------------------
// Function: removeWoodStructureWait
// Comments:
// wait thread for removing the wood
//---------------------
void removeWoodStructureWait()
{
	$woodStructure1.ondamage( "removeWoodStructure" );
	$woodStructure1.playsound( "sound/impact/explosion/expl_inside3.wav", 3 ,.7 ,300 );
	pause();
}

//---------------------
// Function: removeWoodStructure
// Comments:
// removes the wood when it is shot
//---------------------
void removeWoodStructure()
{

	$woodStructure1.nodamage();
	wait( .1 );
	$woodStructure1_crack.remove();
	$woodStructure1.remove();
	
	$player.setobjectivecomplete( "integrityT3", 1 );
}
*/

void coop_endLevel()
//------------------------------------------------------------------------------
//Level end
//------------------------------------------------------------------------------
{
	thread globalCoop_mission_completed("ent-training4");
}

