float NUM_ENEMIES = 6;

void main();
void setupAI();
void adaptAI();
void justLoadedDeckThreadIGM4();
void IGM4IsOver();

void holoDeckSetup();
void holoDeckFadeOut();
void holoDeckFadeIn();
void givePlayerAmmo();
void coop_endLevel();

//Co-op
/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////
#include "coop_mod/maps/main.scr"
#include "coop_mod/maps/global/global_common.scr"
/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////
#include "maps/global_scripts/global_math.scr"

//------------------------
// this sets up Holo Mission IGM4
//------------------------
void main()
{
//Set Soundtrack for this level
	soundtrack( "music/training_weapon.mus" );
//Co-Op
/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////
	coop_vector_lmsStartedPausedWarmuptime_z = 10;//set lms warmuptime to 10 no matter what else is setup
	coop_string_nextMapToCheck			= "ent-training_weap3";//set the map we gona load next while we are in testmode
//Set spawnangles for this level
	coop_float_spawnAngle0 				= 90;
	//coop_vector_spawnOrigin12			= '3480 -1339 320';
	//coop_vector_spawnOrigin11			= '3540 -1339 320';
	//coop_vector_spawnOrigin10			= '3600 -1339 320';
	//coop_vector_spawnOrigin9			= '3660 -1339 320';
	coop_vector_spawnOrigin8			= '3720 -1339 320';
	coop_vector_spawnOrigin7			= '3480 -1250 320';
	coop_vector_spawnOrigin6			= '3540 -1250 320';
	coop_vector_spawnOrigin5			= '3600 -1250 320';
	coop_vector_spawnOrigin4			= '3660 -1250 320';
	coop_vector_spawnOrigin3			= '3720 -1250 320';
	coop_vector_spawnOrigin2			= '3480 -1170 320';
	coop_vector_spawnOrigin1			= '3540 -1170 320';
//Definie Objective
	coop_string_objectiveItem1			= "IGM4HoloDeck";
/* 	coop_string_objectiveItem1			= "IGM1VisitArmory";
	coop_string_objectiveItem2			= "IGM1VisitBreifing";
	coop_string_objectiveItem3			= "IGM1VisitLockers";
	coop_string_objectiveItem4			= "IGM1HoloDeck";
	coop_string_objectiveItem5			= "IGM1GoToBridge"; */
//definie weapons
	coop_string_weapon1 = "models/weapons/worldmodel-GrenadeLauncher.tik";
//Start the Co-Op Script
	globalCoop_main();
/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////
	spawn("script_model","model","sky/sky_stardome-nebula.tik","origin","-448.00 -256.00 640.00","scale",".25");
	thread holoDeckSetup();
	setupAI();
	waitforplayer();

	if(!cvar_bool_multiplayer){//Singleplayer
		$holoDeck.origin ( $player.getorigin () );
	}
	justLoadedDeckThreadIGM4();
	holoDeckFadeOut();
}

void setupAI()
{
	entity e;
	float i;

	for( i = 1; i <= NUM_ENEMIES; i++ )
	{
		e = getentity( "enemy" + i );
		if( doesEntityExist( e ) )
		{
			// group them together and set a groupdeaththread that transitions back to IGM after
			// they are all killed
			e.setgroupid( 123 );
			e.groupdeaththread( "IGM4IsOver" );
			e.setFloatVar("coop_defaultHealth",e.getHealth());
			// set their wander tendency to 0 so they don't move around
			e.settendency( "wander", 0.0 );
		}
	}
}

void coop_newPlayerEntered()
//----------------------------------------------------------------------
//Welcome Player to this map
//----------------------------------------------------------------------
{
	thread adaptAI();
}


void adaptAI()
{
entity e;
float i;

	for( i = 1; i <= NUM_ENEMIES; i++ )
	{
		e = getentity( "enemy" + i );
		if( doesEntityExist( e ) )
		{
		//Update health per player
			e.health(e.getFloatVar("coop_defaultHealth"));
			globalCoop_actor_healthMultiplicatePerPlayer(e);
		}
	}
}


//---------------------
// justLoadedDeckThreadIGM4
// Triggered and check for mission objectives on IGM4
//------------------------
void justLoadedDeckThreadIGM4()
{
	if(!cvar_bool_multiplayer){//Singleplayer
		$player.loadobjectives( "IGM4" );
		wait( .05 );

		if( getfloatvar( "game.IGM4GoToTelsia" ) >= 1 )
		{
			$player.loadobjectives("IGM4alt");

			$player.setobjectiveshow( "IGM4GoToTelsia", 1 );
		}
		else
		{
			$player.loadobjectives("IGM4");
		}
		if( getfloatvar( "game.IGM4GoToTelsia" ) == 2 )
		{
			$player.setobjectivecomplete( "IGM4GoToTelsia", 1 );
		}

		if( getfloatvar( "game.IGM4TalkWithKleeya" ) >= 1 )
		{
			$player.setobjectiveshow( "IGM4TalkWithKleeya", 1 );
		}
		if( getfloatvar( "game.IGM4TalkWithKleeya" ) == 2 )
		{
			$player.setobjectivecomplete( "IGM4TalkWithKleeya", 1 );
		}

		if( getfloatvar( "game.IGM4GoToBridge" ) >= 1 )
		{
			$player.setobjectiveshow( "IGM4GoToBridge", 1 );
		}
		if( getfloatvar( "game.IGM4GoToBridge" ) == 2 )
		{
			$player.setobjectivecomplete( "IGM4GoToBridge", 1 );
		}

		if( getfloatvar( "game.IGM4GoToArmory" ) >= 1 )
		{
			$player.setobjectiveshow( "IGM4GoToArmory", 1 );
		}
		if( getfloatvar( "game.IGM4GoToArmory" ) == 2 )
		{
			$player.setobjectivecomplete( "IGM4GoToArmory", 1 );
		}
		if( getfloatvar( "game.IGM4HoloDeck" ) >= 1 )
		{
			$player.setobjectiveshow( "IGM4HoloDeck", 1 );
		}
		if( getfloatvar( "game.IGM4HoloDeck" ) == 2 )
		{
			$player.setobjectivecomplete( "IGM4HoloDeck", 1 );
		}
	}
	else{
		globalCoop_objectives_update("inComplete",1,1);	
	}
}

//------------------------
// called when bug is Dead to end level
//------------------------
void IGM4IsOver()
{
	holoDeckFadeIn();

	$teleportTrigger.remove();

	wait( .5 );

	$world.playsound( "sound/player/player_invdrop.wav", 3, 1, 10000 );
	thread globalCoop_player_takeAll(coop_string_weapon1);
	thread globalCoop_main_globalCommand("health");

	if(!cvar_bool_multiplayer){//Singleplayer
		$player.setobjectivecomplete( "IGM4HoloDeck", 1 );
	}
	else{
		globalCoop_objectives_update("complete",1,1);
	}
	
	setFloatVar( "game.IGM4HoloDeck", 2 );

	if(!cvar_bool_multiplayer){//Singleplayer
		globalCommon_SpawnActor( "char/hazardteam_sf-kourban.tik", "korban", '0 0 0', 0 );
		$korban.hide();

		globalCommon_SpawnActor( "char/starfleet_picard.tik", "picard",	'0 0 0', 0 );
		$picard.hide();
		
		globalCommon_SpawnActor( "char/hazardteam_sf-munro.tik", "munro",	'0 0 0', 0 );
		$munro.hide();
	}
	else{
		spawn("script_model","model","fx/fx-dummy.tik","targetname","picard");
		spawn("script_model","model","fx/fx-dummy.tik","targetname","korban");
		spawn("script_model","model","fx/fx-dummy.tik","targetname","munro");
	}

	wait( .05 );
//Almost as good as a Klingon!
	globalCoop_dialog_play($korban,"ent_m7/korban_almost.mp3",1, 20000,1);
	wait( .5 );
//Lieutenant Munro.  Please report to the Bridge.
	globalCoop_dialog_play($picard,"ent_m1/picard_munro.mp3",1, 20000,1);
	wait( .5 );
//Aye sir.
	globalCoop_dialog_play(globalCoop_return_playerClosestPreferActive($world),"ent_m1/munro_mhere.mp3",1, 20000,1);//$scriptmunro
	wait( 2 );



	if(!cvar_bool_multiplayer){//Singleplayer
		cam_fadeout( .5, 0, 0, 0, 1, 0 );
		wait( .6 );	
		spawn ( "trigger_changelevel", "targetname", "trigger_endTraining", "map", "ent-deck1_bridge" );
		wait( .05 );
	}
	
	// Setup Level Vars
	//setfloatvar ("game.checkEntMission4RoomsVisited", 9 );

	setfloatvar("game.checkEntMission4RoomsVisited", 10 );

	wait ( .55 );
	setfloatvar( "game.globalMissionEnterprise", 5 );
	
	if(!cvar_bool_multiplayer){//Singleplayer
		trigger ( "$trigger_endTraining" );
	}
	else{
		coop_endLevel();
	}
}

//------------------------
// this thread sets up the Holo Deck And Player
//------------------------
void holoDeckSetup()
{
	if(!cvar_bool_multiplayer){//Singleplayer
		cam_fadeout( .05, 0, 0, 0, 1, 0 );
	}
	level_ai ( 0 );
	$holoDeck.notsolid();
	$holoDeck.rendereffects( "+depthhack" );
	$holoDeck.forcealpha( 1 );
	if(cvar_bool_multiplayer){//Multiplayer
		$holoDeck.warp('3582 -1080 318');
	}
	else{
		waitForPlayer();
		$player.immobilize( 1 );
		$holoDeck.warp($player.getOrigin());
	}
}

//------------------------
// this thread makes the Holo Deck Fade Out
//------------------------
void holoDeckFadeOut()
{
	waitForPlayer();
	if(!cvar_bool_multiplayer){//Singleplayer
		cam_fadeIn( .5, 0, 0, 0, 1, 0 );
	}
	globalCoop_main_waitForWarmupTime();
	wait( 1 );
	$holoDeck.fade( 2, 0 );
	wait( 2 );
	$holoDeck.hide();
	$holoDeck.alpha( 0 );

	if(!cvar_bool_multiplayer){//Singleplayer
		$player.immobilize( 0 );
	}
	thread givePlayerAmmo();
	level_ai ( 1 );
}

//------------------------
// this thread makes the Holo Deck Fade Out
//------------------------
void holoDeckFadeIn()
{
	$lift01.wait( -1 );
	//$lift01.targetName( "0" );	//this will crash the engine...don't change targetnames on func_ objects :)
	$lift01_Trigger.nottriggerable ();

	if(cvar_bool_multiplayer){//Multiplayer
		globalCoop_objectives_update("complete",1,1);
	}
	else{
		$player.setobjectivecomplete("IGM4HoloDeck",1);
	}
	wait( 3 );

	if(!cvar_bool_multiplayer){//Singleplayer
		$player.gravity( 0 );
		$player.immobilize( 1 );

		$holoDeck.origin( $player.getorigin() );

		$holoDeck.show();
		$holoDeck.fadein( 2, 1 );
		wait( 2 );
		//$holoDeck.alpha( 1 );
	}
}

void givePlayerAmmo()
{
	while( 1 )
	{
		globalCoop_main_globalCommand("ammo");
		wait( 5 );
	}
}


void coop_endLevel()
//------------------------------------------------------------------------------
//Level end
//------------------------------------------------------------------------------
{
	setfloatvar ("game.checkEntMission4RoomsVisited", 10 );
	setfloatvar ( "game.globalMissionEnterprise", 4 );
	if(cvar_bool_multiplayer)
	{
		stuffCMD("seta coop_igm 4\n");
		stuffCMD("seta coop_igmH 1\n");
		stuffCMD("seta coop_igmR 10\n");
	}
	globalCoop_main_camFadeOut(1);
	wait(.2);
	thread globalCoop_mission_completed("ent-deck1_bridge");
}

