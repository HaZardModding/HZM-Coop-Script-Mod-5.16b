//-------------------------------------------
// EF2 Level Script File
//
// Level: m7l2c-attrexian_colony
// Script by: Jerry K, Benson R, R. 'Charon' Heath
// Geometry by: Jerry K, Todd R
// Created on: 18 July, 2002
//
// Last Edited: R. 'Charon' Heath
//--------------------------------------------

void main();
void init();

void setupShuttle();
void setupArchetypes();

void moveAxisAlignedEntity( string entityName, float totalTime, float totalDistance, string moveDirection );

void factory_backdoor_switch_wait();
void factory_backdoor_action();
void factory_backdoor_piston_up();
void factory_backdoor_piston_down();

void platformLift_wait();
void platformLift_move();
void platformLift_downWait();
void platformLift_reject();

void platformShuttle_takeoff();
void platformQuad_hitShuttle();
void platformQuad_land();
void platformQuad_death();

void factory_elevator_switch1_check();
void factory_elevator_up();
void factory_elevator_door1_open();
void factory_elevator_switch2_wait();
void factory_elevator_door1_close();
void factory_elevator_down();
void factory_elevator_door2_open();

void gdoor_open();
void gdoor_close();
void gdoor_closed_switch_wait();
void gdoor_open_switch_wait();

void citydoor1_switch_wait();
void citydoor1_open();
void citydoor1_close();
void citydoor2_switch_wait();
void citydoor2_open();
void citydoor2_close();
void crashSiteAliens();
void endAliensDeath();

void shuttle_flyby_thread();
void shuttle_explosion_thread();
void crash_site_dialog();

void randomLightningFlash();
void randomExplosionSound();

void attrexianDoorBeepAccept();
void attrexianDoorBeepReject();
void attrexianAutoSave();

void attrexianDeathFallOff();
void attrexianDeathFallOn();
void coop_waitForTeam();
void coop_endLevel();

float shuttleIsHit = 0;
float endAliensKilled = 0;

//--- cinematic armature
entity cinematicArm_Shuttle;

//==========================================================================================
// Includes
//==========================================================================================
#include "coop_mod/maps/main.scr"
#include "coop_mod/maps/optional_modules/earthquake.scr"
#include "coop_mod/maps/global/global_common.scr"
#include "maps/global_scripts/global_debugUtils.scr"
//#include "maps/global_scripts/global_transporter.scr"
#include "coop_mod/maps/global/global_flyin.scr"
#include "maps/global_scripts/global_archetype.scr"
#include "coop_mod/maps/missions/7/m7l2c_cin.scr"


//==========================================================================================
// Main Stuff
//==========================================================================================
//---------------------
// Function: main
// Comments:
// main thread called at the start of the level
//---------------------
void main()
{
	soundtrack( "music/m7l1-attrexian_colony.mus" );
	$sky.rendereffects( "+skyorigin" );
	//$world.entity_fade_dist( 6000 );
	//$world.farplane_cull( 1 );
	//$world.farplane_color ( '0.0875 0.1 0.145' );
	//$world.farplane ( 4000 );

	$world.weather( "rain", 400 );
	//$world.canShakeCamera( 1 );
//Co-Op
/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////	
	coop_string_nextMapToCheck			= "m8l1a-drull_ruins2";//set the map we gona load next while we are in testmode
//Definie Objectives
/////////////////////////////////////////////////////////////////////////
	coop_string_objectiveItem1			= "EnterFactory";
	coop_string_objectiveItem2			= "ExploreFactory";
	coop_string_objectiveItem3			= "RestoreLiftPower";
	coop_string_objectiveItem4			= "FindElderlyIdryll";
	coop_string_objectiveItem5			= "InvestigateCrash";
//Give each player a Item/weapon, the integer stands for the player-ID
/////////////////////////////////////////////////////////////////////////
	coop_string_weapon6 				= "models/weapons/worldmodel-Tricorder-stx.tik";
	coop_string_weapon5 				= "models/weapons/worldmodel-Phaser-stx.tik";
	coop_string_weapon4					= "models/weapons/worldmodel-BurstRifle.tik";
	coop_string_weapon3 				= "models/weapons/worldmodel-FieldAssaultRifle.tik";
	coop_string_weapon2 				= "models/weapons/worldmodel-GrenadeLauncher.tik";
	coop_string_weapon1 				= "models/weapons/worldmodel-attrex-rifle.tik";
//Set spawnangles for this level
/////////////////////////////////////////////////////////////////////////
	coop_float_spawnAngle0 			= 270;//SpawnOrigin0 Angle
//spawnorigins, Spawn Players on those locations, at map start
/////////////////////////////////////////////////////////////////////////
	coop_vector_spawnOrigin1 			= '-1710 4070 1380';
	//coop_vector_spawnOrigin1 			= '-1398 -38404 200';
	coop_vector_spawnOrigin2 			= '-1790 4070 1380';
	coop_vector_spawnOrigin3 			= '-1640 4160 1380';
	coop_vector_spawnOrigin4 			= '-1710 4160 1380';
	coop_vector_spawnOrigin5 			= '-1790 4160 1380';
	coop_vector_spawnOrigin6 			= '-1640 4250 1380';
	coop_vector_spawnOrigin7 			= '-1710 4250 1380';
	coop_vector_spawnOrigin8 			= '-1790 4250 1380';
//Inizial the co-Op Mod
/////////////////////////////////////////////////////////////////////////
	globalCoop_main();
/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////
	
	thread init();
	if(cvar_bool_multiplayer){//multiplayer
	//spawn Class Selection
		thread globalCoop_class_setup("Medic",'-1000 3561 832');
		thread globalCoop_class_setup("HeavyWeapons",'-1075 3561 832');
		thread globalCoop_class_setup("Technician",'-1150 3561 832');
		
		globalCoop_objectives_update("complete",1,0);
		globalCoop_objectives_update("complete",2,0);
		globalCoop_objectives_update("complete",3,0);
		thread globalCoop_objectives_update("incomplete",4,1);
	}
	else{//singleplayer
		waitForPlayer();
		$player.loadobjectives( "m7l2-attrexian_colony" );
		wait( .1 );

		$player.setobjectiveshow( "EnterFactory", 1 );
		$player.setobjectiveshow( "ExploreFactory", 1 );
		$player.setobjectiveshow( "RestoreLiftPower", 1 );
		wait( .1 );
		$player.setobjectivecomplete( "EnterFactory", 1 );
		$player.setobjectivecomplete( "ExploreFactory", 1 );
		$player.setobjectivecomplete( "RestoreLiftPower", 1 );
		$player.setobjectiveshow( "FindElderlyIdryll", 1 );
	}
}

//---------------------
// Function: init
// Comments:
// initilization thread called by main
//---------------------
void init()
{
	$factory_backdoor.dmg( 16384 );
	
	spawn("trigger_multiple","thread","platformLift_callLift","targetname","coop_liftTriggerTop","origin","-1710 3401 1400","nottriggerable","1");
	spawn("trigger_multiple","thread","platformLift_callLift","targetname","coop_liftTriggerBottom","origin","-1719 3111 900","nottriggerable","1");
	thread setupShuttle();
	thread setupArchetypes();

	//setup the cinematic characters
	$cinematicMunro.ai_off();
	$sydney.ai_off();
	$inigor.ai_off();
	$picard.ai_off();
	globalCoop_level_hideAndNotSolid($sydney);
	globalCoop_level_hideAndNotSolid($inigor);
	globalCoop_level_hideAndNotSolid($picard);
	globalCoop_level_hideAndNotSolid($cinematicMunro);
	$cinematicMunro.targetname("Munro");//fix the STUPID targetname...

	//--- setup sky stuff
	$starDome.hide();
	$starDome.angles ( '45 0 0' );

	//--- setup the cinematic shuttles
	$shuttleSpace_Body.bind( $shuttleSpace_Origin );
	$shuttleSpace_Door.bind( $shuttleSpace_Origin );
	$shuttleSpace_Body.scale( .01 );
	$shuttleSpace_Door.scale( .01 );

	//setup the doors and lifts
	$factory_elevator_leftdoor.bind ( $factory_elevator0 );
	$factory_elevator_rightdoor.bind ( $factory_elevator0 );
	$factory_elevator_switch2.bind ( $factory_elevator0 );
	$arch_elevator.bind( $factory_elevator0 );

	$platformLift_gate.bind( $platformLift );
	$platformLift_switch.bind( $platformLift );
	$platformLift_clip.bind( $platformLift );
	$arch_platformLift.bind( $platformLift );

	$platformQuadFlame.hide();
	$factory_backdoor_portal.hide();
	globalCoop_level_hideAndNotSolid($citydoor1_portalblock);
	globalCoop_level_hideAndNotSolid($citydoor2_portalblock);
	
	$warning_trigger.nottriggerable();
	$shuttle_landing.nottriggerable();
	if(doesEntityExist($end_trigger)){$end_trigger.nottriggerable();}

	$crashSiteTrigger.nottriggerable();

	thread platformLift_wait();
	thread factory_elevator_door2_open();
	thread gdoor_closed_switch_wait();
//	thread factory_backdoor_switch_wait();
	thread citydoor1_switch_wait();
	thread citydoor2_switch_wait();

	thread randomExplosionSound();
	thread randomLightningFlash();

	wait( 1 );
	$coop_liftTriggerTop.setSize('-128 -128 0','128 128 64');
	$coop_liftTriggerBottom.setSize('-128 -128 0','128 128 64');
	thread factory_backdoor_action();
}


//==========================================================================================
// Setup Functions
//==========================================================================================
//---------------------
// Function: setupShuttle
// Comments:
// setups the shuttle
//---------------------
void setupShuttle()
{
	$drull_shuttle_clip.solid();
	$drull_shuttle.notsolid();
	$drull_shuttle_int.bind( $drull_shuttle );
	$drull_shuttle_int.anim( "closed" );
	$drullShuttleClip.bind( $drull_shuttle );
	$drull_shuttle.bind( $platformShuttle_origin );

	$shuttle_flyby_door.bind( $shuttle_flyby );
	globalCoop_level_hideAndNotSolid($shuttle_flyby_door);

	globalCoop_level_hideAndNotSolid($shuttle_end);
	globalCoop_level_hideAndNotSolid($shuttle_end_door_open);
	if(doesEntityExist($shuttle_end_clip)){$shuttle_end_clip.notsolid();}
	if(doesEntityExist($shuttle_end_door_open)){$shuttle_end_door_open.bind( $shuttle_end );}
	if(doesEntityExist($shuttle_end_clip)){$shuttle_end_clip.bind( $shuttle_end_clip_org );}

	if(doesEntityExist($end_shuttle_mover_door)){$end_shuttle_mover_door.bind( $end_shuttle_mover );}
	
	globalCoop_level_hideAndNotSolid($end_shuttle_mover);
	globalCoop_level_hideAndNotSolid($end_shuttle_mover_door);
	$idryllshuttle_int.anim( "closed" );
}

//---------------------
// Function: setupArchetypes
// Comments:
// setups the archetypes in the level
//---------------------
void setupArchetypes()
{
	$arch_doorpanel1.contents( "targetable" );
	$arch_doorpanel1.archetype( "AttrexianDoorPanel" );

	$arch_platformLift.contents( "targetable" );
	$arch_platformLift.archetype( "AttrexianLiftControl" );

	$arch_elevator.contents( "targetable" );
	$arch_elevator.archetype( "AttrexianLiftControl" );
}


//==========================================================================================
// Generic
//==========================================================================================
//------------------------------------------------------------------
// Function: moveAxisAlignedEntity
// Comments:
// moves an entity with acceleration and deceleration.
//
// Parameters:
//   entity name - targetname of entity .
//
//   totalTime - A floating point value that represents the
//   the total time it takes the door to move the specified distance.
//
//   totalDistance - The total distance the door will move.
//
//   moveDirection - The direction the door will move. Valid values are:
//       up
//       down
//		 north
//		 south
//       east
//       west
//------------------------------------------------------------------
void moveAxisAlignedEntity( string entityName, float totalTime, float totalDistance, string moveDirection )
{
	entity entityReference;
	entityReference = getEntity( entityName );

	//
	// IMPORTANT timeStep should be about .1 seconds.
	// Otherwise, you may get jerks at low frame rates (fps < 20).
	//

	float iterations = 100;		// Number of iterations
	float halfDistance;			// Distance at which transition occurs
	float transitionStep;		// Step at which transition from acceleration to deceleration occurs
	float timeStep;				// Time duration for a single step
	timeStep = totalTime / iterations;
	transitionStep = iterations / 2;
	halfDistance = totalDistance / 2;

	float sum;
	float distanceInc;
	sum = (transitionStep * (transitionStep + 1)) / 2;
	distanceInc = halfDistance / sum;

	float moveDir = 0;
	if ( moveDirection == "up" )
	{
		moveDir = 0;
	}

	else if ( moveDirection == "down" )
	{
		moveDir = 1;
	}
	else if ( moveDirection == "west" )
	{
		moveDir = 2;
	}
	else if ( moveDirection == "east" )
	{
		moveDir = 3;
	}
	else if ( moveDirection == "north" )
	{
		moveDir = 4;
	}

	else
	{
		centerprint( "Invalid move direction" );
	}

	float idx;
	float moveInc = 0;
	for ( idx = 0; idx < iterations; idx += 1)
	{
	 	if ( idx < transitionStep )
	 	{
	 		moveInc += distanceInc;
	 	}
	 	else
	 	{
	 	    moveInc -= distanceInc;
	 	}
		entityReference.time( timeStep );
		if ( moveDir == 0 )
		{
			entityReference.moveUp( moveInc );
		}
		else if ( moveDir == 1 )
		{
			entityReference.moveDown( moveInc );
		}
		else if ( moveDir == 2 )
		{
			entityReference.moveWest( moveInc );
		}
		else if ( moveDir == 3 )
		{
			entityReference.moveEast( moveInc );
		}
		else
		{
			entityReference.moveNorth( moveInc );
		}
		waitfor ( entityReference );
	}
}


//==========================================================================================
// entrance door functions
//==========================================================================================
//---------------------
// Function: factory_backdoor_switch_wait
// Comments:
// wait thread for the entrance door
//---------------------
void factory_backdoor_switch_wait()
{
	$factory_backdoor_switch.onuse( "factory_backdoor_action" );
	pause();
}

//---------------------
// Function: factory_backdoor_action
// Comments:
// entrance door move
//---------------------
void factory_backdoor_action()
{
	$factory_backdoor_switch.nouse();

	// move door up
	float threadNum;
	wait ( .1 );

	$factory_backdoor_portal.openportal();
	$factory_backdoor.playsound ( "sound/ships/attrexian/att-beepconfirm.wav", 3, .4, 500 );
	$factory_backdoor.playsound ( "sound/ships/attrexian/att-colonydoor1.wav", 4, 1, 500 );
	thread moveAxisAlignedEntity( "factory_backdoor" , 2, 160, "up" );
	thread factory_backdoor_piston_up();
	wait ( 8 );

	// move door down
	$factory_backdoor.playsound ( "sound/ships/attrexian/att-colonydoor1.wav", 4, 1, 500 );
	threadNum = thread moveAxisAlignedEntity( "factory_backdoor" , 2, 160, "down" );
	thread factory_backdoor_piston_down();
	waitforthread( threadNum );

	$factory_backdoor_portal.closeportal();
	wait ( .1 );

	thread factory_backdoor_switch_wait();

}

//---------------------
// Function: factory_backdoor_piston_up
// Comments:
// entrance door pistons move up
//---------------------
void factory_backdoor_piston_up()
{
	wait ( .5 );
	float threadNum;
	thread moveAxisAlignedEntity( "factory_backdoor_piston" , 1, 80, "up" );
	threadNum = thread moveAxisAlignedEntity( "factory_backdoor_piston2" , 1, 80, "up" );
	waitforthread( threadNum );
}

//---------------------
// Function: factory_backdoor_piston_down
// Comments:
// entrance door pistons move down
//---------------------
void factory_backdoor_piston_down()
{
	wait ( .5 );
	float threadNum;
	thread moveAxisAlignedEntity( "factory_backdoor_piston" , 1, 80, "down" );
	threadNum = thread moveAxisAlignedEntity( "factory_backdoor_piston2" , 1, 80, "down" );
	waitforthread( threadNum );
}


//==========================================================================================
// drull shuttle and quad functions
//==========================================================================================
//---------------------
// Function: platformShuttle_takeoff
// Comments:
// drull shuttle on the plaform takes off and is struck by a quad
//---------------------
void platformShuttle_takeoff()
{
	float shuttleShrinking , shuttleScale , shuttleSomething;

	shuttleShrinking = 1;
	shuttleScale = 1;
	shuttleSomething = 0;

	$drull_shuttle.playsound ( "sound/vehicles/shuttlecraft/shuttle_accelerate.wav", 16, 1, 2048 );
	$platformShuttle_origin.followpath( $platformShuttle_path1 );

	$drull_shuttle_clip.time( 8 );
	$drull_shuttle_clip.moveUp( 256 );

	wait( 8 );
	$drull_shuttle_clip.remove();

	thread globalFlyin_SpawnLaunch( $platformQuad_launcher ,"char/alien-type3-quad.tik" , 0 , "" , "" , 1 );

	$platformQuadFlame.show();
	$platformQuadFlame.followpath( $platformQuadFlame_path );

	wait( 1.25 );

	thread platformQuad_hitShuttle();

	wait( 1.75 ); // make it 11 seconds after shuttle takes off

	while( shuttleShrinking == 1 )
	{
		shuttleScale = shuttleScale * .9625;
		$drull_shuttle.scale( shuttleScale );
		$drull_shuttle_int.scale( shuttleScale );

		shuttleSomething++;
		wait( .1 );
		if( shuttleSomething >= 100 )
		{
			shuttleShrinking = 0;
			wait( 1 );
		}
	}

	$drull_shuttle.remove();
	$drull_shuttle_int.remove();
}

//---------------------
// Function: platformQuad_hitShuttle
// Comments:
// Quad hits the shuttle
//---------------------
void platformQuad_hitShuttle()
{
	shuttleIsHit++;

	if( shuttleIsHit <= 1 )
	{
		$platformQuad_shipExplosion.spawntargetname( "platformQuad_shipExploder" );
		globalCoop_level_triggerEntity($platformQuad_shipExplosion);

		wait( 1 );

		$drull_shuttle.playsound ( "sound/impact/explosion/expl_drullship.wav", 0, 1.5, 2048 );
		//By the fates!  We've been hit!  Losing altitude fast.  We're going down…
		thread globalCoop_dialog_play($inigor,"m7l2/inigor_beenhit.mp3", 1, 20000, 1);
		wait( 2 );

		$platformQuad_horizonExplosion.spawntargetname( "platformQuad_horizonExploder" );
		globalCoop_level_triggerEntity($platformQuad_horizonExplosion);

		wait( 2 );

		$platformQuad_shipExploder.remove();
		$platformQuad_horizonExploder.remove();
	}
}

//---------------------
// Function: platformQuad_land
// Comments:
// Quad hits the ground
//---------------------
void platformQuad_land()
{
	globalCoop_eartquakeAll(1.25,1);
	$platformQuad.playsound ( "sound/impact/explosion/explo_wall1.wav", 0, 1.5, 2048 );

	wait( .40 );
	$platformQuad.ai_off();
	$platformQuadFlame.remove();

	$platformQuad.anim( "crash" );
	waitForAnimation( $platformQuad, "crash", 0 );

	globalCoop_level_triggerEntity($platformQuad_groundExplosion);

	$platformQuad.ai_on();

	$platformQuad.attack(globalCoop_return_playerClosestPreferActive($platformQuad));
	wait( .1 );

	$platformQuad.killthread( "platformQuad_death" );

	//$player.setobjectiveshow( "InvestigateCrash", 1 );
	thread globalCoop_objectives_update("incomplete",5,1);

	$idryllshuttle.missionobjective( 1 );
}

//---------------------
// Function: platformQuad_death
// Comments:
// killthread for the quad on the platform
//---------------------
void platformQuad_death()
{
	wait( 5 );
	//globalCoop_level_remove($inigor);
	
	//Munro?  Are you ok?
	globalCoop_dialog_play($sydney,"m7l2/syd_myouok.mp3", 1, 20000, 1);
	//I'm fine Syd.  I'm about to investigate the crash site, stay on the comm.
	globalCoop_dialog_play(globalCoop_return_playerClosestPreferActive($sydney),"m7l2/munro_finesyd.mp3", 1, 80000, 1);
	//Will do, sir!
	globalCoop_dialog_play($sydney,"m7l2/syd_willdo.mp3", 1, 20000, 1);

	globalCommon_Autosave();	
}


//==========================================================================================
// platform lift functions
//==========================================================================================
//---------------------
// Function: platformLift_wait
// Comments:
// wait thread for the switch on the lift
//---------------------
void platformLift_wait()
{
	$platformLift_switch.onuse( "platformLift_move" );
	pause();
}



void platformLift_callLift()
{
entity eTrigger;
eTrigger = getCurrentEntity();
	if($platformLift.getFloatVar("liftBussy")){
		return;
	}
	if(doesEntityExist(eTrigger)){
		entity ePlayer;
		ePlayer = eTrigger.getLastActivatingEntity();
		if(doesEntityExist(ePlayer)){
			ePlayer.hudPrint("^5INFO: ^8Calling Lift, please stand by...\n");
		}
	}
	platformLift_move();
}



//---------------------
// Function: platformLift_move
// Comments:
// moves the lift downward
//---------------------
void platformLift_move()
{
	if($platformLift.getFloatVar("liftBussy")){
		return;
	}
	$platformLift.setFloatVar("liftBussy",1);
	
	if(!cvar_bool_multiplayer){//singleplayer
		$platformLift_clip.solid();
	}
	else{
		$platformLift_clip.notSolid();
	}

	$platformLift_switch.nouse();
	$platformLift_switch.playsound ( "sound/ships/attrexian/att-beepconfirm.wav", 3, .4, 500 );

	float threadNum = 0;
	float fLiftState;
	fLiftState = $platformLift.getFloatVar("liftState");
	if(!fLiftState){
		$coop_liftTriggerBottom.notTriggerable();
		$platformLift.bind( $platformLift_support );
		$platformLift.playsound ( "sound/environment/machine/lift4.wav", 3, 1, 500 );
		threadNum = thread moveAxisAlignedEntity( "platformLift_support" , 2, 416, "down" );
		waitforthread ( threadNum );

		$platformLift.playsound ( "sound/environment/machine/lift4stop.wav", 3, 1, 500 );
		$platformLift.unbind();
		$platformLift.playsound ( "sound/environment/machine/mech_move2.wav", 2, 2, 500 );
		threadNum = thread moveAxisAlignedEntity( "platformLift" , 1, 72, "down" );
		waitforthread ( threadNum );

		if(!cvar_bool_multiplayer){//singleplayer
			$platformLift_clip.notSolid();
		}
		$platformLift.playsound ( "sound/environment/machine/mech_stop1.wav", .5, 2, 500 );
		$platformLift_gate.playsound ( "sound/environment/machine/mech_move2.wav", 2, 2, 500 );
		$platformLift_gate.time( 2 );
		$platformLift_gate.rotateYup( 90 );
		waitfor( $platformLift_gate );
		$platformLift.setFloatVar("liftState",1);
		$coop_liftTriggerTop.triggerable();
	}
	else{
		$coop_liftTriggerTop.nottriggerable();
		$platformLift.playsound ( "sound/environment/machine/mech_stop1.wav", .5, 2, 500 );
		$platformLift_gate.playsound ( "sound/environment/machine/mech_move2.wav", 2, 2, 500 );
		$platformLift_gate.time( 2 );
		$platformLift_gate.rotateYdown( 90 );
		waitfor( $platformLift_gate );
		
		$platformLift.bind( $platformLift_support );
		$platformLift.playsound ( "sound/environment/machine/lift4.wav", 3, 1, 500 );
		threadNum = thread moveAxisAlignedEntity( "platformLift_support" , 2, 416, "up" );
		waitforthread ( threadNum );

		$platformLift.unbind();
 		$platformLift.playsound ( "sound/environment/machine/lift4stop.wav", 3, 1, 500 );
		$platformLift.playsound ( "sound/environment/machine/mech_move2.wav", 2, 2, 500 );
		threadNum = thread moveAxisAlignedEntity( "platformLift" , 1, 72, "up" );
		waitforthread ( threadNum );
		$platformLift.setFloatVar("liftState",0);
		$coop_liftTriggerBottom.triggerable();
	}
	$platformLift_gate.playsound ( "sound/environment/machine/mech_stop1.wav", .5, 2, 500 );
	$platformLift.setFloatVar("liftBussy",0);
	
	//thread platformLift_downWait();
	thread platformLift_wait();
}

//---------------------
// Function: platformLift_downWait
// Comments:
// wait thread for the lift when it is at the bottom of its path
//---------------------
void platformLift_downWait()
{
	$platformLift_switch.onuse( "platformLift_reject" );
	pause();
}

//---------------------
// Function: platformLift_reject
// Comments:
// reject sound played when the lift is at the bottom of its path
//---------------------
void platformLift_reject()
{
	$platformLift_switch.nouse();
	$world.playsound ( "sound/ships/attrexian/att-beepreject.wav", 3, .7, 500000 );
	wait( 3 );

	thread platformLift_downWait();
}


//==========================================================================================
// factory elevator functions
//==========================================================================================
//---------------------
void factory_elevator_switch1_check()
//---------------------
{
	$factory_elevator_switch1.onuse( "factory_elevator_up" );
	$factory_elevator_switch2.onuse( "factory_elevator_up" );
	pause();

}

//---------------------
void factory_elevator_up()
//---------------------
{
	//closes lower doors before moving elevator up
	$factory_elevator_switch1.playsound ( "sound/ships/attrexian/att-beepconfirm.wav", 3, .4, 500 );
	$factory_elevator_switch2.playsound ( "sound/ships/attrexian/att-beepconfirm.wav", 3, .4, 500 );
	$factory_elevator_switch1.nouse();
	$factory_elevator_switch2.nouse();
	$factory_elevator_switch3.nouse();
	float threadNum = 0;
	$factory_elevator_screendoorbtm.playsound ( "sound/ships/attrexian/att-elevat_door.wav", 4, 1, 500 );
	thread  moveAxisAlignedEntity( "factory_elevator_screendoorbtm" , 2, 196, "down" );
	thread  moveAxisAlignedEntity( "factory_elevator_rightdoor" , 2, 96, "west" );
	threadNum = thread  moveAxisAlignedEntity( "factory_elevator_leftdoor" , 2, 96, "east" );
	waitforthread ( threadNum );
  	$factory_elevator_screendoorbtm.playsound ( "sound/ships/attrexian/att-elevat_door_stp.wav", 4, 1, 500 );

	float threadNum;
	$factory_elevator0.playsound ( "sound/ships/attrexian/att-elevat_start.wav", 3, 1, 500 );

	threadNum = thread  moveAxisAlignedEntity( "factory_elevator0" , 6, 782, "up" );
	//NOTE: the actual travel unit distance of the elevator is 768, this number is tweaked to 782 due to inaccuracy
	waitforthread( threadNum );

	$factory_elevator.playsound ( "sound/ships/attrexian/att-elevat_stop.wav", 3, 1, 500 );

	wait ( 1.0 ); //pause before opening door

	thread factory_elevator_door1_open();
}

//---------------------
void factory_elevator_door1_open()
//---------------------
{
	float threadNum = 0;
	$factory_elevator_screendoortop.playsound ( "sound/ships/attrexian/att-elevat_door.wav", 4, 1, 500 );
	thread  moveAxisAlignedEntity( "factory_elevator_screendoortop" , 2, 196, "up" );
	thread  moveAxisAlignedEntity( "factory_elevator_rightdoor" , 2, 96, "east" );
	threadNum = thread  moveAxisAlignedEntity( "factory_elevator_leftdoor" , 2, 96, "west" );
	waitforthread ( threadNum );
	$factory_elevator_screendoortop.playsound ( "sound/ships/attrexian/att-elevat_door_stp.wav", 4, 1, 500 );
	thread factory_elevator_switch2_wait();
}

//---------------------
void factory_elevator_switch2_wait()
//---------------------
{
	$factory_elevator_switch2.onuse ( "factory_elevator_door1_close" );
	$factory_elevator_switch3.onuse ( "factory_elevator_door1_close" );
	pause();
}

//---------------------
void factory_elevator_door1_close()
//---------------------
{
	$factory_elevator_switch1.nouse();
	$factory_elevator_switch2.nouse();
	$factory_elevator_switch3.nouse();
	$factory_elevator_switch2.playsound ( "sound/ships/attrexian/att-beepconfirm.wav", 3, .4, 500 );
	$factory_elevator_switch3.playsound ( "sound/ships/attrexian/att-beepconfirm.wav", 3, .4, 500 );
	float threadNum;
	thread  moveAxisAlignedEntity( "factory_elevator_screendoortop" , 2, 196, "down" );
	thread  moveAxisAlignedEntity( "factory_elevator_rightdoor" , 2, 96, "west" );

	threadNum = thread  moveAxisAlignedEntity( "factory_elevator_leftdoor" , 2, 96, "east" );
	waitforthread( threadNum );
	thread factory_elevator_down();
}

//---------------------
void factory_elevator_down()
//---------------------
{
	float threadNum = 0;
	threadNum = thread  moveAxisAlignedEntity( "factory_elevator0" , 8, 782, "down" );
	$factory_elevator0.playsound ( "sound/ships/attrexian/att-elevat_start.wav", 3, 1, 500 );
	//NOTE: the actual travel unit distance of the elevator is 768, this number is tweaked to 782 due to inaccuracy
	waitforthread( threadNum );
	$factory_elevator0.playsound ( "sound/ships/attrexian/att-elevat_stop.wav", 3, 1, 500 );
	thread factory_elevator_door2_open();
}

//---------------------
void factory_elevator_door2_open()
//---------------------
{
	float threadNum = 0;
	$factory_elevator_screendoorbtm.playsound ( "sound/ships/attrexian/att-elevat_door.wav", 4, 1, 500 );
	thread  moveAxisAlignedEntity( "factory_elevator_screendoorbtm" , 2, 196, "up" );
	thread  moveAxisAlignedEntity( "factory_elevator_rightdoor" , 2, 96, "east" );
	threadNum = thread  moveAxisAlignedEntity( "factory_elevator_leftdoor" , 2, 96, "west" );
	waitforthread ( threadNum );
  	$factory_elevator_screendoorbtm.playsound ( "sound/ships/attrexian/att-elevat_door_stp.wav", 4, 1, 500 );
	thread factory_elevator_switch1_check();
}


//==========================================================================================
// garage door functions
//==========================================================================================
//---------------------
void gdoor_closed_switch_wait()
//---------------------
{
	$gdoor_switch.onuse ( "gdoor_open" );
	pause();
}

//---------------------
void gdoor_open()
//---------------------
{

	$gdoor_switch.nouse();
	$gdoor_main.playsound ( "sound/ships/attrexian/att-beepconfirm.wav", 3, .4, 500 );
	float threadNum = 0;

	$gdoor_main.playsound ( "sound/ships/attrexian/att-colonydoor1.wav", 4, 1, 500 );
	thread  moveAxisAlignedEntity( "gdoor_el" , 6, 240, "east" );
	thread  moveAxisAlignedEntity( "gdoor_wl" , 6, 240, "west" );
	thread  moveAxisAlignedEntity( "gdoor_main" , 6, 248, "up" );
	wait( 1 );
	thread  moveAxisAlignedEntity( "gdoor_eu" , 3.75, 152, "east" );
	threadNum = thread  moveAxisAlignedEntity( "gdoor_wu" , 3.75, 152, "west" );
	waitforthread ( threadNum );
	thread gdoor_open_switch_wait();
}

//---------------------
void gdoor_open_switch_wait()
//---------------------
{
	$gdoor_switch.onuse ( "gdoor_close" );
	pause();
}

//---------------------
void gdoor_close()
//---------------------
{
	$gdoor_switch.nouse();
	$gdoor_main.playsound ( "sound/ships/attrexian/att-beepconfirm.wav", 3, .4, 500 );
	float threadNum = 0;
	$gdoor_main.playsound( "sound/ships/attrexian/att-colonydoor1.wav", 4, 1, 500 );
	thread  moveAxisAlignedEntity( "gdoor_main" , 6, 248, "down" );
	thread  moveAxisAlignedEntity( "gdoor_el" , 6, 240, "west" );
	thread  moveAxisAlignedEntity( "gdoor_wl" , 6, 240, "east" );
	wait( 1 );
	thread  moveAxisAlignedEntity( "gdoor_eu" , 4.5, 152, "west" );
	threadNum = thread  moveAxisAlignedEntity( "gdoor_wu" , 4.5, 152, "east" );
	waitforthread ( threadNum );
	thread gdoor_closed_switch_wait();
}


//==========================================================================================
// city functions
//==========================================================================================
//---------------------
void citydoor1_switch_wait()
//---------------------
{
	$citydoor1_switch.onuse ( "citydoor1_open" );
	pause();
}

//---------------------
void citydoor1_open()
//---------------------
{

	$citydoor1_switch.nouse();
	$citydoor1_top.playsound ( "sound/ships/attrexian/att-beepconfirm.wav", 3, .4, 500 );
	$citydoor1_portalblock.openportal();
	float threadNum = 0;
	$citydoor1_top.playsound ( "sound/ships/attrexian/att-colonydoor1.wav", 4, 1, 500 );
	thread  moveAxisAlignedEntity( "citydoor1_top" , 3, 144, "up" );
	threadNum = thread  moveAxisAlignedEntity( "citydoor1_bottom" , 3, 104, "down" );
	waitforthread ( threadNum );
	thread citydoor1_close();
}

//---------------------
void citydoor1_close()
//---------------------
{
	wait( 5 );
	float threadNum = 0;
	$citydoor1_top.playsound ( "sound/ships/attrexian/att-colonydoor1.wav", 4, 1, 500 );
	thread  moveAxisAlignedEntity( "citydoor1_top" , 3, 144, "down" );
	threadNum = thread  moveAxisAlignedEntity( "citydoor1_bottom" , 3, 104, "up" );
	waitforthread ( threadNum );
	$citydoor1_portalblock.closeportal();
	thread citydoor1_switch_wait();
}

//---------------------
void citydoor2_switch_wait()
//---------------------
{
	$citydoor2_switch.onuse ( "citydoor2_open" );
	pause();
}


//---------------------
void citydoor2_open()
//---------------------
{

	$citydoor2_switch.nouse();
	$citydoor2_top.playsound ( "sound/ships/attrexian/att-beepconfirm.wav", 3, .4, 500 );
	$citydoor2_portalblock.openportal();
	float threadNum = 0;
	$citydoor2_top.playsound ( "sound/ships/attrexian/att-colonydoor1.wav", 4, 1, 500 );
	thread  moveAxisAlignedEntity( "citydoor2_top" , 3, 144, "up" );
	threadNum = thread  moveAxisAlignedEntity( "citydoor2_bottom" , 3, 104, "down" );
	waitforthread ( threadNum );
	thread citydoor2_close();
}

//---------------------
void citydoor2_close()
//---------------------
{
	wait( 5 );
	float threadNum = 0;
	$citydoor2_top.playsound ( "sound/ships/attrexian/att-colonydoor1.wav", 4, 1, 500 );
	thread  moveAxisAlignedEntity( "citydoor2_top" , 3, 144, "down" );
	threadNum = thread  moveAxisAlignedEntity( "citydoor2_bottom" , 3, 104, "up" );
	waitforthread ( threadNum );
	$citydoor2_portalblock.closeportal();
	thread citydoor2_switch_wait();
}

//---------------------
// Function: crashSiteAliens
// Comments:
// sets a death thread on the aliens at the crash site
//---------------------
void crashSiteAliens()
{
	coop_float_spawnAngle0 			= 1;
	coop_float_spawnAngle1 			= 1;
	coop_float_spawnAngle2 			= 1;
	coop_float_spawnAngle3 			= 1;
	coop_float_spawnAngle4 			= 1;
	coop_float_spawnAngle5 			= 1;
	coop_float_spawnAngle6 			= 1;
	coop_float_spawnAngle7 			= 1;
	coop_float_spawnAngle8 			= 1;
	coop_vector_spawnOrigin1 			= '-1137 -1929 109';
	coop_vector_spawnOrigin2 			= '-1137 -1830 109';
	coop_vector_spawnOrigin3 			= '-1137 -1760 109';
	coop_vector_spawnOrigin4 			= '-1137 -1690 109';
	coop_vector_spawnOrigin5 			= '-1137 -1620 109';
	coop_vector_spawnOrigin6 			= '-1137 -1550 109';
	coop_vector_spawnOrigin7 			= '-1137 -1480 109';
	coop_vector_spawnOrigin8 			= '-1137 -1410 109';
	thread globalCoop_applaySpawnOriginOnReSpwanOrigin();
	wait( .1 );
	$endAlien1.killthread( "endAliensDeath" );
	$endAlien2.killthread( "endAliensDeath" );
	$endAlien3.killthread( "endAliensDeath" );
	$endAlien4.killthread( "endAliensDeath" );

	$world.weather( "rain", 0 );
}

//---------------------
// Function: endAliensDeath
// Comments:
// after the aliens at the end die you can then trigger the end cinematic
//---------------------
void endAliensDeath()
{
	endAliensKilled++;
	if( endAliensKilled >= 4 )
	{
		$crashSiteTrigger.triggerable();
	}
}


//==========================================================================================
// end shuttle functions
//==========================================================================================
//--------------------------
void shuttle_flyby_thread()
//--------------------------
{
	$shuttle_flyby.show();
	$shuttle_flyby_door.show();
	float threadNum;
	$shuttle_flyby.playsound ( "sound/vehicles/shuttlecraft/shuttle_decelerate.wav", 6, 1, 1024 );
	threadNum = thread  moveAxisAlignedEntity( "shuttle_flyby" , 6, 1688, "north" );
	waitforthread( threadNum );
}

//--------------------------
void shuttle_flyby_continue()
//--------------------------
{
	float threadNum;
	$shuttle_flyby.playsound ( "sound/vehicles/shuttlecraft/shuttle_flyby_slow.wav", 10, 1, 1024 );
	threadNum = thread  moveAxisAlignedEntity( "shuttle_flyby" , 15, 9600, "north" );
	waitforthread( threadNum );
	wait( 3 );
	$shuttle_flyby.remove();
	$shuttle_flyby_door.remove();
}


//-----------------------
void shuttle_explosion_thread()
//------------------------
{
	$shuttle_explosion.spawnnow();
}


//==========================================================================================
// dialog and cinematic functions
//==========================================================================================
//---------------------
// Function: crash_site_dialog
// Comments:
// dialog at the crash site
//---------------------
void crash_site_dialog()
{
//allow loading next mission on timeranout
	coop_vector_svnextmaporrebootDoloadnextmimapWaitingForTeam_y=1;
//allow loading next mission on timeranout
	$idryllshuttle.missionobjective( 0 );
	// $player.setobjectivecomplete( "FindElderlyIdryll", 1 );
	// $player.setobjectivecomplete( "InvestigateCrash", 1 );
	
//set objective
	globalCoop_objectives_update("complete",4,0);
	thread globalCoop_objectives_update("complete",5,1);
	wait( 2 );

	globalCoop_dialog_play(globalCoop_return_playerClosestPreferActive($idryllshuttle),"m7l2/munro_scanlife.mp3", 1, 80000, 1);

	//Scanning now, sir
	globalCoop_dialog_play($sydney,"m7l2/syd_scannow.mp3", 1, 20000, 1);

	//I'm not reading anything sir, either he's dead or he has left the area.
	globalCoop_dialog_play($sydney,"m7l2/syd_notread.mp3", 1, 20000, 1);

	//if(globalCoop_return_integerPlayersQuantity(0) < 2){
		//Ok, there is nothing more we can do here.  One to beam up.
		globalCoop_dialog_play(globalCoop_return_playerClosestPreferActive($idryllshuttle),"m7l2/munro_okbeam.mp3", 1, 80000, 1);
	//}
	
	thread coop_waitForTeam();
}


//==========================================================================================
// random functions
//==========================================================================================
//---------------------
// Function: randomExplosionSound
// Comments:
// randomly picks an explosion sound to play and then randomly waits
//---------------------
void randomExplosionSound()
{
	float choice;

	$explosionFlash1.hide();
	$explosionFlash2.hide();
	$explosionFlash3.hide();
	$explosionFlash4.hide();
	$explosionFlash5.hide();
	$explosionFlash6.hide();

	while( 1 )
	{
		choice = randomint( 6 );

		if( choice == 0 )
		{
			$world.playsound( "sound/impact/explosion/expl_lowfreq.wav" , 3 , 1.25 , 200000 );
			globalCoop_eartquakeAll( .15 , .75 );
			$explosionFlash1.show();
			$explosionFlash1.fadein( .05 );
			$explosionFlash1.alpha ( 1 );
			$explosionFlash1.fade( 2 );
		}
		else if( choice == 1 )
		{
			$world.playsound( "sound/impact/explosion/expl_muffle1.wav" , 3 , 1.25 , 200000 );
			globalCoop_eartquakeAll( .15 , .75 );
			$explosionFlash2.show();
			$explosionFlash2.fadein( .05 );
			$explosionFlash2.alpha ( 1 );
			$explosionFlash2.fade( 2 );
		}
		else if( choice == 2 )
		{
			$world.playsound( "sound/impact/explosion/expl_muffle2.wav" , 3 , 1.25 , 200000 );
			globalCoop_eartquakeAll( .15 , .75 );
			$explosionFlash3.show();
			$explosionFlash3.fadein( .05 );
			$explosionFlash3.alpha ( 1 );
			$explosionFlash3.fade( 2 );
		}
		else if( choice == 3 )
		{
			$world.playsound( "sound/impact/explosion/expl_muffle3.wav" , 3 , 1.25 , 200000 );
			globalCoop_eartquakeAll( .15 , .75 );
			$explosionFlash4.show();
			$explosionFlash4.fadein( .05 );
			$explosionFlash4.alpha( 1 );
			$explosionFlash4.fade( 2 );
		}
		else if( choice == 4 )
		{
			$world.playsound( "sound/impact/explosion/expl_lowfreq.wav" , 3 , 1.25 , 200000 );
			globalCoop_eartquakeAll( .15 , .75 );
			$explosionFlash5.show();
			$explosionFlash5.fadein( .05 );
			$explosionFlash5.alpha ( 1 );
			$explosionFlash5.fade( 2 );
		}
		else if( choice == 5 )
		{
			$world.playsound( "sound/impact/explosion/expl_muffle1.wav" , 3 , 1.25 , 200000 );
			globalCoop_eartquakeAll( .15 , .75 );
			$explosionFlash6.show();
			$explosionFlash6.fadein( .05 );
			$explosionFlash6.alpha ( 1 );
			$explosionFlash6.fade( 2 );
		}
		wait( randomint (11) +10 );

	}
}

//---------------------
// Function: randomLightningFlash
// Comments:
// flashes lighting in the skybox every so often
//---------------------
void randomLightningFlash()
{
	float LightningTime;
	float LightningBrightness;
	float LightningCountFlashes;
	float LightningWait;
	float LightningVisualIndex;
	float MAX_lightningFlashObjects;
	float i;
	float LightningLastFlash;
	string lightningFlashName;
	entity lightningFlashObject;

	LightningVisualIndex = 1;
	MAX_lightningFlashObjects = 8;

	for( i = MAX_lightningFlashObjects; i >= 0; i-- )
		{
			lightningFlashName = "lightning" + LightningVisualIndex;
			lightningFlashObject = getentity( lightningFlashName );
			lightningFlashObject.hide();
			LightningVisualIndex += 1;
		}

	LightningVisualIndex = 1;

	while( 1 )
	{
		LightningWait = randomint( 10 );
		LightningWait += 5;
		wait ( LightningWait );

		LightningCountFlashes = randomint( 5 );
		LightningCountFlashes += 3;

		while(( LightningCountFlashes >= 1 ))
		{
			LightningTime = randomfloat( 0.15 );
			LightningTime += .05;
			LightningBrightness = randomfloat( 0.3 );
			LightningBrightness += .2;

			lightningFlashName = "lightning" + LightningVisualIndex;

			lightningFlashObject = getentity( lightningFlashName );

			lightningFlashObject.show();
			lightningFlashObject.fadein( .05 );
			lightningFlashObject.alpha ( 1 );

			if( LightningCountFlashes <= 1 )
			{
				if( LightningBrightness >= .4 )
				{
					$world.playsound( "sound/environment/nature/thunder3.wav", 3, 1.5, 1000000 );
				}
				else if( LightningBrightness == .3 )
				{
					$world.playsound( "sound/environment/nature/thunder2.wav", 3, 1.5, 1000000 );
				}
				else if( LightningBrightness <= .2 )
				{
					$world.playsound( "sound/environment/nature/thunder1.wav", 3, 1.5, 1000000 );
				}
			}

			lightningFlashObject.fade( LightningTime );
			wait( LightningTime );

			lightningFlashObject.hide();

			LightningCountFlashes -= 1;
			LightningLastFlash = LightningVisualIndex;
			LightningVisualIndex = randomint( MAX_lightningFlashObjects );
			LightningVisualIndex ++;

			if( LightningLastFlash == LightningVisualIndex )
			{
				LightningVisualIndex ++;
			}
		}
	}
}


//==========================================================================================
// other functions
//==========================================================================================
//---------------------
// Function: attrexianDoorBeepAccept
// Comments:
// plays a confirmation beep when the player pushes a door button
//---------------------
void attrexianDoorBeepAccept()
{
	$world.playsound ( "sound/ships/attrexian/att-beepconfirm.wav", 3, .7, 500000 );
}

//---------------------
// Function: attrexianDoorBeepReject
// Comments:
// plays a rejection beep when the player pushes a door button
//---------------------
void attrexianDoorBeepReject()
{
	$world.playsound ( "sound/ships/attrexian/att-beepreject.wav", 3, .7, 500000 );
}

//---------------------
// Function: attrexianAutoSave
// Comments:
// called to autosave the level
//---------------------
void attrexianAutoSave()
{
	globalCommon_Autosave();
}

//---------------------
// Function: attrexianDeathFallOff
// Comments:
// deathfall trigger turns off
//---------------------
void attrexianDeathFallOff()
{
	$deathfall.nottriggerable();
}

//---------------------
// Function: attrexianDeathFallOn
// Comments:
// deathfall trigger turns on
//---------------------
void attrexianDeathFallOn()
{
	$deathfall.triggerable();
}


void coop_waitForTeam()
//------------------------------------------------------------------------------
//waitForPlayers beeing close, end then the Level
//------------------------------------------------------------------------------
{
	globalCoop_main_waitForTeam('2641 -3257 233','-1100 -1100 -100','1100 1100 2000');
	thread shuttle_flyby_continue();
	thread levelEnding();
}


void coop_endLevel()
//------------------------------------------------------------------------------
//Level end
//------------------------------------------------------------------------------
{
	thread globalCoop_mission_completed("m8l1a-drull_ruins2");
}

