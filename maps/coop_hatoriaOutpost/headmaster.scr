//hadmaster features - The purpose of this file is to keep the map-script easy to read
void headmasterLightsToogle()
{
	if($world.getlightintensity("headmasterCeilingLight") ==  0){
		$world.light_fade("headmasterCeilingLight",1,1);
	}
	else{
		$world.light_fade("headmasterCeilingLight",0,1);
	}
}


void headmasterlaptopUsed()
{//headmaster laptopi
	$laptop.anim("open_idle");
	$laptopSymbol.remove();
	spawn("script_model","model","coop_symbols_modulate.tik","origin","2460 -88 -23","targetname","laptopSymbol2");
//add puzzle to download the new security-clearance, will set stringvar-key
	if(cvar_bool_multiplayer){
		globalCoop_puzzle_add("Security_Clearance","headmasterUpdateSecurityClearance",5,$laptop.getOrigin());
	//wait for puzzle to be ready
		wait(.5);
	//do not deactivate that puzzle
		$Security_Clearance.setFloatVar("coop_puzzle_doNotDisable",1);
		$Security_Clearance.setFloatvar("globalCoop_puzzleAllowAllClasses",1);	
	}else{
		singleplayerSpawnRealTricorderPuzzle($laptop.getOrigin(),10,"Security_Clearance","","headmasterUpdateSecurityClearance");
	}
}

void headmasterUpdateSecurityClearance()
{
	$laptopSymbol2.remove();
	
	if(!float_headmasterPuzzleIsSolved){
		float_headmasterPuzzleIsSolved=1;
		
		$coop_momMenuAccessTrigger1.setStringVar("uservar1","coop_hatoriaOutpost_replicatorC");
		$coop_momMenuAccessTrigger2.setStringVar("uservar1","coop_hatoriaOutpost_replicatorC");
		
		if(!cvar_bool_multiplayer){
			centerprint("^5Security Clearance Upgradad...\n^2Full access to Replicators granted!\n");
		}
	//ADD WEAPONS TO REPLICATOR INVENTORY
		replicator_AddItemAll("replicator","weapon","models/weapons/worldmodel-phaser-STX.tik",.6,50,'0 0 0','0 0 0',"","Phaser");
		replicator_AddItemAll("replicator","weapon","models/weapons/worldmodel-compressionrifle.tik",.4,50,'0 0 0','0 0 0',"","CompressionRifle");
		replicator_AddItemAll("replicator","weapon","models/weapons/worldmodel-sniperrifle.tik",.4,50,'0 0 0','0 0 0',"","SniperRifle");
		replicator_AddItemAll("replicator","weapon","models/weapons/worldmodel-photon.tik",.4,50,'0 0 0','0 0 0',"","PhotonBurst");
	//ENABLE TRIGGER - UPDATES OBJECTIVES
		$replicatorObjective.triggerable();
	}
}


void headmasterDeskLightToggle()
{//getlightintensity
//toggle desk ligth in the headmaster office
	if($world.getlightintensity("headmasterDeskLight") ==  0){
		$world.light_intensity("headmasterDeskLight",1);
		$headmasterDeskLightBeam.show();
	}else{
		$world.light_intensity("headmasterDeskLight",0);
		$headmasterDeskLightBeam.hide();
	}
}


