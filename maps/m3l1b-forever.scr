//-----------------------------------------------------------------
//  EF2 Level Script File
//
//  Level:	  m3l1b-forever - USS Dallas 
//  Script By:    Kenny 'Gullie' Thompson, Richard "Charon" H., Adam 'Senn' Bellefeuil
//  Geometry By:  Adam 'Senn' Bellefeuil
//  Created on:   02/05/2002
//
//  Last Edited:  02/05/2003 - Adam
//-----------------------------------------------------------------

void main();
void init();

void doorJTubeFloor_Open();

void setupCameras();
void setup_archetypes();
void setupAI();
void setup_sounds();
void brokenhall_setup();

void firstEncounter1();
void firstEncounter2();
void firstEncounter3();

void alienJtube_sequence();
void sequenceBodyDrag();
void crewDeadMoving1_move1();
void deadHole1_thread();
void sequenceHallwayAlien1_1();
void sequenceHallwayAlien1_2();
void brokenhall1_explode1();

void sequenceEngineering();
void sequenceEngineering_skipThread();
void sequenceEngineering_end();

void sequenceLoungeAlienSpoof();
void loungeAlienSpoof_doorbreak2_explode();
void sequenceLoungeShake();
void sequenceEngineeringDoorExplode();
void dialogTeamUnderAttack();
void sequenceTeamUnderAttack();
void checkTeamUnderAttackAliensKilled();
void engineeringRestorePower();

void sequenceSubsystemOn();
//void Subsystem_ActivateFunction();
void switchExposeSubsystem_Function();
void doorBroken1Move();
void doorBroken3Move();
void soundAlienSounds1();
void soundAlienSounds2();
void soundAlienSounds3();

void dialogTurbolift();
void restoreTeamPositions();
void enterTurbolift();
void coop_waitForTeam();
void coop_endLevel();
void coop_findAndTargetnameEntities();
void coop_followClosestPlayer(entity eActor);

float coop_playLiftSequence=0;
float float_powerRestored=0;
float counterTeamUnderAttackAliensKilled = 0;


//=============================================================================
// Includes
//=============================================================================

//Co-op
/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////
#include "coop_mod/maps/main.scr"
#include "coop_mod/maps/optional_modules/teammateM3.scr"
#include "coop_mod/maps/optional_modules/contextM3.scr"
#include "coop_mod/maps/optional_modules/puzzles.scr"
#include "coop_mod/maps/optional_modules/lights.scr"
#include "coop_mod/maps/global/global_common.scr"
#include "coop_mod/maps/global/global_tubeDoor.scr"
#include "coop_mod/maps/global/global_soundPan.scr"
/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////
#include "maps/global_scripts/global_debugUtils.scr"
#include "maps/global_scripts/global_archetype.scr"
#include "maps/global_scripts/global_tricorderBase.scr"
#include "maps/global_scripts/global_tricorderRoute.scr"


void coop_newPlayerEntered()
//----------------------------------------------------------------------
//add breathing sound to each player
//----------------------------------------------------------------------
{
//LET THE BATTLE COMMENCE
	entity ePlayer;
	float fPlayerIdInUse = 1;
	while(fPlayerIdInUse<=coop_integer_maxPlayers)
	{
		globalCoop_main_waitAFrame();
		ePlayer = getentity("player"+fPlayerIdInUse);
		if(doesEntityExist(ePlayer)){
			if(ePlayer.getFloatVar("coop_newPlayerEnteredPROCESSED") != 1){
				ePlayer.setFloatVar("coop_newPlayerEnteredPROCESSED",1);
				ePlayer.setStringVar("coop_loopSoundName","sound/player/player_rebreath.wav");
				ePlayer.setFloatVar("coop_loopSoundVolume",7);
				ePlayer.setFloatVar("coop_loopSoundRange",75);
				ePlayer.loopsound ( "sound/player/player_rebreath.wav",7,75);
			}
		}
		fPlayerIdInUse++;
	}
}



void coop_findAndTargetnameEntities()
//find entities without targetname and give em a targetname
{
	float 	fEntity,
			fToFind=2;
	entity	e;
	
	for(fEntity=0;fEntity<1023;fEntity++){
		e=getEntity("*"+fEntity);
		if(doesEntityExist(e)){
			if(e.getOrigin() == '-892 3578 8'){
				e.targetname("spawnTeamUnderAttackAlien1");
				fToFind--;
			}
			if(e.getOrigin() == '-990 3576 8'){
				e.targetname("spawnTeamUnderAttackAlien2");
				fToFind--;
			}
		}
//Exit if all entites have been found
		if(fToFind < 1){
			return;
		}
	}
}


float _turboliftsPowered;

//===================================================================================================================
// Main Stuff 
//===================================================================================================================

//---------------------
// main	
// do start up stuff
//---------------------
void main()
{
	globalCoop_server_precacheSingleplayer();
//calling multiple entites with the same targetname does not work in mp...
	thread coop_findAndTargetnameEntities();
//--- set the soundtrack
	soundtrack( "music/m3l1b-forever.mus" );
//cache
	thread globalCoop_main_executeInSp("coop_mod/cfg/maps/m3l1b/main.cfg\n");
	
	//crewDeadMoving1
	
//only used to appear on the radar, which we don't have in multiplayer
//remove em to save some resources
	$sequenceUnderFloor_alien1.remove();
	$sequenceUnderFloor_alien2.remove();
	$sequenceUnderFloor_alien3.remove();
	$sequenceUnderFloor_alien4.remove();
	$sequenceUnderFloor_alien5.remove();

//deactivate when blocking the players on spawn
	// $Franklin.ai_off();
	// $Jurot.ai_off();
	// $Chell.ai_off();
	
//CO-OP
/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////
	coop_string_nextMapToCheck			= "m3l2-forever";//set the map we gona load next while we are in testmode
//Set Tactical information, to be added at the Objectives hud
	coop_string_objectiveTacticalInfo1 = "UseNightVision";
//spawnorigins, Spawn Players on those locations, at map start
	coop_vector_spawnOrigin1 			= '-436 821 0';
	coop_vector_spawnOrigin2 			= '-360 821 0';
	coop_vector_spawnOrigin3 			= '-290 821 0';
	coop_vector_spawnOrigin4 			= '-593 935 0';
	coop_vector_spawnOrigin5 			= '-136 974 0';
	coop_vector_spawnOrigin6 			= '-597 1156 0';
	coop_vector_spawnOrigin7 			= '-142 1143 0';
	coop_vector_spawnOrigin8 			= '-567 1219 400';
	
//Set spawnangles for this level
	coop_float_spawnAngle0 			= -90;//SpawnOrigin0 Angle
	coop_float_spawnAngle1 			= 90;//SpawnOrigin0 Angle
	coop_float_spawnAngle2 			= 90;//SpawnOrigin0 Angle
	coop_float_spawnAngle3 			= 90;//SpawnOrigin0 Angle
	coop_float_spawnAngle4 			= 90;//SpawnOrigin0 Angle
	coop_float_spawnAngle5 			= 180;//SpawnOrigin0 Angle
	coop_float_spawnAngle6 			= 90;//SpawnOrigin0 Angle
	coop_float_spawnAngle7 			= 180;//SpawnOrigin0 Angle
	coop_float_spawnAngle8 			= 90;//SpawnOrigin0 Angle

//Definie Objective
	coop_string_objectiveItem1			= "RestorePowerInEngineering";
	coop_string_objectiveItem2			= "RetrieveCaptainLogsOnBridge";
	coop_string_objectiveItem3			= "RestoreTurboLiftPower";
	
//Setup the Set of Weapons for the players
	coop_string_weapon1				= "models/weapons/worldmodel-BurstRifle.tik";
	coop_string_weapon3				= "models/weapons/worldmodel-Tricorder-stx.tik";
	coop_string_weapon2				= "models/weapons/worldmodel-Phaser-stx.tik";
	string sExtraWeapon;
	sExtraWeapon = getCvar("coop_eWtik");
	if("models/weapons/worldmodel-imod.tik" == sExtraWeapon)
	{
		coop_string_weapon4	= sExtraWeapon;
	}
//Start the Co-Op Script
	globalCoop_main();
/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////
//Setup ai
	$Chell.loopsound ( "sound/player/player_rebreath.wav",7,75);
	$Jurot.loopsound ( "sound/player/player_rebreath.wav",7,75);
	$Franklin.loopsound ( "sound/player/player_rebreath.wav",7,75);
	thread restoreTeamPositions();	
	$sky.hide();
	$sky.rendereffects ( "+skyorigin" );
	$world.entity_fade_dist ( 8000 );
	
	$stardome.rotateX( 0.2 );
	$stardome.rotateY( 0.7 );

	thread init();

	//thread globalCommon_OnUse ( $doorJTubeFloor , "doorJTubeFloor_Open" );
	//thread globalCommon_OnUse ( $switchJTubeElevatorLevel1 , "doorJTubeElevatorLevel1_OpenFunction" );
	//thread globalCommon_OnDamage ( $triggerDoorEngineeringEast , "sequenceEngineeringDoorExplode" );
	
	thread doorBroken1Move();
	thread doorBroken3Move();
	thread setupCameras();
	thread setup_archetypes();
	thread setupAI();
	thread brokenhall_setup();
	thread setup_sounds();
	
//spawn Class Selection
	thread globalCoop_class_setup("Medic",'-601 1377 0');
	thread globalCoop_class_setup("HeavyWeapons",'-530 1377 0');
	thread globalCoop_class_setup("Technician",'-460 1377 0');
	
	
//--- wait for the player
	$world.clearAvailableViewModes();
	cam_fadein( .01, 0, 0, 0, 1, 0 );
	
//open teh dore to invite the player leaving the room
	$smalldoor4.open( $world );
	
//If single player load objectives the regular way
	if(!cvar_bool_multiplayer){
		$player.loopsound ( "sound/player/player_rebreath.wav", .7, 51);
		$player.loadobjectives("m3l1b-forever");
		$player.setobjectiveshow( "RestorePower", 1 );
		$player.model("models/char/munro_evosuit.tik");
	}
	else{	
	//Use co-op mod function to set objectives
		globalCoop_objectives_update("inComplete" ,1,0);
		thread globalCoop_objectives_update("inComplete" ,2,1);
	}
	
	
//Make object visibe on the tricorder radar
	$panelEngineering.missionobjective( 1 );
	
	//--- turbolifts have no power
	_turboliftsPowered = FALSE;
	
	$doorEngineeringWest.noise( "sound/doors/door_locked.wav" );
	$smalldoor5.noise( "sound/doors/door_locked.wav" );
}

//---------------------
// init	
// world setup stuff
//---------------------
void init()
{
	// setup lights too off
	$world.light_lightstyle ( "lightDoorEngineeringWest" , "baaaaaaaa" , 0 );
	$world.light_lightstyle ( "lightDoorEngineeringEast" , "baaaaaaaa" , 0 );
	$world.light_lightstyle ( "lightEngineeringGroup1" , "baaaaaaaa" , 0 );
	$world.light_lightstyle ( "lightEngineeringGroup2" , "baaaaaaaa" , 0 );
	$world.light_lightstyle ( "lightAreaFlicker1", "aazzaazzjjaaazzaajaajzzzazazazajjzaazzkahjszwufkjaz", 0 );
	$world.light_lightstyle ( "lightAreaFire1" , "baaaaaaaa" , 0 );
//setup lights
	globalCoop_dynLight_change("firstEncounter_light1", "z","firstEncounter_light2","z");
	globalCoop_dynLight_change("firstEncounter_light3", "z","firstEncounter_light4","z");
	globalCoop_dynLight_change("firstEncounter_light5", "z","firstEncounter_light6","z");
	
	// set up lights turning back on
	$world.light_lightstyle ( "lightPowerOn" , "a" , 0 ); 
	$fieldPowerOn.hide();
	$panelPowerOn.hide();	
	
	$doorEngineeringWest.lock();
	$turboLift.lock();
	
//Replace puzzles
	if(cvar_bool_multiplayer){
		thread globalCoop_puzzle_replace($TurboliftUnitButton,"sequenceSubsystemOn",10);
		globalCoop_puzzle_replace($panelEngineering,"engineeringRestorePower",10);
		globalCoop_puzzle_extendModulatingRange($panelEngineering,20);
	}
	else{
		$TurboliftUnitButton.puzzleobject_deactivate();
	}
	
	// binding doors and stuff	
	$doorJTubeFloor.bind ( $doorJTubeFloorOrigin );
	$triggerDialogTurbolift.nottriggerable();
	
	// Jeffries tube doors
	thread globalTubeDoor_Setup( "doorJTubeLevel2Access_West", "normal", 48, "north_south" , 0 );
	thread globalTubeDoor_AddComponent( "button", "button1JTubeLevel2Access_West", "doorJTubeLevel2Access_West" );
	thread globalTubeDoor_AddComponent( "button", "button2JTubeLevel2Access_West", "doorJTubeLevel2Access_West" );
	thread globalTubeDoor_AddComponent( "lockedlight", "lockedlightJTubeLevel2Access_West", "doorJTubeLevel2Access_West" );
	thread globalTubeDoor_AddComponent( "unlockedlight", "unlockedlightJTubeLevel2Access_West", "doorJTubeLevel2Access_West" );

	thread globalTubeDoor_Setup( "doorJTubeLevel2Access_East", "normal", 48, "east_west" , 0 );
	thread globalTubeDoor_AddComponent( "button", "button1JTubeLevel2Access_East", "doorJTubeLevel2Access_East" );
	thread globalTubeDoor_AddComponent( "button", "button2JTubeLevel2Access_East", "doorJTubeLevel2Access_East" );

	$doorJTube_broke1_1.moveNorth( 32 );
	$doorJTube_broke1_2.moveSouth( 32 );

	// dead crew in hole stuff
	$deadHole1_alien1.forcealpha( 1 );
	$deadHole1_alien1.hide();

	// Lounge Alien Spoof Stuff
	$loungeAlienSpoof_doorbreak_clip1.solid();
	$loungeAlienSpoof_doorbreak1.solid();
	globalCoop_level_hideAndNotSolid($loungeAlienSpoof_doorbreak2);
	$loungeAlienSpoof_lightbreak2.hide();
	$loungeAlienSpoof_doorbreak_fracture.hide();

	//--- recess the gravity unit
	$switchSubsystem.time( .1 );
	$switchSubsystem.movewest( 36 );
	waitfor( $switchSubsystem );
	globalCoop_level_hideAndNotSolid($switchSubsystem_online);
	//$warpcore_forcefield.hide();
	
	//--- setup nono sounds
	thread globalCommon_OnUse ( $switchExposeSubsystem , "inaccessible_sound" );	
	$secretDoorFracture.viewmode( "structuralintegrity" );
	thread globalCommon_OnDamage( $secretDoor, "secretDoorExplode" );
	$secretDoor.health( 50 );
	
	$smalldoor5.lock();
	
	globalCoop_dynLight_change(
	"jeffreydoor_red", "z",
	"jeffreydoor_green", "a");
	
	$turboliftSwitch.nottriggerable();
	
	globalCoop_dynLight_change(
	"engineeringLightsWest_red", "z",
	"engineeringLightsWest_green", "a");
	
	globalCoop_dynLight_change(
	"jeffyTubs_green", "z",
	"jeffyTubs_red", "a");
	
	globalCoop_level_remove($secret_shipclip);
	
	
	//spawn("script_model","model","fx/fx-dummy","targetname","coop_entOrigin","origin","2273 264 289");
	spawn("script_model","model","vehicle/enterprise-e-01.tik","targetname","coop_ent1","origin","2273 264 320");//2273 264 289
	spawn("script_model","model","vehicle/enterprise-e-02.tik","targetname","coop_ent2","origin","2273 264 320");
	spawn("script_model","model","vehicle/enterprise-e-03.tik","targetname","coop_ent3","origin","2273 264 320");
	
	wait(.5);
	$coop_ent1.angle(-90);
	$coop_ent2.angle(-90);
	$coop_ent3.angle(-90);
	$coop_ent1.rendereffects("minlight");//fullbright
	$coop_ent2.rendereffects("minlight");
	$coop_ent3.rendereffects("minlight");
	
//Make AI look like standing by
	$Chell.anim("comp_idle");
	$Franklin.anim("comp_idle");
	$Jurot.anim("comp_idle");
}

void restoreTeamPositions()
{
	vector v;
	
	// restore position/angle of everybody
	if( getfloatvar( "game.savedTeamPos" ) != 1 || cvar_bool_multiplayer == 1){
		return;
	}
		
	removevar( "game.savedTeamPos" );
	
	v = getvectorvar( "game.saveChellOrigin" );	
	$Chell.warp( v );
	$Chell.angle( getfloatvar( "game.saveChellAngle" ) );
	removevar( "game.saveChellOrigin" );
	removevar( "game.saveChellAngle" );
	
	v = getvectorvar( "game.saveJurotOrigin" );	
	$Jurot.warp( v );
	$Jurot.angle( getfloatvar( "game.saveJurotAngle" ) );
	removevar( "game.saveJurotOrigin" );
	removevar( "game.saveJurotAngle" );

	v = getvectorvar( "game.saveFranklinOrigin" );	
	$Franklin.warp( v );
	$Franklin.angle( getfloatvar( "game.saveFranklinAngle" ) );
	removevar( "game.saveFranklinOrigin" );
	removevar( "game.saveFranklinAngle" );
	
	v = getvectorvar( "game.savePlayerOrigin" );
	if(v != '0 0 0'){
		$player.warp( v );
	}
	removevar( "game.savePlayerOrigin" );
}

//===================================================================================================================
// Setup Stuff
//===================================================================================================================

//---------------------
// setupCameras
// setting up cameras
//---------------------
void setupCameras()
{
//spawn cameras
	spawn( "Camera", "targetname", "cam1");
	spawn( "Camera", "targetname", "cam2");
	spawn( "Camera", "targetname", "cam3");
	spawn( "Camera", "targetname", "cam4");
	spawn( "Camera", "targetname", "cam5");

	wait( .1 );

//load camera paths
	cam.load( "m3l1b_engineering_adam1" );
	cam.load( "m3l1b_engineering_adam2" );
	cam.load( "m3l1b_engineering_adam3" );
	cam.load( "m3l1b_engineering_adam4" );
	cam.load( "m3l1b_engineering_adam5" );
}


//----------------------------

//-----------------------------------
void setup_sounds()
{
	//globalSoundPan_Init();
	/*
	chars\wraith\wr_walk.wav
	chars\quad\quad_step1.wav
	chars\quad\quad_alert.wav
	chars\lurker\lurk_alert.wav
	chars\klingon\kling_fpain2.wav
	chars\chewer\chew_amb1.wav
	chars\basher\bash_sprayloop.wav
	chars\basher\bash_amb1.wav
	chars\attrexian\attrex_glassslide.wav
	footsteps\metal_duct\duct6.wav
	footsteps\metal_duct\duct1.wav
	weapons\phaser\phaser_stun.wav
	sound\player\player_heartbeat.wav
	loc\Deu\sound\dialog\combat\attrex_longp.mp3
	*/	
 	globalSoundPan_AddSound ( "sound/environment/metal/metal_scrape1.wav", 1, 1, 1000, 0.5, 3, 200, randomint( 5 ) + 5 );
	globalSoundPan_AddSound ( "sound/environment/metal/metal_scrape2.wav", 2, 1, 1000, 0.5, 3, 200, randomint( 5 ) + 5 );
	globalSoundPan_AddSound ( "sound/environment/metal/metal_scrape3.wav", 3, 1, 1000, 0.5, 3, 200, randomint( 5 ) + 5 );
	globalSoundPan_AddSound ( "sound/environment/metal/metal_scrape4.wav", 4, 1, 1000, 0.5, 3, 200, randomint( 5 ) + 5 );
	globalSoundPan_AddSound ( "sound/chars/lurker/lurk_ambship1.wav", 1, 1, 1000, 0.5, 3, 200, randomint( 5 ) + 5 );
	globalSoundPan_AddSound ( "sound/chars/lurker/lurk_ambship2.wav", 2, 1, 1000, 0.5, 3, 200, randomint( 5 ) + 5 );
	globalSoundPan_AddSound ( "sound/chars/lurker/lurk_ambship3.wav", 3, 1, 1000, 0.5, 3, 200, randomint( 5 ) + 5 );
	globalSoundPan_AddSound ( "sound/chars/lurker/lurk_ambship4.wav", 4, 1, 1000, 0.5, 3, 200, randomint( 5 ) + 5 );
	globalSoundPan_AddSound ( "sound/chars/lurker/lurk_ambship5.wav", 4, 1, 1000, 0.5, 3, 200, randomint( 5 ) + 5 );	
	globalSoundPan_AddSound ( "sound/ships/forever/ship_creak_low1.wav", 1, 1, 1000, 0.5, 3, 200, randomint( 5 ) + 5 );
	globalSoundPan_AddSound ( "sound/ships/forever/ship_creak_low2.wav", 2, 1, 1000, 0.5, 3, 200, randomint( 5 ) + 5 );
	globalSoundPan_AddSound ( "sound/ships/forever/ship_creak_low3.wav", 3, 1, 1000, 0.5, 3, 200, randomint( 5 ) + 5 );
	globalSoundPan_AddSound ( "sound/ships/forever/ship_creak_low4.wav", 4, 1, 1000, 0.5, 3, 200, randomint( 5 ) + 5 );
	globalSoundPan_AddSound ( "sound/ships/forever/ship_creak_low5.wav", 5, 1, 1000, 0.5, 3, 200, randomint( 5 ) + 5 );
	globalSoundPan_AddSound ( "sound/ships/forever/ship_creak_medium1.wav", 1, 1, 1000, 0.5, 3, 200, randomint( 5 ) + 5 );
	globalSoundPan_AddSound ( "sound/ships/forever/ship_creak_medium2.wav", 2, 1, 1000, 0.5, 3, 200, randomint( 5 ) + 5 );
	globalSoundPan_AddSound ( "sound/ships/forever/ship_creak_medium3.wav", 3, 1, 1000, 0.5, 3, 200, randomint( 5 ) + 5 );	
	thread globalSoundPan_Start(); 
}

void setupArchetypeObject( entity e, string theArchetype )
{
	e.contents( "targetable" );
	e.archetype( theArchetype );
}

//---------------------
// setup_archetypes
// setting up the archetypes
//---------------------
void setup_archetypes()
{
	globalArchetype_Setup( $turboLift_archetype, "TurboLiftControls" );
	globalArchetype_Setup( $panelEngineering_archetype, "EngineeringControls" );
	globalArchetype_Setup( $switchExposeSubsystem_archetype, "TurboliftPanel" );
	globalArchetype_Setup( $jdoor1_p_archetype1, "DoorPanel" );
	globalArchetype_Setup( $jdoor1_p_archetype2, "DoorPanel" );
	globalArchetype_Setup( $jdoor2_p_archetype1, "DoorPanel" );
	globalArchetype_Setup( $jdoor2_p_archetype2, "DoorPanel" );
	globalArchetype_Setup( $turbolift_waypoint, "Waypoint" );
	setupArchetypeObject( $doorpanelarch, "DoorPanel" );
}

//---------------------
// setupAI
// used to setup the ai
//---------------------
void setupAI()
{
	thread globalCoop_actor_useWeapon($Chell,"fed-compressionrifle-enhanced");
	thread globalCoop_actor_useWeapon($Franklin,"fed-compressionrifle-enhanced");
	globalCoop_actor_useWeapon($Jurot,"fed-compressionrifle-enhanced");//ai was off so it won't be reactivated
	
	$Munro.notsolid();
	$Munro.ai_off();
	$Franklin.ai_on();
	$Jurot.ai_on();
	$Chell.ai_on();	

	if(doesEntityExist($sequenceHallwayAlien1_alien1))
	{
		$sequenceHallwayAlien1_alien1.ai_off();
	}
	
	globalCoop_main_executeInSp("coop_mod/cfg/maps/m3l1b/setupAI.cfg");
/* 	if(!cvar_bool_multiplayer){//For singleplayer only, not shown in mp on tricorder
		$sequenceUnderFloor_alien1.ai_off();
		$sequenceUnderFloor_alien2.ai_off();
		$sequenceUnderFloor_alien3.ai_off();
		$sequenceUnderFloor_alien4.ai_off();
		$sequenceUnderFloor_alien5.ai_off();
		
		$sequenceUnderFloor_alien1.ghost();
		$sequenceUnderFloor_alien2.ghost();
		$sequenceUnderFloor_alien3.ghost();
		$sequenceUnderFloor_alien4.ghost();
		$sequenceUnderFloor_alien5.ghost();
		
		$sequenceUnderFloor_alien1.archetype("Lurker");
		$sequenceUnderFloor_alien2.archetype("Lurker");
		$sequenceUnderFloor_alien3.archetype("Lurker");
		$sequenceUnderFloor_alien4.archetype("Lurker");
		$sequenceUnderFloor_alien5.archetype("Lurker");
	} */
	wait(1);
	if(doesEntityExist($alienJTube_alien1)){
		$alienJTube_alien1.ai_off();
		$alienJTube_alien1.angles( '0 0 0' );
		$alienJTube_alien1.notsolid();
	}
	globalCoop_contextM3_setup($Chell , "chell");
	globalCoop_contextM3_setup($Jurot , "jurot");
	globalCoop_contextM3_setup($Franklin , "franklin");
	
	globalCoop_teammate_register($Chell);
	globalCoop_teammate_register($Jurot);
	globalCoop_teammate_register($Franklin);
}

//---------------------
// brokenhall_setup
// setup for the brokenhallway
//---------------------
void brokenhall_setup()
{
	$brokehall_brokehall_fires1.hide();
	$brokenhall_brokenhallwalls2.hide();
}


//===================================================================================================================
// First Encounter
//===================================================================================================================

//---------------------
// Function: firstEncounter1()
// Comments:
// first part of the firstEncounter sequence in the hallway
// triggers a sound when the player steps into the hallway
//---------------------
void firstEncounter1()
{
	$world.playsound ( "sound/chars/lurker/lurk_alert_amb.wav" , 8 , 1 , 200000 );

	// lock the blasted jeffy tubs
	if(!cvar_bool_multiplayer){
		globalCoop_main_executeInSp("coop_mod/cfg/maps/m3l1b/firstEncounter1.cfg");
/* 		$doorJeffriesTubeEntrance.lock();
		$jeffyTubsLight_red.show();
		globalCoop_level_remove($jeffyTubsLight_green); */
	
		$doorJeffriesTubeEntrance.noise( "sound/doors/door_locked.wav" );
		globalCoop_dynLight_change(
		"jeffyTubs_red", "z",
		"jeffyTubs_green", "a");
	}

	// move hazard team to hallway for engineering sequence
	thread globalCoop_actor_walkToVector($Chell,'-368 3344 0',"walk");
	thread globalCoop_actor_walkToVector($Jurot,'-368 3440 0',"walk");
	thread globalCoop_actor_walkToVector($Franklin,'-560 3344 0',"walk");
	
	$Chell.angle ( 0 );
	$Jurot.angle ( 0 );
	$Franklin.angle ( 0 );
}

//---------------------
// Function: firstEncounter2()
// Comments:
// second part of the firstEncounter sequence in the hallway
// causes lights to shut off
// triggers sounds
//---------------------
void firstEncounter2()
{
	spawn("script_model","model","fx/fx-dummy.tik","targetname","coop_fx_sound","origin","1141 3437 200");
	wait(.2);
	music( "suspense","normal" );
	$coop_fx_sound.playsound ( "sound/ships/forever/for_powerdown.wav" , 8 , .8 , 500 );
	globalCoop_dynLight_change("firstEncounter_light1", "a","","");
	wait (.75);
	$coop_fx_sound.playsound ( "sound/ships/forever/for_powerdown.wav" , 9 , .8 , 500 );
	globalCoop_dynLight_change("firstEncounter_light2", "a","","");
	wait (.75);
	$coop_fx_sound.playsound ( "sound/ships/forever/for_powerdown.wav" , 8 , .8 , 500 );
	globalCoop_dynLight_change("firstEncounter_light3", "a","","");
	wait (.75);
	$coop_fx_sound.playsound ( "sound/ships/forever/for_powerdown.wav" , 9 , .8 , 500 );
	globalCoop_dynLight_change("firstEncounter_light4", "a","","");
	wait (.75);
	$coop_fx_sound.playsound ( "sound/ships/forever/for_powerdown.wav" , 8 , .8 , 500 );
	globalCoop_dynLight_change("firstEncounter_light5", "a","","");
	wait (.75);
	$coop_fx_sound.playsound ( "sound/ships/forever/for_powerdown.wav" , 9 , .8 , 500 );
	globalCoop_dynLight_change("firstEncounter_light6", "a","","");

	wait( 1 );

	$coop_fx_sound.playsound ( "sound/chars/lurker/lurk_amb4.wav" , 10 , 1 , 2000 );
	wait( 5 );
	$coop_fx_sound.playsound ( "sound/chars/lurker/lurk_amb3.wav" , 11 , 1 , 2000 );
}

//---------------------
// Function: firstEncounter3()
// Comments:
// this is where the player first meets the lurker alien
//---------------------
void firstEncounter3()
{
	if(cvar_bool_multiplayer){//Multiplayer	
	//relocate spawn
		coop_float_spawnAngle1 				= 90;
		coop_float_spawnAngle2 				= 90;
		coop_float_spawnAngle3 				= 90;
		coop_float_spawnAngle4 				= 90;
		coop_float_spawnAngle5 				= 90;
		coop_float_spawnAngle6 				= 90;
		coop_float_spawnAngle7 				= 90;
		coop_float_spawnAngle8 				= 90;
		coop_vector_spawnOrigin8 			= '250 3169 3';
		coop_vector_spawnOrigin7 			= '200 3169 3';
		coop_vector_spawnOrigin6 			= '150 3169 3';
		coop_vector_spawnOrigin5 			= '100 3169 3';
		coop_vector_spawnOrigin4 			= '50 3169 3';
		coop_vector_spawnOrigin3 			= '-09 3169 3';
		coop_vector_spawnOrigin2 			= '-59 3169 3';
		coop_vector_spawnOrigin1 			= '-109 3169 3';
		globalCoop_applaySpawnOriginOnReSpwanOrigin();		
	//move class selection
		thread globalCoop_level_moveToPos($coop_class_MedicModel,'-81 3570 -2');
		thread globalCoop_level_moveToPos($coop_class_TechnicianModel,'-81 3710 -2');
		thread globalCoop_level_moveToPos($coop_class_HeavyWeaponsModel,'-81 3871 -2');
	}
	
	$firstEncounter_alienspawn1.spawntargetname( "firstEncounter_alien1" );
	$firstEncounter_alienspawn1.modelname( "char/alien-type1a-lurker.tik" );
	$firstEncounter_alienspawn2.spawntargetname( "firstEncounter_alien2" );
	$firstEncounter_alienspawn2.modelname( "char/alien-type1a-lurker.tik" );
	$firstEncounter_alienspawn1.scale ( 1.25 );
	globalCoop_level_triggerEntity($firstEncounter_alienspawn1);
	globalCoop_level_triggerEntity($firstEncounter_alienspawn2);
	music( "surprise","normal" );
	
	wait( 0.5 );
	$firstEncounter_alien1.playsound ( "sound/chars/lurker/lurk_ambush.wav" , 8 , 1 , 2000 );
}


//===================================================================================================================
// Stuff
//===================================================================================================================

//---------------------
// Function: alienJtube_sequence
// Comments:
// alien in jtube runs down length away from player and then the door shuts
//---------------------
void alienJtube_sequence()
{
	$alienJTube_alien1.playsound ( "sound/chars/lurker/lurk_spottarget.wav" , 8 , 1 , 2000 );
	$alienJTube_alien1.walkto( "alienJTube_node1" , "attack_charge_run" );
	waitfor( $alienJTube_alien1 );
	
	$doorJTube_broke1_1.time( .75 );
	$doorJTube_broke1_2.time( .75 );
	$doorJTube_broke1_1.moveSouth( 32 );
	$doorJTube_broke1_2.moveNorth( 32 );

	$doorJTube_broke1_1.solid();
	$doorJTube_broke1_2.solid();

	wait( 1 );

	$alienJTube_alien1.remove();
}

//---------------------
// sequenceBodyDrag
// Team sees alien drag dead body drug into hole
//---------------------
void sequenceBodyDrag()
{
	thread crewDeadMoving1_move1();
	globalCoop_dialog_play($Franklin,"dallas01/frank_heywhat.mp3", 1, 11111, 1);
}

//---------------------
// Function: deadHole1_thread
// Comments:
// alien in hole slashes at player and then fades away
//---------------------
void deadHole1_thread()
{
	$deadHole1_alien1.show();
	$deadHole1_alien1.playsound ( "sound/chars/lurker/lurk_ambush.wav" , 8 , 1 , 2000 );
	$deadHole1_alien1.animonce( "attackcombo1" );
	waitforanimation( $deadHole1_alien1, "attackcombo1" );
	
	//$deadHole1_alien1.fade( 1 , 0 );
	$deadHole1_alien1.remove();
}


//---------------------
// crewDeadMoving1_move1()
// dead crew member being drug into the hole
//---------------------
void crewDeadMoving1_move1()
{
	//In singelplayer only
	$crewDeadMoving1.angle(180);
	music ("aux5","normal");
	$crewDeadMoving1.playsound ( "sound/chars/lurker/lurk_draggore.wav" , 8 , 1 , 2000 );
		
	$crewDeadMoving1.time(.25);
	$crewDeadMoving1.moveEast( 32 );
	waitfor( $crewDeadMoving1 );
	wait( .25 );

	//$crewDeadMoving1.playsound ( "sound/chars/lurker/lurk_dragloop.wav" , 8 , .65 , 2000 );
	
	$crewDeadMoving1.time(.25);
	$crewDeadMoving1.moveEast( 32 );
	waitfor( $crewDeadMoving1 );
	wait( .25 );

	$crewDeadMoving1.time(.25);
	$crewDeadMoving1.moveEast( 32 );
	waitfor( $crewDeadMoving1 );
	wait( .25 );

	$crewDeadMoving1.time(.25);
	$crewDeadMoving1.moveEast( 32 );
	waitfor( $crewDeadMoving1 );
	wait( .25 );

	$crewDeadMoving1.time(.5);
	$crewDeadMoving1.moveEast( 32 );
	waitfor( $crewDeadMoving1 );
	wait( .5 );

	$crewDeadMoving1.time(.25);
	$crewDeadMoving1.moveEast( 32 );
	waitfor( $crewDeadMoving1 );
	wait( .25 );

	$crewDeadMoving1.playsound ( "sound/chars/lurker/lurk_dragstop.wav" , 9 , .65 , 2000 );
	globalCoop_level_hideAndNotSolid($crewDeadMoving1);
	wait( 2 );
	globalCoop_level_remove($crewDeadMoving1);
}

//---------------------
// sequenceHallwayAlien1_1()
// this is where the player first meets the lurker alien
//---------------------
void sequenceHallwayAlien1_1()
{
	$sequenceHallwayAlien1_alien1.notsolid();
	$sequenceHallwayAlien1_alien1.origin( '1084 2116 8' );
	wait(.1);
	$sequenceHallwayAlien1_alien1.walkto( "sequenceHallwayAlien1_alien1_node1" , "attack_charge_run" );
	waitfor( $sequenceHallwayAlien1_alien1 );
	$sequenceHallwayAlien1_blackwall1.solid();
}

//---------------------
// sequenceHallwayAlien1_2()
// this is where the player first meets the lurker alien
//---------------------
void sequenceHallwayAlien1_2()
{
	$sequenceHallwayAlien1_alien1.playsound ( "sound/chars/lurker/lurk_spottarget.wav" , 8 , .65 , 2000 );
	$sequenceHallwayAlien1_alien1.walkto( "sequenceHallwayAlien1_alien1_node2" , "attack_charge_run" );
	waitfor( $sequenceHallwayAlien1_alien1 );
	$sequenceHallwayAlien1_alien1.remove();
	wait( 4 );
	$sequenceHallwayAlien1_blackwall1.playsound ( "sound/environment/metal/grate_blow.wav", 4, 1, 10001);
}


void coop_followClosestPlayer(entity eActor)
{
	waitforplayer();
	if(doesEntityExist(eActor))
	{
		entity ePlayer;
		string sPlayerToFollow;
		ePlayer = getEntity(""+eActor.getFloatvar("coop_aiOrderFollowTargetname"));
		if(!doesEntityExist(ePlayer))
		{
			ePlayer = globalCoop_return_playerClosestActive($brokenhall_brokenhallwalls2);
			if(sPlayerToFollow == "world")
			{
				sPlayerToFollow = "player1";
			}
			else
			{
				sPlayerToFollow		= ePlayer.getRawTargetName();
			}
			eActor.setStringVar("coop_aiOrderFollowTargetname",sPlayerToFollow);
			eActor.settendency("follow_"+sPlayerToFollow,1);
			eActor.followrange(450);
			eActor.followrangemin(164);
			eActor.followtarget(ePlayer);
		}
	}
}


//---------------------
// brokenhall1_explode1()
// hallway explodes and forces player to go another direction
//---------------------
void brokenhall1_explode1()
{	
	globalCoop_level_remove($brokenhall_brokenhallwalls1);
	globalCoop_level_showAndSolid($brokenhall_brokenhallwalls2);
	
	$brokehall_brokehall_fires2.spawntargetname( "brokehall_brokehall_fires2_exploder" );
	$brokehall_brokehall_fires2.modelname( "fx/fx-explosion-crate-fire.tik" );	
	
	globalCoop_level_triggerEntity($brokehall_brokehall_fires2);
	
	$Chell.ai_on();
	$Jurot.ai_on();
	$Franklin.ai_on();

	$brokenhall_brokenhallwalls2.playsound ( "sound/impact/explosion/explo_neonsign.wav", 2, 1, 10001);
	
	$brokehall_brokehall_fires1.show();
	
	coop_followClosestPlayer($Chell);
	coop_followClosestPlayer($Jurot);
	coop_followClosestPlayer($Franklin);
	wait( 2 );

	//COMPUTER VOICE DIALOG
	globalCoop_dialog_play($Picard,"dallas01/comp_lifeeffic.mp3", 1, 11111,0);
}


//===================================================================================================================
// Observation Lounge and Jeffries Tube Stuff 
//===================================================================================================================

//---------------------
// sequenceLoungeAlienSpoof	
// Aliens appear on radar team says stuff, then they scatter
//---------------------
void sequenceLoungeAlienSpoof()
{
	forcemusic( "aux2", "normal" );
	thread globalCoop_actor_walkToVector($Chell,'-1595 2864 10',"run");
	thread globalCoop_actor_walkToVector($Franklin,'-1690 2391 10',"run");
	thread globalCoop_actor_walkToVector($Jurot,'-1696 2563 0',"run");
	
	wait(5);
	
	$world.playsound ( "sound/chars/lurker/lurk_amb4.wav" , 8 , .65 , 2000 );

	if(!cvar_bool_multiplayer){
		$sequenceUnderFloor_alien1.show();
		$sequenceUnderFloor_alien2.show();
		$sequenceUnderFloor_alien3.show();
		$sequenceUnderFloor_alien4.show();
		$sequenceUnderFloor_alien5.show();
		
		$sequenceUnderFloor_alien1.walkto( "$sequenceUnderFloor_alien1_node2" );
		$sequenceUnderFloor_alien2.walkto( "$sequenceUnderFloor_alien2_node2" );
		$sequenceUnderFloor_alien3.walkto( "$sequenceUnderFloor_alien3_node2" );
		$sequenceUnderFloor_alien4.walkto( "$sequenceUnderFloor_alien4_node2" );
		$sequenceUnderFloor_alien5.walkto( "$sequenceUnderFloor_alien5_node2" );
	}
	globalCoop_actor_useWeapon($Jurot,"fed-tricorder-STX");
	
	//I've got lifesigns! Check your tricorder!
	globalCoop_dialog_play($Jurot,"dallas01/jurot_lifetri.mp3", 1, 11111, 1);
	
	globalCoop_actor_useWeapon($Chell,"fed-tricorder-STX");

	//Look at all of them! What are they?
	globalCoop_dialog_play($Chell,"dallas01/chell_lookall.mp3", 1, 11111, 1);
	$world.playsound ( "sound/environment/metal/metal_scrape3.wav" , 8 , .8 , 2000000 );	
	
	globalCoop_actor_useWeapon($Franklin,"fed-tricorder-STX");

	//Those blips are headed this way!
	globalCoop_dialog_play($Franklin,"dallas01/frank_blips.mp3", 1, 11111, 1);
	
	//They're getting closer! There almost here! They're- 
	globalCoop_dialog_play($Chell,"dallas01/chell_closer.mp3", 1, 11111, 1);

	$loungeAlienSpoof_explosion1.spawntargetname( "loungeAlienSpoof_exploder1" );
	$loungeAlienSpoof_explosion1.modelname( "fx/fx-explosion-metaldebris-directional.tik" );

    $loungeAlienSpoof_explosion1.playsound( "sound/ships/drull/drull_doorpound.wav", 6, 2, 690 );

	globalCoop_level_triggerEntity($loungeAlienSpoof_explosion1);

	if(!cvar_bool_multiplayer){
	//do not waste animations!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
		$alienLoungeSpoofInHallway_spawn1.spawntargetname( "alienLoungeSpoofInHallway_spawned1" );
		$alienLoungeSpoofInHallway_spawn1.modelname( "char/alien-type1a-lurker.tik" );
		globalCoop_level_triggerEntity($alienLoungeSpoofInHallway_spawn1);
		wait( .1 );
		$alienLoungeSpoofInHallway_spawned1.ai_off();
		$alienLoungeSpoofInHallway_spawned1.playsound ( "sound/chars/lurker/lurk_amb2.wav" , 8 , .65 , 2000 );
		$alienLoungeSpoofInHallway_spawned1.walkto( "alienLoungeSpoofInHallway_node1" , "attack_charge_run" );
	//relict ?
		$sequenceUnderFloor_alien1.walkto( "$sequenceUnderFloor_alien1_node1" , "run" );
		$sequenceUnderFloor_alien2.walkto( "$sequenceUnderFloor_alien2_node1" , "run" );
		$sequenceUnderFloor_alien3.walkto( "$sequenceUnderFloor_alien3_node1" , "run" );
		$sequenceUnderFloor_alien4.walkto( "$sequenceUnderFloor_alien4_node1" , "run" );
		$sequenceUnderFloor_alien5.walkto( "$sequenceUnderFloor_alien5_node1" , "run" );
	}
	
	//They're going away.
	globalCoop_dialog_play($Jurot,"dallas01/jurot_going.mp3", 1, 11111, 1);
	
	//Hey!
	globalCoop_dialog_play($Chell,"dallas01/chell_hey.mp3", 1, 11111, 1);
	
	thread globalCoop_actor_useWeapon($Jurot,"fed-compressionrifle-enhanced");
	globalCoop_actor_useWeapon($Chell,"fed-compressionrifle-enhanced");
	
	$loungeAlienSpoof_explosion2.spawntargetname( "loungeAlienSpoof_exploder2" );
	$loungeAlienSpoof_explosion2.modelname( "fx/fx-explosion-debris-metal-forever-small.tik" );
	globalCoop_level_triggerEntity($loungeAlienSpoof_explosion2);

	globalCoop_level_show($loungeAlienSpoof_lightbreak_sparks1);
	$loungeAlienSpoof_lightbreak2.show();
	globalCoop_level_show($loungeAlienSpoof_sparks);
	globalCoop_level_remove($loungeAlienSpoof_lightbreak1);

	globalCoop_level_showAndSolid($loungeAlienSpoof_doorbreak2);
    $loungeAlienSpoof_doorbreak2.playsound( "sound/ships/drull/drull_doorpound.wav", 6, 1.2, 690 );
	globalCoop_level_remove($loungeAlienSpoof_doorbreak1);

	globalCoop_level_remove($alienLoungeSpoofInHallway_spawned1);
	globalCoop_level_remove($loungeAlienSpoof_doorbreak_clip1);
	$loungeAlienSpoof_doorbreak_fracture.viewmode( "structuralintegrity" );
	wait( .1 );
	$loungeAlienSpoof_doorbreak_fracture.show();

	thread globalCommon_OnDamage( $loungeAlienSpoof_doorbreak2, "loungeAlienSpoof_doorbreak2_explode" );
	
	//You sound disappointed.
	globalCoop_dialog_play($Franklin,"dallas01/frank_disapp.mp3", 1, 11111, 1);
	thread globalCoop_actor_useWeapon($Franklin,"fed-compressionrifle-enhanced");
	
	//The only thing worse than an alien attack is an alien attack that hides when you see it coming.
	globalCoop_dialog_play($Chell,"dallas01/chell_hides.mp3", 1, 11111, 1);

	if(!cvar_bool_multiplayer){
		globalCoop_level_remove($sequenceUnderFloor_alien1);
		globalCoop_level_remove($sequenceUnderFloor_alien2);
		globalCoop_level_remove($sequenceUnderFloor_alien3);
		globalCoop_level_remove($sequenceUnderFloor_alien4);
		globalCoop_level_remove($sequenceUnderFloor_alien5);
	}
}

//---------------------
// Function: loungeAlienSpoof_doorbreak2_explode
// Comments:
// explodes the structuralintegrity door in the lounge when you shoot it
//---------------------
void loungeAlienSpoof_doorbreak2_explode()
{
	$loungeAlienSpoof_explosion1.spawntargetname( "loungeAlienSpoof_exploder1" );
	$loungeAlienSpoof_explosion1.modelname( "fx/fx-explosion-door-forever.tik" );
	globalCoop_level_triggerEntity($loungeAlienSpoof_explosion1);
	$loungeAlienSpoof_explosion1.playsound( "sound/impact/explosion/explo_neonsign.wav", 5, 1, 690 );	
	globalCoop_level_remove($loungeAlienSpoof_doorbreak_fracture);
	globalCoop_level_remove($loungeAlienSpoof_doorbreak2);
}

//---------------------
// sequenceEnterJeffriesTubes	
// plays dialog make ai wander and wait for you to open the door
//---------------------
void sequenceEnterJeffriesTubes()
{
	music( "mystery", "normal" );
	globalCoop_actor_aiOff($Chell);	
	globalCoop_actor_aiOff($Franklin);
	globalCoop_actor_aiOff($Jurot);
}

//---------------------
// doorJTubeFloor_Open
// opens door in floor
//---------------------
void doorJTubeFloor_Open()
{
	$doorJTubeFloor.playsound ( "sound/ships/forever/for_floorhatch.wav" , 21 , 1 , 250 );
	$doorJTubeFloorOrigin.time( .5 );
	$doorJTubeFloorOrigin.rotateZdown( 110 );
	$doorJTubeFloorOrigin.movenorth( 2 );
	$doorJTubeFloorOrigin.moveUp( 13 );
	waitfor( $doorJTubeFloorOrigin );
}


//---------------------
// sequenceLoungeShake	
// little lounge sequence before aliens attack, big bang some stuff breaks
//---------------------
void sequenceLoungeShake()
{
	//globalCommon_Autosave();
	$world.playsound ( "sound/environment/metal/grate_blow.wav", 11, 1, 10001);
}	



//===================================================================================================================
// Engine Room and TurbLift Area with Jeffries Tubes
//===================================================================================================================
//---------------------
// dialogTubeEngineering
// team talks about finding way into engineering
//---------------------
void dialogTubeEngineering()
{
	//We've reached main engineering!
	globalCoop_dialog_play($Jurot,"dallas01/jurot_reached.mp3", 1, 11111, 1);

	//Locked.
	globalCoop_dialog_play($Chell,"dallas01/chell_locked.mp3", 1, 11111, 1);

	//We have to find a way in.
	globalCoop_dialog_play($Jurot,"dallas01/jurot_wayin.mp3", 1, 11111, 1);

	//No, but they should bring you to the observation lounge near Engineering.
	globalCoop_dialog_play($Franklin,"dallas01/frank_noobs.mp3", 1, 11111, 1);	
	
	//Good enough for me. Stay here.
	globalCoop_dialog_play(globalCoop_return_playerClosestPreferActive($Chell),"dallas01/munro_goode.mp3", 1, 11111, 1);
}


//-------------------------------------------------------------------------------------------------
void chellLetUsIn()
{
// Munro! We're still trapped!
	globalCoop_dialog_play($Chell,"dallas01/chell_hurry.mp3", 1, 11111, 1);
}

//---------------------
// dialogTeamUnderAttack
// team gets attacked on other side of door
//---------------------
void dialogTeamUnderAttack()
{
	forcemusic( "aux3","normal" );

	//We've got blips again!
	globalCoop_dialog_play($Chell,"dallas01/chell_weblips.mp3", 1, 11111, 1);
	
	//Moving in fast!
	globalCoop_dialog_play($Franklin,"dallas01/frank_movefast.mp3", 1, 11111, 1);
	
	//Munro!  We’re trapped! Get us in there!
	globalCoop_dialog_play($Jurot,"dallas01/jurot_trapped.mp3", 1, 11111, 1);

	//Scan your tricorder on the main computer station to interface with it.
	globalCoop_dialog_play($Franklin,"dallas01/frank_scantri.mp3", 1, 11111, 1);
	
//do not lock this door in multiplayer
	if(!cvar_bool_multiplayer){
		thread globalTubeDoor_Setup( "doorJTubeLevel2Access_West", "normal", 36, "north_south" , 1 );
	}
	sequenceTeamUnderAttack();
}

void engineeringRestorePower()
{
	//COMPUTER VOICE DIALOG FOR RESTORING MAIN POWER HERE
	globalCoop_dialog_play($Picard,"dallas01/comp_mainprest.mp3", 1, 11111,0);

	//globalArchetype_Setup( $panelEngineering_archetype, "EngineeringControlsUsed" );
	globalCoop_level_remove($panelEngineering_archetype);
	//$panelEngineeringUsed_archetype.show();
	//$panelEngineering_archetype.archetype( "NonInteractive" );
	globalCoop_level_remove($panelEngineering);
	
	forcemusic( "success","normal");

	// removes dialog trigger if it hasn't been used yet
	globalCoop_level_remove($triggerDialogTubeEngineering);
	// play power on sound
	$world.playsound ("sound/ships/forever/for_poweron.wav" , 2 , .75 , 200000 );
	spawn("script_model","model","fx/fx-dummy.tik","targetname","Warpcore","origin","119 4475 231");

	// turn lights on in engineering
	$world.light_lightstyle ( "lightDoorEngineeringWest" , "ghjkhjk" , 0 );
	$world.light_lightstyle ( "lightDoorEngineeringEast" , "ghjkhjk" , 0 );
	wait (.5);
	$world.light_lightstyle ( "lightDoorEngineeringWest" , "yzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz" , 0 );
	$world.light_lightstyle ( "lightDoorEngineeringEast" , "yzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz" , 0 );
	
	$world.light_lightstyle ( "lightEngineeringGroup1" , "ghjajka" , 0 );
	wait (.5);
	$world.light_lightstyle ( "lightEngineeringGroup1" , "yzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz" , 0 );
	
	$world.light_lightstyle ( "lightEngineeringGroup2" , "jgifwfsd" , 0 );
	wait (.4);
	$world.light_lightstyle ( "lightEngineeringGroup2" , "yzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz" , 0 );
	
	$world.light_lightstyle ( "lightPowerOff" , "baaaaaaaaaaaaaaaaaaaaaaaaaaaaaa" , 0 );
	$world.light_lightstyle ( "lightPowerOn" , "yzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz" , 0 );
	$world.light_lightstyle ( "lightAreaFlicker1", "yzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz", 0 );
	
	
	// globalCoop_dynLight_change("firstEncounter_light1", "z","firstEncounter_light2","z");
	// globalCoop_dynLight_change("firstEncounter_light3", "z","firstEncounter_light4","z");
	// globalCoop_dynLight_change("firstEncounter_light5", "z","firstEncounter_light6","z");
	
	$panelPowerOff.hide();
	$panelPowerOn.show();
	
	$Warpcore.loopsound( "sound/ships/enterprise/eng_warpcorewav.wav", .27, 1000 );
	$panelPowerOff.loopsound ( "sound/ships/enterprise/eng_pulse01.wav", .37, 1000 );
	$panelPowerOn.loopsound ( "sound/ships/forever/for_alarm_breach.wav" , .15 , 258.7 );
	
//Set Objective
	if(!cvar_bool_multiplayer){
		$player.setobjectivecomplete( "RestorePower", 1 );
		$panelEngineering.missionobjective( 0 );
	}
	else{
		globalCoop_objectives_update("complete" ,1,1);
	}
	
	$doorEngineeringWest.noise( "sound/ships/forever/for_doorbeep.wav" );
	$doorEngineeringWest.unlock();
	globalCoop_level_triggerEntity($doorEngineeringWest);

	$engineeringLightsWest_green.show();
	globalCoop_level_remove($engineeringLightsWest_red);
	
	globalCoop_dynLight_change(
	"engineeringLightsWest_green", "yzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz",
	"engineeringLightsWest_red", "baaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa");
	
	$Chell.followcombatrange( 512 );
	$Chell.followcombatrangemin( 384 );
	$Jurot.followcombatrange( 512 );
	$Jurot.followcombatrangemin( 384 );
	$Franklin.followcombatrange( 512 );
	$Franklin.followcombatrangemin( 384 );

	float_powerRestored = 1;
	$alien1TeamUnderAttack.immortal( 0 );
	$alien2TeamUnderAttack.immortal( 0 );
}

//---------------------
// sequenceTeamUnderAttack
// moves teammates to place and starts fake battle on other side of door
//---------------------
void sequenceTeamUnderAttack()
{
	$Chell.immortal( 1 );
	$Jurot.immortal( 1 );
	$Franklin.immortal( 1 );

	globalCoop_level_triggerEntity($spawnTeamUnderAttackAlien1);
	globalCoop_level_triggerEntity($spawnTeamUnderAttackAlien2);

	$Chell.ai_on();
	$Jurot.ai_on();
	$Franklin.ai_on();

	$Chell.settendency( "follow" , 0 );
	$Jurot.settendency( "follow" , 0 );
	$Franklin.settendency( "follow" , 0 );

	$Chell.followcombatrange( 2048 );
	$Chell.followcombatrangemin( 1024 );
	$Jurot.followcombatrange( 2048 );
	$Jurot.followcombatrangemin( 1024 );
	$Franklin.followcombatrange( 2048 );
	$Franklin.followcombatrangemin( 1024 );
	wait( .5 );
	
//build 16
//do not make immortal if the power has been restored #bugfix
	if(float_powerRestored != 1)
	{
		$alien1TeamUnderAttack.immortal( 1 );
		$alien2TeamUnderAttack.immortal( 1 );
	}
	wait( .1 );
	
	$alien1TeamUnderAttack.masterstatemap ( "ai/Lurk/MS_Aln_Lurk.st" );
	$alien2TeamUnderAttack.masterstatemap ( "ai/Lurk/MS_Aln_Lurk.st" );
	
	$alien1TeamUnderAttack.attack( $Chell );
	$alien2TeamUnderAttack.attack( $Jurot );

	$Chell.attack( $alien1TeamUnderAttack );
	$Franklin.attack( $alien2TeamUnderAttack );
	$Jurot.attack( $alien2TeamUnderAttack );

	wait (.1);
	$alien1TeamUnderAttack.killthread ( "checkTeamUnderAttackAliensKilled" );
	$alien2TeamUnderAttack.killthread ( "checkTeamUnderAttackAliensKilled" );
}

//---------------------
// checkTeamUnderAttackAliensKilled
// gets runs when aliens in sequence TeamUnderAttack 
// are killed, checks number killed and if should exit
//---------------------
void checkTeamUnderAttackAliensKilled()
{
	counterTeamUnderAttackAliensKilled ++;
	if ( counterTeamUnderAttackAliensKilled == 2 ) // this number equals aliens in sequence
	{
		wait (2);
		if(!cvar_bool_multiplayer){
			if( $player.gethealth () < 1 ){
				return;
			}
		}
		thread sequenceEngineering();
	}
}

//---------------------
// sequenceEngineering - adam 4/11/2003
// engineering sequence events, tricorder puzzle to get main power online 
//---------------------
void sequenceEngineering()
{
	globalCoop_main_camFadeOut(.25);
   	wait( .25 );
	globalCoop_cin_start();
	
//forcefield
	globalCoop_level_showAndSolid($fieldPowerOn);
	$fieldPowerOn.loopsound ("sound/ships/enterprise/ent_forcefield.wav",  .45, 80 );
	
    $Chell.ai_off();
    $Franklin.ai_off();
    $Jurot.ai_off();
    $Chell.anim( "idle" );
    $Franklin.anim( "idle" );
   	$Jurot.anim( "idle" );  
	$Chell.origin( '68 3720 8' );
	$Franklin.origin( '52 3784 8' );
	$Jurot.origin( '188 3720 8' );
	$Munro.origin( '256 4032 0' );
	$Munro.angle( 225 );
	$Munro.useactorweapon( "none" );
	$Munro.walkto( "engineering_munroNode1" );
	$Chell.angle( 60 );
	$Franklin.angle( 315 );	
	$Jurot.angle( 150 );
	$Chell.useactorweapon( "none" );
	$Jurot.useactorweapon( "Tricorder" );
	$Franklin.useactorweapon( "none" );
	$Jurot.anim( "tricorder_calm_idle" );
	music( "aux4" );
//SHOT 1
	globalCoop_cin_follow($cam1, $m3l1b_engineering_adam1 );
	wait( .25 );
	globalCoop_cin_cuecamera( $cam1 );
	globalCoop_main_camFadeIn(.5);
	wait( 1.25 );
	globalCoop_cin_skipThread( "sequenceEngineering_skipThread" );
	$Munro.headwatch( $Franklin );
	$Chell.headwatch( $Munro );
	
	//Thanks
	globalCoop_dialog_play($Chell,"dallas01/chell_thanks.mp3", 1, 11111, 0);
	
	//What the hell are those things?
	globalCoop_dialog_play($Franklin,"dallas01/frank_hellthose.mp3", 1, 11111, 0);
	
	//I have no idea
	globalCoop_dialog_play($Chell,"dallas01/chell_noidea.mp3",1,11111,0);
	wait( 1 );

//SHOT 2
	globalCoop_cin_follow($cam2, $m3l1b_engineering_adam3 );
	wait( .25 );
	
	globalCoop_cin_cuecamera( $cam2 );
	wait( .5 );

	$Chell.headwatch( $Jurot );
	$Jurot.headwatch( $Franklin );
	
	//Their attacks seem savage and instinctual. As if they are not a sentient species.
	thread globalCoop_dialog_play($Jurot,"dallas01/jurot_savage.mp3", 1, 11111, 0);
	wait( 3.5 );

	//SHOT 3
	globalCoop_cin_follow($cam2, $m3l1b_engineering_adam5 );
	wait( 2 );
	
	$Franklin.headwatch( $Jurot );
	
	//Sentient enough to wait until we were cornered before attacking.
	globalCoop_dialog_play($Franklin,"dallas01/frank_sentient.mp3",1,11111,1);

	wait( .5 );
	
//SHOT 4
	globalCoop_cin_follow($cam3, $m3l1b_engineering_adam2 );
	wait( .25 );
	
	globalCoop_cin_cuecamera( $cam3 );
		
	$Munro.headwatch( $Jurot );
	$Munro.anim( "tap_comm" );
	waitForAnimation( $Munro, "tap_comm" );		
	
	//Munro to Enterprise
	thread globalCoop_dialog_play($Munro,"dallas01/munro_toent.mp3",1,11111,0);
	$Munro.anim( "idle" );
	
//Co-Op Stability replacment////////////////////////////////////////////////////
	waitDialogLength("localization/sound/dialog/dallas01/munro_toent.mp3");
	wait(.8);
//Co-Op Stability replacment END////////////////////////////////////////////////

	//Go ahead, Lieutenant.
	globalCoop_dialog_play($Picard,"dallas01/picard_goahead.mp3",1,11111,1);
	
	//We’ve engaged hostile aliens, Sir. Unknown lifeforms.
	globalCoop_dialog_play($Munro,"dallas01/munro_engage.mp3",1,11111,0);
	
	//Team status?
	globalCoop_dialog_play($Picard,"dallas01/picard_status.mp3",1,11111,1);

	$Munro.headwatch( $Chell );
	
	//We’re intact.
	globalCoop_dialog_play($Munro,"dallas01/munro_intact.mp3",1,11111,0);
	
	//And the Dallas?
	globalCoop_dialog_play($Picard,"dallas01/picard_dallas.mp3",1,11111,1);

	$Munro.resethead();
	
	//We've restored partial power.
	globalCoop_dialog_play($Munro,"dallas01/munro_ppower.mp3",1,11111,0);
	
	//Proceed to the bridge. Find out what happened to Captain Galloway.
	globalCoop_dialog_play($Picard,"dallas01/picard_logs.mp3",1,11111,1);
	
	//Aye sir.
	globalCoop_dialog_play($Munro,"dallas01/munro_yessir.mp3",1,11111,1);
	wait(1);
	
	thread sequenceEngineering_skipThread();
}


//---------------------
void sequenceEngineering_skipThread()
{
	skipthread( "null" );
	killthread( "sequenceEngineering" );
	thread sequenceEngineering_end();
}

//---------------------
void sequenceEngineering_end()
{
	$Munro.stopdialog();
	$Chell.stopdialog();
	$Jurot.stopdialog();
	$Franklin.stopdialog();
	$Picard.stopdialog();
	
	globalCoop_main_camFadeOut(.25);
	wait( 0.25 );
	$triggerDialogTurbolift.triggerable();
	globalCoop_level_hideAndNotSolid($Munro);
	$Munro.origin( '-448.00 2737.00 -40.00' );
	
	if(!cvar_bool_multiplayer){
		$player.setobjectiveshow( "RetrieveLogs", 1 );
		$turboLift_archetype.missionobjective( 1 );	
		$player.warp( '256 4032 1' );
	}
	else{
		thread globalCoop_objectives_update("",2,1);
	}
	
	thread globalCommon_OnUse ( $switchExposeSubsystem , "switchExposeSubsystem_Function" );

	$Chell.useactorweapon( "CompressionRifleEnhanced" );
	$Chell.useactorweapon( "CompressionRifleEnhanced" );
	$Franklin.useactorweapon( "CompressionRifleEnhanced" );
	
	$Chell.ai_on();
	$Jurot.ai_on();
	$Franklin.ai_on();
	
	$Chell.settendency( "follow" , 1 );
	$Jurot.settendency( "follow" , 1 );
	$Franklin.settendency( "follow" , 1 );
	
	$Munro.resethead();
	$Chell.resethead();
	$Jurot.resethead();
	$Franklin.resethead();

	$smalldoor5.noise( "sound/ships/forever/for_doorbeep.wav" );	
	$smalldoor5.unlock();
	globalCoop_level_remove($jeffreydoor_red);
	$jeffreydoor_green.show();
	globalCoop_dynLight_change("jeffreydoor_green", "z","jeffreydoor_red", "a");
	
	globalCoop_cin_end();
	
//warp team to engenering to avoid getting stuck ?
	if(cvar_bool_multiplayer){
		globalCoop_player_warpToSpawnOriginAll();
		wait( 0.25 );
	}
	
//back from black
	globalCoop_main_camFadeIn(.25);
}


//---------------------
// sequenceEngineeringDoorExplode
// engineering door goes boom 
//---------------------
void sequenceEngineeringDoorExplode()
{
	globalCoop_level_triggerEntity($doorEngineeringEast);	
}


//===================================================================================================================
// Cinematics
//===================================================================================================================

//---------------------
// dialogTurbolift
// Team says turbolift is locked and can be opened nearby
//---------------------
void dialogTurbolift()
{
	//COMPUTER VOICE DIALOG FOR DEAD TURBOLIFT SYSTEMS HERE
	globalCoop_dialog_play($Picard,"dallas01/comp_turbooffl.mp3",1,11111,0);
	wait( .25 );
	
	//The turbolift has no power. We need to route power to it. 
	globalCoop_dialog_play($Chell,"dallas01/chell_noturb.mp3",1,11111,0);

	//The computer that governs lift power is right near here. That nearby Jeffries Tube goes right to it.
	globalCoop_dialog_play($Franklin,"dallas01/frank_computer.mp3",1,11111,0);
	
	//You stay here. I'm taking that Jeffries tube.
	globalCoop_dialog_play(globalCoop_return_playerClosestPreferActive($Chell),"dallas01/munro_stayhere.mp3",1,11111,0);

	if(!cvar_bool_multiplayer){
		$player.setobjectiveshow( "TurboliftPower", 1 );
		$turbolift_waypoint.missionobjective( 1 );
	}
	else{
		globalCoop_objectives_update("incomplete",3,1);
	}
	//$switchSubsystem_archetype.missionobjective( 1 );
}


//===================================================================================================================
// Misc
//===================================================================================================================

//---------------------
// soundAlienSounds1
// Aliens scratching in ducts
//---------------------
void soundAlienSounds1()
{
}

//---------------------
// soundAlienSounds2
// Aliens scratching in ducts
//---------------------
void soundAlienSounds2()
{
}

//---------------------
// soundAlienSounds3
// Aliens scratching in ducts
//---------------------
void soundAlienSounds3()
{
}

//---------------------
// doorBroken1Move
// Broken door bangs around 
//---------------------
void doorBroken1Move()
{
	while( 1 )
	{
    	$sparksDoorBroken1.playsound ( "sound/environment/electric/elec_impact.wav", 15, .7, 31);
    	$sparksDoorBroken1.playsound ( "sound/ships/forever/for_tubelatch.wav", 16, .4, 31);
                
		$sparksDoorBroken1.show();
		$doorBroken1West.time( .2 );
		$doorBroken1West.moveWest( 2 );
		$doorBroken1West.rotateXup( 1 );
		$doorBroken1East.time( .2 );
		$doorBroken1East.moveEast( 2 );
		$doorBroken1East.rotateXdown( 1 );
		waitfor( $doorBroken1West );
		$sparksDoorBroken1.hide();
		wait( 2 );
    	$sparksDoorBroken1.playsound ( "sound/environment/electric/elec_impact.wav", 15, .7, 31);
    	$sparksDoorBroken1.playsound ( "sound/ships/forever/for_tubelatch.wav", 16, .4, 31);
		$sparksDoorBroken1.show();
		$doorBroken1West.time( .4 );
		$doorBroken1West.moveEast( 2 );
		$doorBroken1West.rotateXdown( 1 );
		$doorBroken1East.time( .4 );
		$doorBroken1East.moveWest( 2 );
		$doorBroken1East.rotateXup( 1 );
		waitfor( $doorBroken1West );
		$sparksDoorBroken1.hide();
		wait( 2 );	
	}
}

//---------------------
// doorBroken3Move
// Broken door bangs around 
//---------------------
void doorBroken3Move()
{
	while( 1 )
	{
    	$sparksDoorBroken3.playsound ( "sound/environment/electric/elec_impact.wav", 15, .7, 31);
    	$sparksDoorBroken3.playsound ( "sound/ships/forever/for_tubelatch.wav", 16, .4, 21);
		$sparksDoorBroken3.show();
		$doorBroken3West.time( .2 );
		$doorBroken3West.moveWest( 2 );
		$doorBroken3West.rotateXup( 1 );
		$doorBroken3East.time( .2 );
		$doorBroken3East.moveEast( 2 );
		$doorBroken3East.rotateXdown( 1 );
		waitfor( $doorBroken3East );
		$sparksDoorBroken3.hide();
		wait( 2 );
    	$sparksDoorBroken3.playsound ( "sound/environment/electric/elec_impact.wav", 15, .7, 31);
    	$sparksDoorBroken3.playsound ( "sound/ships/forever/for_tubelatch.wav", 16, .4, 21);
		$sparksDoorBroken3.show();
		$doorBroken3West.time( .4 );
		$doorBroken3West.moveEast( 2 );
		$doorBroken3West.rotateXdown( 1 );
		$doorBroken3East.time( .4 );
		$doorBroken3East.moveWest( 2 );
		$doorBroken3East.rotateXup( 1 );
		waitfor( $doorBroken3East );
		$sparksDoorBroken3.hide();
		wait( 2 );	
	}
}

//---------------------
// switchExposeSubsystem_Function
// opens the door and exposes the elevator unit
//---------------------
void switchExposeSubsystem_Function()
{
	//--- make the switch unusable
	$switchExposeSubsystem.nouse();
	$switchExposeSubsystem.playsound( "sound/ships/forever/for_doorbeep.wav", 3, .9, 10001);

	//$switchExposeSubsystem.archetype( "NonInteractive" );

	$TurboliftUnitButton.puzzleobject_activate();
	
	//--- set the times on the objects
	$switchSubsystem.time( 1.25 );
	$doorSubsystemNorth.time( .75 );
	$doorSubsystemSouth.time( .75 );
	
	//--- slide the door aside
	$doorSubsystemNorth.moveNorth( 24 );
	$doorSubsystemSouth.moveSouth( 24 );
	//$doorSubsystemNorth.playsound ( "sound/ships/forever/for_doorbeep.wav", 3, .9, 10001);
	$doorSubsystemSouth.playsound ( "sound/doors/enterprise_jefftube.wav", 2, .3, 10001);
	waitfor( $doorSubsystemNorth );
	
	wait( .5 );
	$switchSubsystem.playsound ( "sound/ships/forever/for_gravpanel.wav", 3, .9, 10001);	
	//--- slide the unit forward
	$switchSubsystem.moveeast( 36 );
	waitfor( $switchSubsystem );
	$switchSubsystem.playsound ( "sound/ships/forever/for_gravpanelstop.wav", 3, .9, 10001);	
	//--- make it so the unit can be used
	//thread globalCommon_OnUse ( $switchSubsystem , "Subsystem_ActivateFunction");
	
	// show the archetype object
	globalArchetype_Setup( $switchSubsystem_archetype, "TurboLiftControlGrid" );

	wait( .25 );

	$switchSubsystem_archetype.missionobjective( 1 );
}

void turboliftWaypoint()
{
	$turbolift_waypoint.missionobjective( 0 );
	globalCoop_level_remove($turbolift_waypoint);
}

//---------------------
// GravityUnitButton_Used
// button is used, turns on lights, unlocks door
//---------------------
void TurboliftUnit_Failed()
{
	$TurboliftUnitButton.puzzleobject_reset();
}


void TurboliftUnit_Canceled()
{
	$TurboliftUnitButton.puzzleobject_reset();
}

void TurboliftUnit_Used()
{
	if(cvar_bool_multiplayer){//do not do this in multiplayer
		return;
	}

	entity puzzle;
	puzzle = getcurrententity();
	
	globalTricorderRoute_Reset();
	globalTricorderRoute_SetSource1Row( 7 );
	globalTricorderRoute_SetDestination1( 5, 7 );
    globalTricorderRoute_SetDestination2( 9, 7 );

	//--- begin row definitions
 	globalTricorderRoute_BeginDef();
 	globalTricorderRoute_SetNextRow( 32, 32, 32, 32, 32, 32, 32, 32, 32 );
 	globalTricorderRoute_SetNextRow( 32, 32, 32, 32, 32, 32, 32, 32, 32 );
 	globalTricorderRoute_SetNextRow( 32, 32, 32, 32, 32, 32, 32, 32, 32 );
 	globalTricorderRoute_SetNextRow( 32, 32, 32, 32, 32, 32, 32, 32, 32 );
 	globalTricorderRoute_SetNextRow( 32, 32, 32, 32, 32, 11,  3, 19, 16 );				
 	globalTricorderRoute_SetNextRow( 32, 32,  9, 20, 32,  1,  6,  8, 32 );	
 	globalTricorderRoute_SetNextRow( 16, 24, 21,  3,  0,  7, 11,  0, 32 );	
 	globalTricorderRoute_SetNextRow( 32, 32, 32, 11, 32, 18,  1,  7, 32 );	
 	globalTricorderRoute_SetNextRow( 32, 32, 32, 32, 32, 32, 32,  3,  1 );				
 	globalTricorderRoute_SetNextRow( 32, 32, 32, 32, 32, 32, 32, 32, 32 );	
 	globalTricorderRoute_SetNextRow( 32, 32, 32, 32, 32, 32, 32, 32, 32 );	
    
 	//--- run the puzzle
 	globalTricorderRoute_Run( puzzle, 0 );
 }

//---------------------
// sequenceSubsystemOn
// Munro powers on turbo lifts	
//---------------------
void sequenceSubsystemOn()
{
	music( "success","normal" );

	globalCoop_level_remove($switchSubsystem_archetype);
	//globalArchetype_Setup( $switchSubsystem_archetype, "TurboLiftControlGridUsed" );
	//$switchSubsystem_archetype.archetype( "NonInteractive" );


//allow loading next mission on timeranout
	coop_vector_svnextmaporrebootDoloadnextmimapWaitingForTeam_y=1;
//allow loading next mission on timeranout
	
	globalCoop_level_remove($triggerDialogTurbolift);	
	
	//COMPUTER VOICE DIALOG FOR RESTORING TURBOLIFT SYSTEMS HERE
	globalCoop_dialog_play($Chell,"dallas01/comp_turbopow.mp3", 1, 11111, 0);
	
	$switchSubsystem.playsound ( "sound/ships/forever/for_poweron.wav", 4, 1, 10001);
	$switchSubsystem.loopsound ( "sound/ships/forever/for_rumble3.wav",.4, 401);
	$switchSubsystem.nouse();
	
	thread globalCoop_level_hideAndNotSolid($switchSubsystem);
	thread globalCoop_level_showAndSolid($switchSubsystem_online);
	
	wait( 3 );
	
	//Turbolifts are operational again.
	globalCoop_dialog_play($Chell,"dallas01/chell_turbon.mp3",1,11111,1);

	$turboliftSwitch.triggerable();
	$turboLift.unlock();
	$turboLift.wait( -1 );
	$turboLift.open( $world );
	wait(.1);
	$turboLift.open( $world );
	$exittrigger.triggerable();
	
	if(!cvar_bool_multiplayer){
		$player.setobjectivecomplete( "TurboliftPower", 1 );
		$switchSubsystem_archetype.missionobjective( 0 );
	}
	else{
		thread globalCoop_objectives_update("complete",3,1);
	}
	
	_turboliftsPowered = TRUE;
}

//-------------------------------------------------------------------------------------------------
void testLevelEnd()
{
	//--- this is for testing the cinematic without having to play the entire level everytime.
	//--- I. E. please leave this function in :)
	_turboliftsPowered = TRUE;
	
	thread enterTurbolift();
}


void enterTurbolift()
{
	if(cvar_bool_multiplayer != 1 || coop_playLiftSequence == 1){
		$turboliftSwitch.nottriggerable();
		$turboliftSwitch.playsound( "sound/environment/computer/lcars_yes.wav", 1, 1, 500 );
		
	// do a little cine of everybody in the turbolift...
		cam.load( "m3l1b_turbolift" );
		spawn( "Camera", "targetname", "turboliftcam" );	
		globalCoop_main_camFadeOut(.25);
		wait( .3 );
		globalCoop_cin_start();
		$Chell.ai_off();
		$Franklin.ai_off();
		$Jurot.ai_off();

		$Chell.resetHead();
		$Franklin.resetHead();
		$Jurot.resetHead();
		$Munro.resetHead();
		
		$Chell.notsolid();
		$Franklin.notsolid();
		$Jurot.notsolid();
		$Munro.notsolid();

		$Munro.anim( "idle" );
		$Munro.show();

		$Munro.warp( $turboliftMunro.getorigin() );
		$Munro.angle( 225 );	

		$Chell.warp( $turboliftChell.getorigin() );
		$Chell.angle( 225 );

		$Franklin.warp( $turboliftFranklin.getorigin() );
		$Franklin.angle( 225 );

		$Jurot.warp( $turboliftJurot.getorigin() );
		$Jurot.angle( 225 );

		globalCoop_actor_aiOff($Chell);
		globalCoop_actor_aiOff($Franklin);
		globalCoop_actor_aiOff($Jurot);
		globalCoop_actor_aiOff($Munro);
			
		//--- get the player out of the way
		if(!cvar_bool_multiplayer){
			$player.origin( $Munro.getOrigin() );
		}
			
		//--- make sure the door's open
		$turboLift.open( $world );
		wait( .2 );
		globalCoop_cin_follow($turboliftcam,$m3l1b_turbolift);
		globalCoop_cin_fovMultiplayer($turboliftcam,65);
		globalCoop_cin_cut($turboliftcam);
		globalCoop_cin_cuecamera( $turboliftcam );
		globalCoop_main_camFadeIn(.2);
		wait( 2 );
		$turboLift.close();
		wait( 1 );
		globalCoop_main_camFadeOut(1);
		if(!cvar_bool_multiplayer){
			spawn ( "trigger_changelevel", "targetname", "trigger_endlevel", "map", "m3l2-forever" );
			wait( .2 );
			globalCoop_level_triggerEntity($trigger_endlevel);
		}
	}
//If Multiplayer Player
	else{
		thread coop_waitForTeam();
	}
}

//-----------------------------------
void inaccessible_sound()
{
	entity e;
	e = getcurrententity();
	e.playsound( "sound/doors/door_locked.wav", 1, .7, 90 );
}

//-----------------------------------
void panelEngineering_go()
{
	entity puzzle;
	puzzle = getcurrententity();
	
    globalTricorderRoute_Reset();
	globalTricorderRoute_SetSource1Row( 5 );
	globalTricorderRoute_SetSource2Row( 9 );
	globalTricorderRoute_SetDestination1( 5, 5 );
	globalTricorderRoute_SetDestination2( 9, 9 );

	//--- begin row definitions
    globalTricorderRoute_BeginDef();
    globalTricorderRoute_SetNextRow( 32,  1, 32, 32, 32, 32, 32, 32, 32 );
    globalTricorderRoute_SetNextRow( 32,  1, 32, 32, 32, 32, 32, 32, 32 );
    globalTricorderRoute_SetNextRow( 32,  1, 32, 32, 32, 32, 32, 32, 32 );
    globalTricorderRoute_SetNextRow( 32,  1, 32, 11, 32, 32, 32, 11, 32 );
    globalTricorderRoute_SetNextRow(  9, 10,  0,  6,  0,  0,  0,  6,  0 );				
    globalTricorderRoute_SetNextRow( 32,  1, 32, 17, 32, 32, 32,  1, 32 );	
    globalTricorderRoute_SetNextRow( 32,  1, 32,  9, 16, 11,  0, 23, 32 );	
    globalTricorderRoute_SetNextRow( 32,  1, 32,  1, 32, 32, 32,  1, 32 );	
    globalTricorderRoute_SetNextRow(  1, 10,  0,  8,  0,  0,  0,  8,  0 );				
    globalTricorderRoute_SetNextRow( 32,  1, 32, 11, 32, 32, 32, 11, 32 );	
    globalTricorderRoute_SetNextRow( 32,  1, 32, 32, 32, 32, 32, 32, 32 );	
    
    //--- run the puzzle
    globalTricorderRoute_Run( puzzle, 0 );
}

//-----------------------------------
void panelEngineering_failed()
{
	entity puzzle;
	puzzle = getcurrententity();
	puzzle.puzzleobject_reset();
	chellLetUsIn();
}

//-----------------------------------
void panelEngineering_canceled()
{
	entity puzzle;
	puzzle = getcurrententity();
	puzzle.puzzleobject_reset();
	chellLetUsIn();
}

//-----------------------------------
void secretDoorExplode()
{
	music( "mystery","normal" );
	$secretDoorSpawn.modelname( "fx/fx-explosion-metaldebris-directional.tik" );
	globalCoop_level_triggerEntity($secretDoorSpawn);
	globalCoop_level_remove($secretDoor);
	globalCoop_level_remove($secretDoorFracture);
	globalCoop_level_remove($secret_doorRemove);
}

void coop_waitForTeam()
//------------------------------------------------------------------------------
//waitForPlayers beeing close, end then the Level
//------------------------------------------------------------------------------
{
	if($world.getFloatVar("coop_waitForTeam") == 1){
		return;
	}
	$world.setFloatVar("coop_waitForTeam",1);
	
	globalCoop_main_waitForTeam('1157 4490 85','-200 -1000 -200','200 600 300');
	coop_playLiftSequence=1;
	enterTurbolift();
	coop_endLevel();
}


void coop_endLevel()
//------------------------------------------------------------------------------
//Level end
//------------------------------------------------------------------------------
{
	thread globalCoop_mission_completed("m3l2-forever");
}

